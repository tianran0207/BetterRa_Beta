//===== rAthena Script =======================================
//= Hall of Abyss - Core Scripts
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Expansion for War of Emperium FE.
//= Invest in each realm to unlock new maps:
//= - Guild Dungeon F2, total 20,000,000z.
//= - Hall of Abyss, total 70,000,000z.
//===== Additional Comments: =================================
//= 1.0 First version. Missing official times. [Euphy]
//= 1.1 Added GM management function. [Euphy]
//============================================================

// Global Variables
//============================================================
// Note: an asterisk (*) represents any realm (prt, pay, gef, alde).
/*
$2011_agit_invest
-- 0: Inactive.
-- 1: Investment in progress.
-- 2: Investment results calculating.
-- 4: Investment calculations complete (only when at least one $*_invest_result is 3).
-- 11-21: Tie, revote required.

$agit_result_notice
-- 0: Inactive.
-- 1-12: Interim statements (displaying $@vfund_*x) available.

$agit_result_timer
-- 0: Inactive.
-- 0-59: Time elapsed during investment (in hours).

$agit_revoting_timer
-- 0: Inactive.
-- 0-29: Time elapsed during revote (in minutes).
-- 30: Revote closed.
-- 31: Revote results calculating.

$*_invest_result
-- 0: No data.
-- 1: Realm not connected.
-- 2: Guild Dungeon F2 open.
-- 3: Abyss Gate (Hall of Abyss and Gulid Dungeon F2) open.

$fund_*[01-05]
-- Investment units per guild castle.
$fund_*_extra
-- Investment units per realm.

$@vfund_*[01-05]
$@vfund_*_extra
-- Investment units, used in interim statements only.
*/

// Investment Functions & Controller
//============================================================
// Note: The times in this section are almost entirely custom.

-	script	#invest_timer	-1,{
OnClock0000:	// Open investments on Wednesday (1 hour after WoE)
	if (gettime(DT_DAYOFWEEK) == WEDNESDAY && !agitcheck()) {
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
	}
	end;
OnClock1200:	// Close investments on Friday (60 hours after investments open)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck()) {
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
	}
	end;
OnClock1235:	// Open dungeons on Friday (at least 31 minutes after investments close)
	if (gettime(DT_DAYOFWEEK) == FRIDAY && !agitcheck())
		donpcevent "#fund_master::OnResult";
	end;
OnClock2000:	// Close dungeons on Tuesday (1 hour before WoE)
	if (gettime(DT_DAYOFWEEK) == TUESDAY)
		donpcevent "#fund_master::OnReset";
	end;
}

function	script	F_Invest_Status	{
	set .@day, gettime(DT_DAYOFWEEK);
	set .@hour, gettime(DT_HOUR);
	set .@minute, gettime(DT_MINUTE);

	// Inactive.
	if (agitcheck())
		return 0;

	// Open for investments.
	if (.@day >= WEDNESDAY && (.@day < FRIDAY || (.@day == FRIDAY && .@hour <= 12)))
		return 1;

	// Investments closed, calculating results.
	if (.@day == FRIDAY && .@hour == 12 && .@minute < 35)
		return 2;

	// Calculations complete, dungeons open.
	if ((.@day == FRIDAY && (.@hour > 12 || (.@hour == 12 && .@minute >= 35))) || .@day > FRIDAY ||
	    .@day < TUESDAY || (.@day == TUESDAY && .@hour < 20))
		return 3;

	// Dungeons closed.
	return 0;
}

//callfunc "F_Invest_Abyss","<variable>","<NPC name>"{,"<addon message>"};
function	script	F_Invest_Abyss	{
	.@npc_name$ = getarg(1);
	mes "有2种方式投資, 使用 Zeny 或者 由本商會投資部門發行的 貓手頂級投資証書.";
	mes "請問您想選擇哪种方式?";
	next;
	switch(select("- 用Zeny投資.:- 貓手頂級投資証書.:- 取消")) {
	case 1:
		mes .@npc_name$;
		mes "您決定使用Zeny來投資.";
		mes "最小投資單位是 ^4d4dff10,000 Zeny^000000.";
		mes "最大的投資單位是50倍 ^4d4dff500,000 Zeny^000000.";
		next;
		switch(select("- 1倍~100,000 Zeny:- 10倍 100,000 Zeny:- 50倍 500,000 Zeny:- 輸入金額:- 取消")) {
		case 1:
			set .@units,1;
			break;
		case 2:
			set .@units,10;
			break;
		case 3:
			set .@units,50;
			break;
		case 4:
			mes .@npc_name$;
			mes "請輸入您需要投資的金額(倍數).";
			mes "請選擇 1 ~ 200 之間的數字.";
			mes "或輸入 0 退出.";
			next;
			input .@units;
			if (.@units < 0 || .@units > 200) {
				mes .@npc_name$;
				mes "您輸入的數字有誤.";
				mes "請重試.";
				close;
			} else if (.@units == 0) {
				mes .@npc_name$;
				mes "已取消.";
				close;
			} else
				break;
		case 5:
			mes .@npc_name$;
			mes "Canceled.";
			close;
		}
		set .@zeny, .@units * 10000;
		if (Zeny < .@zeny) {
			mes .@npc_name$;
			mes "您的 Zeny 不夠.";
			close;
		}
		mes .@npc_name$;
		mes "您投資了 "+callfunc("F_InsertPlural",.@units,"單位")+" "+callfunc("F_InsertComma",.@zeny)+" Zeny"+getarg(2,"")+".";
		mes "雖然不多, 但這是一個投資感謝券. 您可以用它到綿綿島兌換一些有用的東西.";
		set Zeny, Zeny-.@zeny;
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 2:
		mes .@npc_name$;
		mes "哇哦, 您帶來了貓手頂級投資証書.";
		mes "您想投資多少份?";
		next;
		switch(select("- 1份 貓手頂級投資証書:- 5份 貓手頂級投資証書:- 輸入數字:- 取消")) {
		case 1:
			set .@tickets,1;
			break;
		case 2:
			set .@tickets,5;
			break;
		case 3:
			mes .@npc_name$;
			mes "請輸入您需要使用貓手頂級投資証書的具体份數.";
			mes "請選擇 1 ~ 20 之間的數字.";
			mes "或按 0 退出.";
			next;
			input .@tickets;
			if (.@tickets < 0 || .@tickets > 20) {
				mes .@npc_name$;
				mes "您輸入的數字有誤.";
				mes "請重新輸入.";
				close;
			} else if (.@tickets == 0) {
				mes .@npc_name$;
				mes "已取消.";
				close;
			} else
				break;
		case 4:
			mes .@npc_name$;
			mes "已取消.";
			close;
		}
		if (countitem(6489) < .@tickets) {
			mes .@npc_name$;
			mes "您想提交的証書數量比您攜帶的要多.";
			close;
		}
		set .@units, .@tickets * 10;
		mes .@npc_name$;
		mes .@tickets+" 貓手頂級投資証書"+((.@tickets == 1)?" 确認":"是")+" 相同想過的投資方式 "+.@units+" 倍數 ("+((.@units < 100)?.@units:insertchar(""+.@units,",",1))+"0,000z)"+getarg(2,"")+".";
		mes "雖然你的投資金額不高, 但這是一個投資感謝券. 您可以用它到綿綿島兌換一些有用的東西.";
		delitem 6489,.@tickets; //Cats_Invest_Certif
		setd getarg(0), getd(getarg(0)) + .@units;
		getitem 6488,.@units; //Thanks_Invest_Ticket
		close;
	case 3:
		mes .@npc_name$;
		mes "已取消.";
		close;
	}
}

// Script Core :: agit_fund
//============================================================
prt_gld,1,1,0	script	#fund_master	844,{
	end;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		set $2011_agit_invest,1;
		end;
	}
	if ((.@invest_status == 2 && $2011_agit_invest == 1) ||
	    (.@invest_status == 3 && ($2011_agit_invest == 1 || $2011_agit_invest == 2))) {
		donpcevent "#fund_master::OnCalculate";
		end;
	}
	end;
OnCalculate_Election:
	set .@force_vote,1;
OnCalculate:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		// All funds per realm are summed.
		set .@funds[.@i], getd("$fund_"+.@cas$[.@i]+"01")+getd("$fund_"+.@cas$[.@i]+"02")+getd("$fund_"+.@cas$[.@i]+"03")+getd("$fund_"+.@cas$[.@i]+"04")+getd("$fund_"+.@cas$[.@i]+"05")+getd("$fund_"+.@cas$[.@i]+"_extra");

		// 70,000,000z opens Hall of Abyss and F2.
		if (.@funds[.@i] >= 7000) {
			setd "$"+.@cas$[.@i]+"_invest_result",3;
			set .@total_7000,1;
		}
		// 20,000,000z opens F2 and a chance for Hall of Abyss.
		else if (.@funds[.@i] >= 2000) {
			setd "$"+.@cas$[.@i]+"_invest_result",2;
			set .@total_2000,1;
		}
		// Anything below that opens nothing.
		else
			setd "$"+.@cas$[.@i]+"_invest_result",1;
	}
	// No further actions if 20,000,000z isn't reached.
	if (!.@total_2000)
		end;
	// No further actions if any realm hits 70,000,000z.
	if (.@total_7000) {
		set $2011_agit_invest,4;
		end;
	}
	// Otherwise, highest investment above 20,000,000z opens Hall of Abyss.
	set .@max, .@funds[0];
	for(set .@i,1; .@i<4; set .@i,.@i+1) {
		if (.@funds[.@i] > .@max) {
			set .@max, .@funds[.@i];
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds[.@i] == .@max && .@funds[.@i] >= 2000) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (.@max < 2000)
		end;
	if (getarraysize(.@rand$)) {

		// Tie: declare a random winner.
		if (!.@force_vote) {
			set .@i, rand(getarraysize(.@rand$));
			setd "$"+.@rand$[.@i]+"_invest_result",3;
			set $2011_agit_invest,4;
			end;
		}

		// Tie: initiate a 30-minute election.
		set .@str$, implode(.@rand$,"|");
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (compare(.@str$,.@cas$[.@i]))
				set .@election, .@election | (1<<.@i);
		}
		switch(.@election) {
			case 3:  set $2011_agit_invest,11; break; //Prontera|Payon
			case 9:  set $2011_agit_invest,12; break; //Prontera|Al De Baran
			case 5:  set $2011_agit_invest,13; break; //Prontera|Geffen
			case 10: set $2011_agit_invest,14; break; //Al De Baran|Payon
			case 6:  set $2011_agit_invest,15; break; //Geffen|Payon
			case 12: set $2011_agit_invest,16; break; //Al De Baran|Geffen
			case 11: set $2011_agit_invest,17; break; //Prontera|Payon|Al De Baran
			case 7:  set $2011_agit_invest,18; break; //Prontera|Payon|Geffen
			case 13: set $2011_agit_invest,19; break; //Prontera|Al De Baran|Geffen
			case 14: set $2011_agit_invest,20; break; //Al De Baran|Payon|Geffen
			case 15: set $2011_agit_invest,21; break; //Prontera|Payon|Al De Baran|Geffen
		}
		if ($2011_agit_invest >= 11)
			donpcevent "附加投票管理員#Invest1::OnStart";
		end;
	} else {
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
		set $2011_agit_invest,4;
		end;
	}
	end;
OnInvest_start:
	announce "從現在起, 我們計划通過投資方式來快速打通'深淵回廊'的通道. 通過回廊我們將連通公會戰區域: 普隆德拉-戰士之路, 斐揚-遠古之風, 吉芬-亡者之丘, 艾-英雄之淚.",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt01,0;
	set $fund_prt02,0;
	set $fund_prt03,0;
	set $fund_prt04,0;
	set $fund_prt05,0;
	set $fund_pay01,0;
	set $fund_pay02,0;
	set $fund_pay03,0;
	set $fund_pay04,0;
	set $fund_pay05,0;
	set $fund_gef01,0;
	set $fund_gef02,0;
	set $fund_gef03,0;
	set $fund_gef04,0;
	set $fund_gef05,0;
	set $fund_alde01,0;
	set $fund_alde02,0;
	set $fund_alde03,0;
	set $fund_alde04,0;
	set $fund_alde05,0;
	donpcevent "Investment_total#fund00::OnEnable";
	end;
OnInvest_stop:
	announce "我們完成了整個投資項目, 成功的連接上了'深淵回廊'.",bc_all,0xFF0000; //FW_NORMAL 10
	donpcevent "Investment_total#fund00::OnReset";
	donpcevent "#fund_master::OnCalculate_Election";
	end;
OnResult:
	announce "我們已經得到了投資的回報 '深淵回廊'. 您可以通過公會戰區域的貓手董事會公告牌進行投資狀態查詢.",bc_all,0xFF0000; //FW_NORMAL 10
	if ($2011_agit_invest == 4) {
		set $fund_prt01,0;
		set $fund_prt02,0;
		set $fund_prt03,0;
		set $fund_prt04,0;
		set $fund_prt05,0;
		set $fund_prt_extra,0;
		set $fund_pay01,0;
		set $fund_pay02,0;
		set $fund_pay03,0;
		set $fund_pay04,0;
		set $fund_pay05,0;
		set $fund_pay_extra,0;
		set $fund_gef01,0;
		set $fund_gef02,0;
		set $fund_gef03,0;
		set $fund_gef04,0;
		set $fund_gef05,0;
		set $fund_gef_extra,0;
		set $fund_alde01,0;
		set $fund_alde02,0;
		set $fund_alde03,0;
		set $fund_alde04,0;
		set $fund_alde05,0;
		set $fund_alde_extra,0;
	} else if ($2011_agit_invest == 2)
		donpcevent "#fund_master::OnCalculate";
	end;
OnReset:
	set $2011_agit_invest,0;
	donpcevent "Investment_total#fund00::OnReset";
	end;
}

prt_gld,2,2,0	script	Investment_total#fund00	844,{
	callfunc "F_GM_NPC";
	mes "這個不能正常工作?";
	next;
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		mes "如果您認為投資計時器已經當机的話, 您可以進行調試.";
		next;
		switch(select("- 重啟計時器:- 取消")) {
		case 1:
			donpcevent "Investment_total#fund00::OnTimerReset";
			mes "您已啟動計時器.";
			close;
		case 2:
			mes "已取消.";
			close;
		}
	} else {
		mes "現在不需要調試.";
		close;
	}

OnInit:
	if (callfunc("F_Invest_Status") == 1) {
		initnpctimer;
		donpcevent "Investment_total#fund00::OnVarInit";
	} else {
		set $agit_result_timer,0;
		set $agit_result_notice,0;
	}
	end;
OnVarInit:
	setarray .@cas$[0],"prt","pay","gef","alde";
	for(set .@i,0; .@i<4; set .@i,.@i+1) {
		setd "$@vfund_"+.@cas$[.@i]+"01", getd("$fund_"+.@cas$[.@i]+"01");
		setd "$@vfund_"+.@cas$[.@i]+"02", getd("$fund_"+.@cas$[.@i]+"02");
		setd "$@vfund_"+.@cas$[.@i]+"03", getd("$fund_"+.@cas$[.@i]+"03");
		setd "$@vfund_"+.@cas$[.@i]+"04", getd("$fund_"+.@cas$[.@i]+"04");
		setd "$@vfund_"+.@cas$[.@i]+"05", getd("$fund_"+.@cas$[.@i]+"05");
		setd "$@vfund_"+.@cas$[.@i]+"_extra", getd("$fund_"+.@cas$[.@i]+"_extra");
	}
	end;
OnEnable:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	initnpctimer;
	end;
OnReset:
	set $agit_result_timer,0;
	set $agit_result_notice,0;
	stopnpctimer;
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer3600000:
	if (callfunc("F_Invest_Status") == 1) {
		if ($agit_result_timer < 59) {
			set $agit_result_timer, $agit_result_timer+1;

			// Trigger on multiples of 5 up to 55, and 58.
			if ($agit_result_timer % 10 == 5 || $agit_result_timer % 10 == 0 || $agit_result_timer == 58) {
				set $agit_result_notice, $agit_result_notice+1;
				donpcevent "Investment_total#fund00::OnVarInit";
				announce "No."+$agit_result_notice+"號深淵之門傳送設備已近完成連接. 您可以通過公會區域的貓手董事會公告牌進行投資進度查詢.",bc_all,0xFF0000; //FW_NORMAL 10
			}

			donpcevent "Investment_total#fund00::OnTimerReset";
		} else
			stopnpctimer;
		end;
	} else {
		donpcevent "Investment_total#fund00::OnReset";
		end;
	}
}

prt_gld,164,98,3	script	附加投票管理員#Invest1	421,{
	mes "[附加投票管理員]";
	mes "我們不接受額外的投票.";
	mes "請等待我們公布結果.";
	close;
OnInit:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 2 && $2011_agit_invest >= 11 && $agit_revoting_timer < 30) {
		donpcevent "附加投票管理員#Invest2::OnEnable";
		initnpctimer;
	} else if (.@invest_status == 3 && $2011_agit_invest >= 11)
		donpcevent "附加投票管理員#Invest1::OnCalculate";
	donpcevent "附加投票管理員#Invest1::OnDisable";
	end;
OnCalculate:
	switch($2011_agit_invest) {
		case 11: setarray.@cas$[0],"prt","pay"; break;
		case 12: setarray.@cas$[0],"prt","alde"; break;
		case 13: setarray.@cas$[0],"prt","gef"; break;
		case 14: setarray.@cas$[0],"alde","pay"; break;
		case 15: setarray.@cas$[0],"gef","pay"; break;
		case 16: setarray.@cas$[0],"alde","gef"; break;
		case 17: setarray.@cas$[0],"prt","pay","alde"; break;
		case 18: setarray.@cas$[0],"prt","pay","gef"; break;
		case 19: setarray.@cas$[0],"prt","alde","gef"; break;
		case 20: setarray.@cas$[0],"alde","pay","gef"; break;
		case 21: setarray.@cas$[0],"prt","pay","alde","gef"; break;
		default: end;
	}
	set .@max, getd("$fund_"+.@cas$[0]+"_extra");
	for(set .@i,1; .@i<getarraysize(.@cas$); set .@i,.@i+1) {
		set .@funds, getd("$fund_"+.@cas$[.@i]+"_extra");
		if (.@funds > .@max) {
			set .@max, .@funds;
			set .@ele, .@i;
			deletearray .@rand$[0], getarraysize(.@rand$);
		} else if (.@funds == .@max) {
			if (!getarraysize(.@rand$))
				set .@rand$[0], .@cas$[.@ele];
			set .@rand$[getarraysize(.@rand$)], .@cas$[.@i];
		}
	}
	if (getarraysize(.@rand$)) {
		set .@i, rand(getarraysize(.@rand$));
		setd "$"+.@rand$[.@i]+"_invest_result",3;
	} else
		setd "$"+.@cas$[.@ele]+"_invest_result",3;
	set $2011_agit_invest,4;
	set $agit_revoting_timer,0;
	end;
OnEnable:
	enablenpc "附加投票管理員#Invest1";
	end;
OnDisable:
	disablenpc "附加投票管理員#Invest1";
	end;
OnStart:
	initnpctimer;
	donpcevent "附加投票管理員#Invest2::OnNotice_on";
	end;
OnEnd:
	stopnpctimer;
	disablenpc "附加投票管理員#Invest1";
	end;
OnTimerReset:
	initnpctimer;
	end;
OnTimer60000:
	set .@revote, $agit_revoting_timer;
	if (.@revote < 30) {
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "附加投票管理員#Invest1::OnTimerReset";
	} else if (.@revote == 30) {
		announce "貓手商會: 附加投票時間即將結束. 我們將盡快公布結果.",bc_all,0xFF0000; //FW_NORMAL 10
		donpcevent "附加投票管理員#Invest2::OnDisable";
		donpcevent "附加投票管理員#Invest1::OnEnable";
		set $agit_revoting_timer, $agit_revoting_timer+1;
		donpcevent "附加投票管理員#Invest1::OnTimerReset";
	} else if (.@revote == 31) {
		donpcevent "附加投票管理員#Invest1::OnCalculate";
		donpcevent "附加投票管理員#Invest1::OnDisable";
		stopnpctimer;
		end;
	} else {
		donpcevent "附加投票管理員#Invest1::OnDisable";
		stopnpctimer;
	}
	end;
}

prt_gld,164,98,3	script	附加投票管理員#Invest2	421,{
	set .@invest, $2011_agit_invest;
	if (.@invest < 11) {
		mes "[附加投票管理員]";
		mes "不! 還沒到公布結果的時候.";
		close2;
		disablenpc "附加投票管理員#Invest2";
		end;
	}
	set .@playtime, checkquest(7349,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[附加投票管理員]";
		mes "你已經參与了投票.";
		mes "請放松并等待結果.";
		close;
	} else if (.@playtime == 2)
		erasequest 7349;
	mes "[附加投票管理員]";
	mes "您想參与投票, 選擇在哪里与'深淵回廊'連接?";
	next;
	if(select("- 參与投票.:- 取消.") == 2) {
		mes "[附加投票管理員]";
		mes "您已取消投票.";
		close;
	}
	mes "[附加投票管理員]";
	mes "你只能投票一次, 不會有額外的選項供您選擇.";
	mes "請選擇一個公會戰區域.";
	next;

	// Create menu.
	explode(.@candidates$, callsub(OnGetCandidates, .@invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@menu$[.@i],"英雄之淚";
		else if (.@candidates$[.@i] == "gef")
			set .@menu$[.@i],"亡者之丘";
		else if (.@candidates$[.@i] == "pay")
			set .@menu$[.@i],"遠古之風";
		else if (.@candidates$[.@i] == "prt")
			set .@menu$[.@i],"戰士之路";
	}
	set .@vote, select(implode(.@menu$,":"))-1;

	mes "[附加投票管理員]";
	mes "您把票投給了 "+.@menu$[.@vote]+".";
	mes "感謝您的參与.";
	setquest 7349;
	setd "$fund_"+.@candidates$[.@vote]+"_extra", getd("$fund_"+.@candidates$[.@vote]+"_extra") + 1;
	close;
OnGetCandidates:
	switch(getarg(0)) {
		case 11: return "prt|pay";
		case 12: return "prt|alde";
		case 13: return "prt|gef";
		case 14: return "alde|pay";
		case 15: return "gef|pay";
		case 16: return "alde|gef";
		case 17: return "prt|pay|alde";
		case 18: return "prt|pay|gef";
		case 19: return "prt|alde|gef";
		case 20: return "alde|pay|gef";
		case 21: return "prt|pay|alde|gef";
	}
	return "";
OnInit:
	disablenpc "附加投票管理員#Invest2";
	end;
OnEnable:
	enablenpc "附加投票管理員#Invest2";
	end;
OnDisable:
	disablenpc "附加投票管理員#Invest2";
	stopnpctimer;
	end;
OnNotice_on:
	initnpctimer;
	end;
OnTimer5000:
	announce "貓手商會: 沒有區域達到連接'深淵回廊'的基礎投票要求...",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer8000:
	announce "貓手商會: 我們將在投資最多的區域連接'深淵回廊'.",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer11000:
	announce "貓手商會: 然而, 可能會碰到不同區域的投資金額相同. 因此, 我們將會采取投票方式.",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer14000:
	announce "貓手商會: 需要進行額外30分鐘的附加投票. 您可以投票給戰士之路.",bc_all,0xFF0000; //FW_NORMAL 10
	end;
OnTimer20000:
	explode(.@candidates$, callsub(OnGetCandidates, $2011_agit_invest),"|");
	for(set .@i,0; .@i<getarraysize(.@candidates$); set .@i,.@i+1) {
		if (.@candidates$[.@i] == "alde")
			set .@choices$[.@i],"英雄之淚";
		else if (.@candidates$[.@i] == "gef")
			set .@choices$[.@i],"亡者之丘";
		else if (.@candidates$[.@i] == "pay")
			set .@choices$[.@i],"遠古之風";
		else if (.@candidates$[.@i] == "prt")
			set .@choices$[.@i],"戰士之路";
	}
	announce "貓手商會: 附加投票的胜出者為... "+implode(.@choices$," vs. ")+".",bc_all,0xFF0000; //FW_NORMAL 10
	set $fund_prt_extra,0;
	set $fund_pay_extra,0;
	set $fund_gef_extra,0;
	set $fund_alde_extra,0;
	set $agit_revoting_timer,0;
	donpcevent "附加投票管理員#Invest2::OnEnable";
	stopnpctimer;
	end;
}

/*
prtg_cas01,2,2,0	script	阿伯萊	55,{
	mes "我應該辭去我的管家工作.";
	close;
OnAgitInvest:
	switch(callfunc("F_Invest_Status")) {
	case 0:
		set $2011_agit_invest,0;
		donpcevent "#fund_master::OnReset";
		break;
	case 1:
		set $2011_agit_invest,1;
		donpcevent "#fund_master::OnInvest_start";
		break;
	case 2:
		set $2011_agit_invest,2;
		donpcevent "#fund_master::OnInvest_stop";
		break;
	case 3:
		donpcevent "#fund_master::OnResult";
		break;
	}
	end;
}
*/

// Realms - Merchant Guilds, Gate Managers, Status Boards
//============================================================
-	script	Cat Paw Merchant Guild	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"格魯茲";
		set .@realm$,"英雄之淚";
		set .@intro$,"艾-英雄之淚管理員";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"蒙爾克";
		set .@realm$,"亡者之丘";
		set .@intro$,"吉芬-亡者之丘管理員";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"菲克達";
		set .@realm$,"遠古之風";
		set .@intro$,"斐揚-遠古之風管理員";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"杜巴依";
		set .@realm$,"戰士之路";
		set .@intro$,"普隆德拉-戰士之路管理員";
	}
	set .@n$,"["+.@name$+"]";

	mes .@n$;
	mes "我是 "+.@name$+", 貓手商會派駐 "+.@intro$+".";
	mes "我能提供關于深淵回廊開發進展的情報.";
	mes "需要幫忙嗎?";
	next;
	switch(select("- 什么是深淵回廊?:- 投資開發深淵回廊?:- 我想進入這個區域.:- 取消")) {
	case 1:
		mes .@n$;
		mes "我們稱之為深淵回廊, 是因為他是^4d4dff一個通道, 用以連接另外一個位面空間^000000.";
		next;
		mes .@n$;
		mes "我們發現在夢羅克出現了無數裂縫, 然后同樣的裂縫開始在這里陸續出現.";
		mes "我個人認為深淵之門很大可能是過去或者未來的一個破碎空間.";
		next;
		mes .@n$;
		mes "意思就是說深淵之門強行連通了我們与另一個空間.";
		mes "我想這里面有可能是因為華麗金屬的強大能量, 而造成了深淵之門的活躍變化.";
		next;
		mes .@n$;
		mes "迄今為止總共發現了4條深淵回廊. 他們不約而同的連接到散發著華麗金屬气息得地方.";
		next;
		mes .@n$;
		mes "我們對4個不同位面的區域進行了命名.";
		mes "^4d4dff戰士之路, 亡者之丘, 遠古之風, 英雄之淚.^000000";
		mes "所有這些位面我們一起統稱為 ^4d4dff深淵回廊^000000.";
		next;
		mes .@n$;
		mes "這些縫隙可能連接著過去和未來, 也有可能連著一些未知的世界.";
		mes "深淵之門所包含的力量是強大卻又可怕的.";
		next;
		mes .@n$;
		mes "您也有興趣探索深淵之門?";
		mes "哈哈. 探索之前您還有些准備工作需要做.";
		next;
		mes .@n$;
		mes "如果您感興趣的話,";
		mes "先讓我們聊聊投資的事情吧?";
		close;
	case 2:
		mes .@n$;
		mes "您是不是很好奇為什么要投資開發深淵之門?";
		mes "您想了解些什么?";
		next;
		switch(select("- 關于深淵之門?:- 關于投資?:- 怎么投資?:- 投資有何回報?")) {
		case 1:
			mes .@n$;
			mes "之前我們已經命名了深淵回廊.";
			mes "深淵之門是貫穿我們的世界与另一空間的出入口.";
			next;
			mes .@n$;
			mes "深淵回廊所建立的通道充滿無數的三維裂縫, 非常的不穩定.";
			mes "因此通過深淵之門旅行需要大量的人力來進行維護, 才能夠保持通道的穩定.";
			next;
			mes .@n$;
			mes "人類擁有先進的科學技術.";
			mes "但是對于我們貓手商會來說, 我們握有更加尖端的核心技術, 對于穩定通道來說, 這非常必要.";
			next;
			mes .@n$;
			mes "因此, 我們提出了一個方案.";
			mes "以我們的尖端技術, 并且動用貓手商會所有的能量來維持住通道. 這將花費商會的巨大人力物力.";
			mes "我們商會立誓不惜代价維系通道穩定.";
			next;
			mes .@n$;
			mes "當然這可不是一個簡單的工作, 不僅耗資巨大而且還隨時面臨生命危險.";
			mes "已經發生了數起失蹤事件了.";
			next;
			mes .@n$;
			mes "我們按以往的經驗判斷, 隧道的穩定期在4天左右.";
			mes "你的投資將會被用到深淵之門的開發上, 這樣也是對你自己的生命安全是一种保障.";
			next;
			mes .@n$;
			mes "感覺如何, 是不是覺得投資是一种雙贏的模式?";
			close;
		case 2:
			mes .@n$;
			mes "通過強行撕裂空間建立隧道, 這是非常危險的.";
			mes "我們撇開巨額投入不說.";
			next;
			mes .@n$;
			mes "你不會為了錢, 命都不要了吧.";
			mes "由我們建立的傳送通道, 几乎沒有任何風險.";
			next;
			mes .@n$;
			mes "但是, 維持深淵之門的支出是非?植賴?";
			mes "不過如果你能夠找到更多的投資者來分攤稀釋成本的話. 就沒什么壓力了.";
			next;
			mes .@n$;
			mes "這里有個基礎的連接裝置.";
			mes "至于是否能夠進行連接改造工作, 就需要看投資者的力度了.";
			next;
			mes .@n$;
			mes "深淵回廊覆蓋著整個公會戰區.";
			mes "可能你無法想象, 我們整個貓手商會停止所有的業務, 將所有的人力物力投入到回廊的工作上來?";
			next;
			mes .@n$;
			mes "這也就是各大區的負責人拼命拉投資的原因了.";
			mes "如果能夠獲得足夠的投資, 將本區的深淵之門穩定好, 這對本區所有人都是非常有利的.";
			next;
			mes .@n$;
			mes "如果 ^4d4dff能夠与一個怪物聚集的地下城相連的話";
			mes "就會開放該區域的使用權給所有人, 而非公會專屬區域^000000 這將是非常有吸引力的投資.";
			next;
			mes .@n$;
			mes "一起的前提都是基于回廊開啟, 而開啟就需要投資, 這是個因果關系.";
			close;
		case 3:
			mes .@n$;
			mes "這很簡單.";
			mes "訪問分區經理 ^4d4dff告訴他你想在他的區域那里投資^000000 然后交錢完成投資.";
			next;
			mes .@n$;
			mes "我們一般收取 ^4d4dff的標准貨幣是 Zeny^000000.";
			mes "不過作為您對我們貓手商會所提供的支持, 我們會給您一張投資感謝票, 可以用來兌換綿綿島的一些物品.";
			next;
			mes .@n$;
			mes "至于物品兌換的話, 你可以到綿綿島找納米斯, 他是我們在那里的分管經理.";
			mes "記得帶回來的貓手頂級投資証書, 可以繼續用來投資.";
			next;
			mes .@n$;
			mes "最后你必須考慮好你想投資的區域.";
			close;
		case 4:
			mes .@n$;
			mes "深淵回廊覆蓋了几乎所有的公會戰區域;";
			mes "因此, 經過我們核算一次最基礎的投資額度可以讓我們勉強保本.";
			next;
			mes .@n$;
			mes "整個工程預算部分,做了保底金額和最大金額.";
			mes "如果總的投資額度 ^4d4dff低于保底預算金額^000000的話, 我們就無法完成与 ^4d4dff回廊后的地下城^000000對接.";
			next;
			mes .@n$;
			mes "如果收到的投資款達到最大金額, 該區域將被連接到";
			mes "^4d4dff深淵回廊^000000 并且無須在關注投資等級, 即可連接到任意一個地方, 當然包括回廊的最深處.";
			next;
			mes .@n$;
			mes "^ff0000如果沒有達到最大的投資限額的話, 那對于該地區來說";
			mes "只有投資最多的人才可以被傳送到回廊的任意一個地方.^000000";
			next;
			mes .@n$;
			mes "深淵之門開啟后, 目前最深的(2F)地下城";
			mes "將開放給所有在這一地區投資過的冒險者們.";
			next;
			mes .@n$;
			mes "進入地下城會被計算一次投資, 并被計入到下一次的總投資內.";
			mes "這無論對于個人或者公會來說都是一次不錯的小獎勵.";
			next;
			mes .@n$;
			mes "与深淵回廊建立連接是非常困難的, 約為每周一次. 因此我們會盡量延長連接的時間. ";
			mes "為了應對未知的力量, 請保持持續投資.";
			close;
		}
	case 3:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "- 您攜帶的物品數量過多 -";
			mes "- 請處理后再來找我. -";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes .@n$;
			mes "您所持的投資感謝票數量已經到了极限.";
			mes "無法再發給您給多的感謝票了. 請先兌換物品后再來.";
			mes "投資感謝票可以在綿綿島兌換物品.";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes .@n$;
			mes "我們暫不接受投資.";
			mes "請在投資期內再來.";
			close;
		}
		set .@guild, getcharid(2);
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@castle_name$[.@i], getcastlename(.@npc$+"g_cas0"+.@i);
			set .@owner_id[.@i], getcastledata(.@npc$+"g_cas0"+.@i,CD_GUILD_ID);
			set .@owner_name$[.@i], getguildname(.@owner_id[.@i]);
			if (.@guild == .@owner_id[.@i]) {
				set .@menu$, .@menu$+.@castle_name$[.@i]+" ["+.@owner_name$[.@i]+"] 公會:";
				set .@castles_owned, .@castles_owned | (1<<.@i);
			} else
				set .@menu$, .@menu$+"^aaaaaa不像是公會 "+.@castle_name$[.@i]+"^000000:";
		}
		mes .@n$;
		mes "您想投資在 ^4d4dff"+.@realm$+"^000000 區域?";
		mes "如果你的公會占有公會城堡的話, 你的投資也會對所占城堡生效.";
		next;
		if (.@castles_owned) {
			set .@i, select(.@menu$+"Cancel");
			if (.@i == 6) {
				mes .@n$;
				mes "- 已取消.";
				close;
			}
			if (.@guild != .@owner_id[.@i]) {
				mes .@n$;
				mes "公會所有權 "+.@castle_name$[.@i]+" 是 "+.@owner_name$[.@i]+".";
				mes "這看上去不是你的公會.";
				close;
			}
			mes .@n$;
			mes "投資于 "+.@castle_name$[.@i]+"的"+.@owner_name$[.@i]+" 公會.";
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@i,.@n$;
			end;
		} else {
			mes .@n$;
			callfunc "F_Invest_Abyss","$fund_"+.@npc$+"_extra",.@n$," 的 "+.@realm$;
			end;
		}
	case 4:
		mes .@n$;
		mes "是這樣的嗎?";
		mes "如果您有興趣投資深淵回廊的話, 請記得來找我.";
		close;
	}
}

-	script	Gate Manager#realm	-1,{

	if (compare(strnpcinfo(4),"alde")) {
		set .@npc$,"alde";
		set .@name$,"奧孔";
		set .@gate$,"英雄之淚";
		set .@realm$,"艾爾帕蘭";
	} else if (compare(strnpcinfo(4),"gef")) {
		set .@npc$,"gef";
		set .@name$,"米扎爾";
		set .@gate$,"亡者之丘";
		set .@realm$,"吉芬";
	} else if (compare(strnpcinfo(4),"pay")) {
		set .@npc$,"pay";
		set .@name$,"阿爾卡伊德";
		set .@gate$,"遠古之風";
		set .@realm$,"斐揚";
	} else if (compare(strnpcinfo(4),"prt")) {
		set .@npc$,"prt";
		set .@name$,"阿里奧斯";
		set .@gate$,"戰士之路";
		set .@realm$,"普隆德拉";
	}
	set .@n$,"["+.@name$+"]";

	if (callfunc("F_Invest_Status") != 3) {
		mes .@n$;
		mes "目前沒有任何通道与公會地下城連接.";
		close;
	}
	set .@status, getd("$"+.@npc$+"_invest_result");
	if (.@status == 3) {
		mes .@n$;
		mes "在您和各位投資者的積极投資下, 我們已經成功的連接上了'深淵回廊'的 ["+.@gate$+"] .";
		mes ""+.@realm$+"公會地下城將向各位開放, 各位僅需要支付很少的一些入場費即可.";
		next;
		mes .@n$;
		mes "您所經常訪問并繳納入場費的區域, 入場費也會被計算入該地區的投資收益.";
		mes "您想現在就進入公會地下城嗎?";
		next;
		set .@i, select(.@realm$+" 地下城 1F - 10000z:"+.@realm$+" 地下城 2F - 10000z:取消");
		if (.@i == 3) {
			mes .@n$;
			mes "已取消.";
			close;
		}
		if (Zeny < 10000) {
			mes .@n$;
			mes "我負責收取小額入場費.";
			mes "您所支付的入場費會計算到下一次的回廊投資中.";
			mes "入場費是 10000z.";
			close;
		}
		mes .@n$;
		mes "我將傳送你到 "+.@realm$+" 公會地下城 "+.@i+"F.";
		set Zeny, Zeny-10000;
		setd "$fund_"+.@npc$+"_extra", getd("$fund_"+.@npc$+"_extra") + 1;
		close2;
		if (compare(strnpcinfo(2),"alde"))
			warp "gld_dun02"+((.@i == 2)?"_2":""),32,122;
		else if (compare(strnpcinfo(2),"gef"))
			warp "gld_dun04"+((.@i == 2)?"_2":""),39,258;
		else if (compare(strnpcinfo(2),"pay"))
			warp "gld_dun01"+((.@i == 2)?"_2":""),186,165;
		else if (compare(strnpcinfo(2),"prt"))
			warp "gld_dun03"+((.@i == 2)?"_2":""),164,268;
		end;
	} else if (.@status == 2) {
		mes .@n$;
		mes ""+.@realm$+" 公會地下城 2F 現在雖然現在處于激活狀態, 但是還沒有連接上深淵回廊,";
		mes "現在僅能到達默認的公會地下城.";
		close;
	} else {
		mes .@n$;
		mes .@realm$+" 區域這次沒有被選中作為回廊的接入點.";
		mes "請等下一個投資期.";
		close;
	}
	end;
}

-	script	Investment Status Board	-1,{
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 1) {
		if (compare(strnpcinfo(4),"alde")) {
			set .@npc$,"alde";
			set .@realm$,"艾爾帕蘭";
		} else if (compare(strnpcinfo(4),"gef")) {
			set .@npc$,"gef";
			set .@realm$,"吉芬";
		} else if (compare(strnpcinfo(4),"pay")) {
			set .@npc$,"pay";
			set .@realm$,"斐揚";
		} else if (compare(strnpcinfo(4),"prt")) {
			set .@npc$,"prt";
			set .@realm$,"普隆德拉";
		}
		set .@notice, $agit_result_notice;
		if (.@notice < 1 || .@notice > 12) {
			mes "There are currently no investment sums available.";
			close;
		}
		mes callfunc("F_GetNumSuffix",.@notice)+" 合計 (金額單位: 10,000z)";
		for(set .@i,1; .@i<=5; set .@i,.@i+1) {
			set .@map$, .@npc$+"g_cas0"+.@i;
			set .@fund[.@i], getd("$@vfund_"+.@npc$+"0"+.@i);
			mes getcastlename(.@map$)+" "+getguildname(getcastledata(.@map$,CD_GUILD_ID))+" 公會: "+.@fund[.@i];
		}
		set .@fund[0], getd("$@vfund_"+.@npc$+"_extra");
		mes .@realm$+" 普通: "+.@fund[0];
		mes .@realm$+" 投資總計: "+(.@fund[0]+.@fund[1]+.@fund[2]+.@fund[3]+.@fund[4]+.@fund[5]);
		close;
	} else if (.@invest_status == 2) {
		mes "!- Notice -!";
		mes "目前總投資金額正在計算中, 暫時還未得出總是.";
		mes "請等待結果公布.";
		close;
	} else if (.@invest_status == 3) {
		setarray .@invest_result[0], $prt_invest_result,$pay_invest_result,$gef_invest_result,$alde_invest_result;
		setarray .@invest_region$[0],  "普隆德拉區域",  "斐揚區域",       "吉芬區域",            "艾區域";
		mes "!- 所有區域的投資結果 -!";
		for(set .@i,0; .@i<4; set .@i,.@i+1) {
			if (.@invest_result[.@i] == 1)
				mes "^aaaaaa["+.@invest_region$[.@i]+"] 未達到最低要求^000000";
			else if (.@invest_result[.@i] == 2) {
				mes "["+.@invest_region$[.@i]+"] 達到基本要求";
				mes "-> 公會地下城 (2F) 可用";
			} else if (.@invest_result[.@i] == 3) {
				mes "^4d4dff["+.@invest_region$[.@i]+"] 達到最高投資要求^000000";
				mes "-> 深淵回廊 可用";
			} else
				mes "沒有相關 ["+.@invest_region$[.@i]+"].的數据";
		}
		close;
	} else {
		mes "!- 公告 -!";
		mes "現在沒有任何投資信息.";
		close;
	}
}

// Inside Castles - Gate Managers
//============================================================
-	script	深淵之門管理員#castle	-1,{

	set .@id, atoi(charat(strnpcinfo(2),getstrlen(strnpcinfo(2))-1));
	if (compare(strnpcinfo(2),"alde")) {
		set .@npc$,"alde";
		set .@realm$,"艾而帕蘭";
	} else if (compare(strnpcinfo(2),"gef")) {
		set .@npc$,"gef";
		set .@realm$,"吉芬";
	} else if (compare(strnpcinfo(2),"pay")) {
		set .@npc$,"pay";
		set .@realm$,"斐揚";
	} else if (compare(strnpcinfo(2),"prt")) {
		set .@npc$,"prt";
		set .@realm$,"普隆德拉";
	}

	set .@GID, getcastledata(strnpcinfo(4),CD_GUILD_ID);
	if (getcharid(2) != .@GID) {
		mes "[深淵之門管理員]";
		mes "你似乎并不是擁有這座城堡的公會成員.";
		mes "如果您需要任何幫助,請訪問我們在 "+.@realm$+" 廣場的公會成員.";
		close;
	}
	mes "[深淵之門管理員]";
	mes "你好, 我是來自貓手商會傳送部門的深淵之門管理員.";
	mes "需要幫忙嗎?";
	next;
	switch(select("- 進行大門連接投資:- 地下城最深處:- 深淵回廊數据:- 取消")) {
	case 1:
		if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
			mes "[深淵之門管理員]";
			mes "您身上攜帶了太多的物品种類.";
			mes "請稍做處理后再來找我.";
			close;
		}
		if (countitem(6488) >= 4800) {
			mes "[深淵之門管理員]";
			mes "您所持的投資感謝票已經達到上限.";
			mes "無法再給您發放. 請使用部分后再來找我.";
			mes "投資感謝票可以在綿綿島兌換物品.";
			close;
		}
		if (callfunc("F_Invest_Status") != 1) {
			mes "[深淵之門管理員]";
			mes "現在不是投資期.";
			mes "請等我們開啟下一輪投資時再來.";
			close;
		}
		if (getcharid(2) != .@GID) {
			mes "[深淵之門管理員]";
			mes "你似乎并不是擁有這座城堡的公會成員.";
			mes "如果您需要任何幫助,請訪問我們在 "+.@realm$+" 廣場的公會成員.";
			close;
		}
		mes "[深淵之門管理員]";
		mes "繼續投資在你的公會名下.";
		callfunc "F_Invest_Abyss","$fund_"+.@npc$+"0"+.@id,"[深淵之門管理員]";
		end;
	case 2:
		callsub L_Enter,1,.@realm$,.@npc$;
		mes "[深淵之門管理員]";
		mes "您對 "+.@realm$+" 公會地下城 (2F)感興趣.";
		mes "讓我來引導你. 祝您好運.";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld_dun02_2",32,122; end;
				case 2: warp "gld_dun02_2",79,30; end;
				case 3: warp "gld_dun02_2",165,38; end;
				case 4: warp "gld_dun02_2",160,148; end;
				case 5: warp "gld_dun02_2",103,169; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld_dun04_2",39,258; end;
				case 2: warp "gld_dun04_2",125,270; end;
				case 3: warp "gld_dun04_2",268,251; end;
				case 4: warp "gld_dun04_2",268,108; end;
				case 5: warp "gld_dun04_2",230,35; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld_dun01_2",186,165; end;
				case 2: warp "gld_dun01_2",54,165; end;
				case 3: warp "gld_dun01_2",54,39; end;
				case 4: warp "gld_dun01_2",186,39; end;
				case 5: warp "gld_dun01_2",223,202; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld_dun03_2",28,251; end;
				case 2: warp "gld_dun03_2",164,268; end;
				case 3: warp "gld_dun03_2",164,179; end;
				case 4: warp "gld_dun03_2",268,203; end;
				case 5: warp "gld_dun03_2",199,28; end;
			}
		}
		end;
	case 3:
		callsub L_Enter,2,.@realm$,.@npc$;
		mes "[深淵之門管理員]";
		mes "讓我來引導你進入深淵回廊所連接的 "+.@realm$+" 所屬的 '戰士之路'.";
		close2;
		if (compare(strnpcinfo(2),"alde")) {
			switch(.@id) {
				case 1: warp "gld2_ald",175,41; end;
				case 2: warp "gld2_ald",77,64; end;
				case 3: warp "gld2_ald",46,127; end;
				case 4: warp "gld2_ald",104,246; end;
				case 5: warp "gld2_ald",241,156; end;
			}
		} else if (compare(strnpcinfo(2),"gef")) {
			switch(.@id) {
				case 1: warp "gld2_gef",28,199; end;
				case 2: warp "gld2_gef",217,46; end;
				case 3: warp "gld2_gef",171,195; end;
				case 4: warp "gld2_gef",30,67; end;
				case 5: warp "gld2_gef",115,40; end;
			}
		} else if (compare(strnpcinfo(2),"pay")) {
			switch(.@id) {
				case 1: warp "gld2_pay",33,112; end;
				case 2: warp "gld2_pay",119,27; end;
				case 3: warp "gld2_pay",205,111; end;
				case 4: warp "gld2_pay",175,221; end;
				case 5: warp "gld2_pay",61,221; end;
			}
		} else if (compare(strnpcinfo(2),"prt")) {
			switch(.@id) {
				case 1: warp "gld2_prt",10,27; end;
				case 2: warp "gld2_prt",14,247; end;
				case 3: warp "gld2_prt",259,236; end;
				case 4: warp "gld2_prt",226,25; end;
				case 5: warp "gld2_prt",97,11; end;
			}
		}
		end;
	case 4:
		mes "[深淵之門管理員]";
		mes "已取消.";
		close;
	}
	end;

L_Enter:
	set .@invest_status, callfunc("F_Invest_Status");
	if (.@invest_status == 3) {
		set .@status, getd("$"+getarg(2)+"_invest_result");
		if ((getarg(0) == 1 && .@status == 2) || .@status == 3)
			return;
		else {
			mes "[深淵之門管理員]";
			mes getarg(1)+" 沒有被選擇為這次的連接點.";
			mes "請等下一次投資期.";
			close;
		}
	} else if (.@invest_status < 3) {
		mes "[深淵之門管理員]";
		mes "目前沒有開啟任何連接點.";
		mes "等待本輪投資結果公布后就能夠使用了.";
		mes "請耐心等待.";
		close;
	} else {
		mes "[深淵之門管理員]";
		mes "深淵之門即將消失.";
		mes "地下城訪問暫時被關閉.";
		mes "請等下一次机會.";
		close;
	}
}

// Item Investments :: tooja_namis
//============================================================
malangdo,218,126,4	script	貓手商會公告	857,{
	mes "<< 貓手商會公告 >>";
	mes "'^C379CE投資閑置道具獲得獎勵!^000000'";
	next;
	switch(select("- 閱讀公告內容:- 事件1 查詢:- 事件2 查詢")) {
	case 1:
		mes "- 公告 -";
		mes "您好尊貴的商會會員們! 再你們的厚愛之下, 我們貓手商會每天都在進步. 所有的經理和工作人員承諾竭盡所能, 使我們能夠始終站在技術的前沿.";
		next;
		mes "- 事件公告! -";
		mes " 名為 '^C379CE投資閑置道具獲得獎勵!^000000' 的活動正在進行, 您可以把您倉庫中閑置的裝備或道具作為投資換取貓手頂級投資証書.";
		next;
		mes "您可以通過查詢物品回收清單, 來獲知那些閑置物品可以和我們交換. 感謝您的耐心觀看, 謝謝!";
		mes "- 全体貓手員工敬上 -";
		next;
		mes "底部有一個小的警告標識.";
		mes " ";
		mes "^FF0000視活動具体執行情況, 相關內容可能會有調整.^000000";
		close;
	case 2:
		mes "'^C379CE投資閑置道具獲得獎勵!^000000'";
		mes "事件1 詳情.";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@menu$, "离開:"+implode(.@event1$[0],":");
		while(1) {
			set .@i, select(.@menu$)-1;
			switch(.@i) {
				case 0: close;
				case 1: set .@str$,"2 Pieces of Queen's Wing"; break;
				case 2: set .@str$,"2 Broken Crowns"; break;
				case 3: set .@str$,"2 Fire Dragon Scales"; break;
				case 4: set .@str$,"3 Mother's Nightmares"; break;
				case 5: set .@str$,"5 Gemstones"; break;
				case 6: set .@str$,"5 Heroic Emblems"; break;
				case 7: set .@str$,"3 Taeguk Plates"; break;
				case 8: set .@str$,"3 Broken Pharaoh Symbols and 3 Tutankhamen's Masks"; break;
				case 9: set .@str$,"2 Pocket Watches"; break;
				case 10: set .@str$,"3 Young Twigs"; break;
				case 11: set .@str$,"5 Rossata Pieces"; break;
				case 12: set .@str$,"5 Fangs of Garm"; break;
				case 13: set .@str$,"3 Baphomet Dolls"; break;
				case 14: set .@str$,"1 Tiger's Footskin and 5 Tiger Skins"; break;
				case 15: set .@str$,"3 Broken Shield Pieces and 3 Shining Spear Blades"; break;
				case 16: set .@str$,"3 Osiris Dolls"; break;
				case 17: set .@str$,"1 Ice Scale"; break;
				case 18: set .@str$,"1 Skeletal Armor Piece"; break;
				case 19: set .@str$,"1 Darkred Scale Piece"; break;
				case 20: set .@str$,"2 Wills of Red Darkness"; break;
				case 21: set .@str$,"2 Foolishness of the Blind"; break;
			}
			mes "["+.@event1$[.@i-1]+"]";
			mes .@str$+" 貓手頂級投資証書.";
			next;
		}
	case 3:
		mes "下一個 '^C379CE投資閑置道具獲得獎勵!^000000' 事件2!! 很快即將開放!";
		close;
	}
}

malangdo,218,123,1	script	納米斯#invest	545,4,4,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 您攜帶了太多的物品 -";
		mes "- 請稍做整理后我們再談. -";
		close;
	}
	emotion ET_HUK;
	emotion ET_FRET;
	emotion ET_ANGER;
	mes "[納米斯]";
	mes "Purr-Purr-Purrr-Purrrr-Pur~ Meow!!";
	mes "什-- 什么? 呢么粗魯. 嚇死我了.";
	next;
	switch(select("- 你想要什么? 我要投資!:- 本次活動的目的是什么?:- 你不會睡著了吧?")) {
	case 1:
		break;
	case 2:
		mes "[納米斯]";
		mes "呃, 又有人被'推倒', 讓貓手商會給忽悠來了?";
		next;
		select("呃? 什么! 我說我被'推倒'?");
		mes "[納米斯]";
		mes "我只說一次, 你听好了!";
		mes "我們, 貓手商會為了我們忠實的客戶推出了這個活動.";
		next;
		mes "[納米斯]";
		mes "回收你們倉庫里面那些快要發霉的老古董, 可以兌換'貓手頂級投資証書'. 這對我們商會來說是吃力不討好的事情.";
		next;
		mes "[納米斯]";
		mes "但是對你來說卻非常划算, 明顯我們是虧本賺吆喝!";
		next;
		mes "[納米斯]";
		mes "想知道更多的就去看公告吧.";
		close;
	case 3:
		mes "[納米斯]";
		mes "鄉巴佬- 傻蛋-";
		mes "記住, 人類! 沒有任何一只貓比我做事更賣力了.";
		close;
	}
	mes "[納米斯]";
	mes "你一定是想參加貓手商會舉辦的 '^C379CE投資閑置道具獲得獎勵!^000000' 活動吧.";
	next;
	mes "[納米斯]";
	mes "呢么, 你想參与哪個活動?";
	next;
	switch(select("- 沒興趣:- 事件1 物品:- 事件2 物品")) {
	case 1:
		mes "[納米斯]";
		mes "我很忙, 沒事別來瞎折騰!";
		close;
	case 2:
		mes "[納米斯]";
		mes "進入事件1.";
		mes "從顯示列表中選擇.";
		next;
		setarray .@event1$[0],
			"Wings of the Insect Queen","Crown of the Fly King","I hear Dragon Scales are tough 01","The Meaning of the Mother's Nightmare",
			"To refine ore","Hekekek Orcs, Orcs!!","Study of Yin-Yang","Secrets of the Pyramid King","Tick-tock the time goes",
			"Planting trees on Mellow Island","Ancient language research","Weapons made of sharp teeth","Toy boss doll 01",
			"A tiger is just a cat after all","The shattered spear and shield?","Toy boss doll 02","Best in summer! Cold scales",
			"Piece of Thanatos' armor","I hear Dragon Scales are tough 02","The identity of the creepy eye","If only I could see!";
		set .@i, select(implode(.@event1$[0],":"))-1;
		switch(.@i+1) {
			case 1: callsub L_Check,"2 Pieces of Queen's Wing",6326,2,"Wings of the Insect Queen? What a pitiful name. I would've given some cool names!",.@event1$[.@i];
			case 2: callsub L_Check,"2 Broken Crowns",7754,2,"For crying out loud! Those cats working on these events must be playing around. Can't they think of a better name?",.@event1$[.@i];
			case 3: callsub L_Check,"2 Fire Dragon Scales",7451,2,"Dragon scales? Well... I guess they are tough. But what are they going to use them for?",.@event1$[.@i];
			case 4: callsub L_Check,"3 Mother's Nightmares",7020,3,"Even though I don't have the authority to find out, I wonder what they're doing with the collected items?",.@event1$[.@i];
			case 5: callsub L_Check,"5 Gemstones",7300,5,"Something useful for a change. The ore will end up as nice pieces of jewelry.",.@event1$[.@i];
			case 6: callsub L_Check,"5 Heroic Emblems",968,5,"Good, good. This is the kind of name I wanted!",.@event1$[.@i];
			case 7: callsub L_Check,"3 Taeguk Plates",7169,3,"Taking in something like this, maybe they're up to something dangerous.",.@event1$[.@i];
			case 8: callsub L_Check,"3 Broken Pharaoh Symbols and 3 Tutankhamen's Masks",7113,3,"I hear the pyramids are in Morocc, but I've never been there. Some humans call it Morco.",.@event1$[.@i],7114,3;
			case 9: callsub L_Check,"2 Pocket Watches",7513,2,"Those lazy merchants, why do they need pocket watches they don't even use?",.@event1$[.@i];
			case 10: callsub L_Check,"3 Young Twigs",7018,3,"Mellow Island has a climate like with where we cats used to live. But the trees die out sometimes due to it being an island.",.@event1$[.@i];
			case 11: callsub L_Check,"5 Rossata Pieces",7211,5,"I guess they collect these because humans have them, but what's the use of researching ancient languages?",.@event1$[.@i];
			case 12: callsub L_Check,"5 Fangs of Garm",7036,5,"Weak humans need sharp weapons, but cats already have nice and sharp claws, so why are we collecting these!!",.@event1$[.@i];
			case 13: callsub L_Check,"3 Baphomet Dolls",750,3,"Baphomet dolls are great for playing around with.",.@event1$[.@i];
			case 14: callsub L_Check,"1 Tiger's Footskin and 5 Tiger Skins",1030,1,"Did you know that tigers are cats too? Therefore, cats are better than tigers.",.@event1$[.@i],1029,5;
			case 15: callsub L_Check,"3 Broken Shield Pieces and 3 Shining Spear Blades",7108,3,"I don't know what they want with these.",.@event1$[.@i],7109,3;
			case 16: callsub L_Check,"3 Osiris Dolls",751,3,"Shame~ I never got to play with an Osiris doll.",.@event1$[.@i];
			case 17: callsub L_Check,"1 Ice Scale",7562,1,"Mellow Island is a fabulous place to live. But the ships are too hot.",.@event1$[.@i];
			case 18: callsub L_Check,"1 Skeletal Armor Piece",7450,1,"Ewww... Why would they collect something so creepy?",.@event1$[.@i];
			case 19: callsub L_Check,"1 Darkred Scale Piece",6091,1,"Dragon scales? Well... I guess they are tough. But what are they going to use them for?",.@event1$[.@i];
			case 20: callsub L_Check,"2 Wills of Red Darkness",7566,2,"Interesting! Does it scream or something when I poke it in the eye?",.@event1$[.@i];
			case 21: callsub L_Check,"2 Foolishness of the Blind",7021,2,"This blind man is foolish in not knowing that gathering 300 cats cures blindness...",.@event1$[.@i];
		}
	case 3:
		mes "[納米斯]";
		mes "參与活動2?";
		mes "這活動還沒開搞. 過段時間再說吧!";
		close;
	}
	end;
L_Check:
	set .@args, getargcount();
	if (.@args > 5) {
		if (countitem(getarg(5)) < getarg(6))
			set .@items,1; //incomplete
		else
			set .@items,2; //complete
	}
	if (countitem(getarg(1)) < getarg(2) || .@items == 1) {
		mes "[納米斯]";
		mes "你沒有的物品?";
		mes getarg(0)+" 來兌換一張貓手頂級投資証書.";
		next;
		mes "[納米斯]";
		mes "不要打扰我, 自己去看那邊在公告上的細節!";
		close;
	}
	mes "[納米斯]";
	mes "'^8E5601"+getarg(4)+" 事件^000000'";
	mes "看上去東西對了.";
	mes getarg(3);
	mes "呃! 差點忘記了. 你要不要投資?";
	next;
	if(select("- 投資:- 不投資") == 2) {
		mes "[納米斯]";
		mes "我很忙, 沒事別來瞎折騰!";
		close;
	}
	delitem getarg(1),getarg(2);
	if (.@items == 2)
		delitem getarg(5),getarg(6);
	getitem 6489,1; //Cats_Invest_Certif
	mes "[納米斯]";
	mes "這是你的証書. 非常感謝您的參与!";
	close;
OnTouch:
	if (rand(2))
		emotion ET_SLEEPY;
	else
		specialeffect EF_SLEEPATTACK;
	end;
}

// Investment Rewards :: tooja_japanki
//============================================================
malangdo,215,119,4	script	感謝票兌換机	564,{
	if (MaxWeight - Weight < 4500 || checkweight(1201,1) == 0) {
		mes "- 您攜帶了太多的物品 -";
		mes "- 請整理背包后再嘗試. -";
		close;
	}
	mes "[感謝票兌換机]";
	mes "^6815EA1. 請不要踢兌換机. @_@";
	mes "2. 您只能使用投資感謝票.";
	mes "3. 關于果凍盒, 開啟時會得到一件隨机物品.";
	mes "4. 希望您能喜歡~^000000";
	mes "          - 机主 Mr. Cat -";
	next;
	switch(select("- 結束:- 物品兌換:- 紅色爪子果凍袋(5張):- 黑色爪子果凍袋(50張)")) {
	case 1:
		close;
	case 2:
		mes "自動售貨机兌換的物品清單如下.";
		next;
		switch(select("- 結束:- 1個攻城箭矢筒A(2張):- 1個攻城箭矢筒S(2張):- 1個攻城白色藥水箱子(12張):- 1個攻城藍色藥水箱子(14張):- 10個暗水(20張):20 Siege_Violet_Potion (30 Tickets):10 Coldproof Potion (30 Tickets):10 Thunderproof Potion (30 Tickets):10 Earthproof Potion (30 Tickets):10 Fireproof Potion (30 Tickets):10 Elemental Converter[Fire] (30 Tickets)")) {
			case 1: close;
			case 2: callsub L_Purchase,2,12678,1; //Siege_Arrow_Quiver_A
			case 3: callsub L_Purchase,2,12677,1; //Siege_Arrow_Quiver_S
			case 4: callsub L_Purchase,12,504,30; //White_Potion
			case 5: callsub L_Purchase,14,547,30; //White_Slim_Potion
			case 6: callsub L_Purchase,20,12020,10; //Water_Of_Darkness
			case 7: callsub L_Purchase,30,11547,20; //Woe_Violet_Potion
			case 8: callsub L_Purchase,30,12119,10; //Resist_Water
			case 9: callsub L_Purchase,30,12121,10; //Resist_Wind
			case 10: callsub L_Purchase,30,12120,10; //Resist_Earth
			case 11: callsub L_Purchase,30,12118,10; //Resist_Fire
			case 12: callsub L_Purchase,30,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
			case 13: callsub L_Purchase,30,12115,10,"Elemental Converter[Water]"; //Elemental_Water
			case 14: callsub L_Purchase,30,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
			case 15: callsub L_Purchase,30,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		}
		end;
	case 3:
		mes "選擇了紅色爪子果凍袋. 需要輸入5張 ^FF0000投資感謝票^000000 會隨机贈送一個物品.";
		next;
		if (countitem(6488) < 5) {
			mes "您沒有足夠的 ^FF0000投資感謝票^000000 進行兌換.";
			close;
		}
		delitem 6488,5; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,10; //Woe_Blue_Potion
		else if (.@i <= 660) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 710) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 760) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 810) callsub L_Bag,505,5; //Blue_Potion
		else if (.@i <= 1110) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 1120) callsub L_Bag,607,1; //Yggdrasilberry
		else if (.@i <= 1130) callsub L_Bag,608,2; //Seed_Of_Yggdrasil
		else if (.@i <= 1140) callsub L_Bag,12292,5; //Unripe_Fruit
		else if (.@i <= 1150) callsub L_Bag,12293,5; //Dried_Yggdrasilberry
		else if (.@i <= 1250) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1471) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1571) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1671) callsub L_Bag,12020,10; //Water_Of_Darkness
		else if (.@i <= 1871) callsub L_Bag,12030,5; //Box_Of_Grudge
		else if (.@i <= 2071) callsub L_Bag,12031,5; //Sleepy_Box
		else if (.@i <= 2371) callsub L_Bag,12033,2; //Box_Of_Sunlight
		else if (.@i <= 2571) callsub L_Bag,12114,5,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 2771) callsub L_Bag,12115,5,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 2971) callsub L_Bag,12117,5,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 3171) callsub L_Bag,12116,5,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 3271) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 3471) callsub L_Bag,12118,5; //Resist_Fire
		else if (.@i <= 3671) callsub L_Bag,12119,5; //Resist_Water
		else if (.@i <= 3871) callsub L_Bag,12121,5; //Resist_Wind
		else if (.@i <= 4071) callsub L_Bag,12120,5; //Resist_Earth
		else if (.@i <= 4271) callsub L_Bag,929,20; //Immortal_Heart
		else if (.@i <= 4471) callsub L_Bag,1059,20; //Transparent_Cloth
		else if (.@i <= 4671) callsub L_Bag,905,20; //Stem
		else if (.@i <= 4871) callsub L_Bag,606,10; //Aloebera
		else if (.@i <= 5071) callsub L_Bag,609,10; //Amulet
		else if (.@i <= 5271) callsub L_Bag,710,2; //Illusion_Flower
		else if (.@i <= 5471) callsub L_Bag,576,20; //Prickly_Fruit
		else if (.@i <= 5671) callsub L_Bag,621,10; //Bitter_Herb
		else if (.@i <= 5871) callsub L_Bag,709,10; //Izidor
		else if (.@i <= 6071) callsub L_Bag,605,10; //Anodyne
		else if (.@i <= 6171) callsub L_Bag,11548,10; //Woe_White_Potion
		else if (.@i <= 6371) callsub L_Bag,706,10; //Four_Leaf_Clover
		else if (.@i <= 6571) callsub L_Bag,521,10; //Leaflet_Of_Aloe
		else if (.@i <= 6771) callsub L_Bag,707,10; //Singing_Plant
		else if (.@i <= 6971) callsub L_Bag,610,10; //Leaf_Of_Yggdrasil
		else if (.@i <= 7021) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 7171) callsub L_Bag,6249,10; //Savage_Meat
		else if (.@i <= 7321) callsub L_Bag,6252,10; //Wolf_Blood
		else if (.@i <= 7471) callsub L_Bag,6254,10; //Beef_Head_Meat
		else if (.@i <= 7621) callsub L_Bag,6253,10; //Cold_Ice
		else if (.@i <= 7771) callsub L_Bag,6256,10; //Ice_Fragment
		else if (.@i <= 7921) callsub L_Bag,6257,10; //Ice_Crystal
		else if (.@i <= 8071) callsub L_Bag,6260,10; //Petti_Tail
		else if (.@i <= 8309) callsub L_Bag,7932,20; //Poison_Herb_Nerium
		else if (.@i <= 8547) callsub L_Bag,7933,20; //Poison_Herb_Rantana
		else if (.@i <= 8785) callsub L_Bag,7934,20; //Poison_Herb_Makulata
		else if (.@i <= 9023) callsub L_Bag,7935,20; //Poison_Herb_Seratum
		else if (.@i <= 9261) callsub L_Bag,7936,20; //Poison_Herb_Scopolia
		else if (.@i <= 9499) callsub L_Bag,7937,20; //Poison_Herb_Amoena
		else if (.@i <= 9699) callsub L_Bag,6217,20; //Mandragora_Flowerpot
		else callsub L_Bag,12016,10; //Speed_Up_Potion
		end;
	case 4:
		mes "選擇了黑色爪子果凍袋. 需要輸入50張^FF0000投資感謝票^000000 會隨机贈送一個物品.";
		next;
		if (countitem(6488) < 50) {
			mes "您沒有足夠的 ^FF0000投資感謝票^000000 進行兌換.";
			close;
		}
		delitem 6488,50; //Thanks_Invest_Ticket
		set .@i, rand(1,10000);
		     if (.@i <= 100) callsub L_Bag,12531,1; //White_Potion_Box
		else if (.@i <= 200) callsub L_Bag,12549,1; //White_Slim_Pot_Box2
		else if (.@i <= 250) callsub L_Bag,11549,20; //Woe_Blue_Potion
		else if (.@i <= 450) callsub L_Bag,617,1; //Old_Violet_Box
		else if (.@i <= 500) callsub L_Bag,12532,1; //Royal_Jelly_Box2
		else if (.@i <= 550) callsub L_Bag,12533,1; //Blue_Herb_Box2
		else if (.@i <= 600) callsub L_Bag,505,10; //Blue_Potion
		else if (.@i <= 800) callsub L_Bag,644,1; //Gift_Box
		else if (.@i <= 810) callsub L_Bag,607,2; //Yggdrasilberry
		else if (.@i <= 820) callsub L_Bag,608,3; //Seed_Of_Yggdrasil
		else if (.@i <= 830) callsub L_Bag,12292,10; //Unripe_Fruit
		else if (.@i <= 840) callsub L_Bag,12293,10; //Dried_Yggdrasilberry
		else if (.@i <= 940) callsub L_Bag,12679,1; //Sg_White_Potion_Box
		else if (.@i <= 1140) callsub L_Bag,603,1; //Old_Blue_Box
		else if (.@i <= 1240) callsub L_Bag,12550,1,"Poison Bottle Box(30)"; //Poison_Bottle_Box2
		else if (.@i <= 1340) callsub L_Bag,12020,20; //Water_Of_Darkness
		else if (.@i <= 1440) callsub L_Bag,12030,10; //Box_Of_Grudge
		else if (.@i <= 1540) callsub L_Bag,12031,10; //Sleepy_Box
		else if (.@i <= 1690) callsub L_Bag,12033,4; //Box_Of_Sunlight
		else if (.@i <= 1790) callsub L_Bag,12114,10,"Elemental Converter[Fire]"; //Elemental_Fire
		else if (.@i <= 1890) callsub L_Bag,12115,10,"Elemental Converter[Water]"; //Elemental_Water
		else if (.@i <= 1990) callsub L_Bag,12117,10,"Elemental Converter[Wind]"; //Elemental_Wind
		else if (.@i <= 2090) callsub L_Bag,12116,10,"Elemental Converter[Earth]"; //Elemental_Earth
		else if (.@i <= 2190) callsub L_Bag,12680,1; //Sg_Blue_Potion_Box
		else if (.@i <= 2290) callsub L_Bag,12118,10; //Resist_Fire
		else if (.@i <= 2390) callsub L_Bag,12119,10; //Resist_Water
		else if (.@i <= 2490) callsub L_Bag,12121,10; //Resist_Wind
		else if (.@i <= 2590) callsub L_Bag,12120,10; //Resist_Earth
		else if (.@i <= 2690) callsub L_Bag,929,40; //Immortal_Heart
		else if (.@i <= 2790) callsub L_Bag,1059,40; //Transparent_Cloth
		else if (.@i <= 2890) callsub L_Bag,905,40; //Stem
		else if (.@i <= 2990) callsub L_Bag,606,20; //Aloebera
		else if (.@i <= 3090) callsub L_Bag,609,20; //Amulet
		else if (.@i <= 3190) callsub L_Bag,710,4; //Illusion_Flower
		else if (.@i <= 3290) callsub L_Bag,576,40; //Prickly_Fruit
		else if (.@i <= 3390) callsub L_Bag,621,20; //Bitter_Herb
		else if (.@i <= 3490) callsub L_Bag,709,20; //Izidor
		else if (.@i <= 3590) callsub L_Bag,605,20; //Anodyne
		else if (.@i <= 3690) callsub L_Bag,11548,20; //Woe_White_Potion
		else if (.@i <= 3790) callsub L_Bag,706,20; //Four_Leaf_Clover
		else if (.@i <= 3890) callsub L_Bag,521,20; //Leaflet_Of_Aloe
		else if (.@i <= 3990) callsub L_Bag,707,20; //Singing_Plant
		else if (.@i <= 4090) callsub L_Bag,610,20; //Leaf_Of_Yggdrasil
		else if (.@i <= 4140) callsub L_Bag,12676,1; //Sg_Violet_Potion_Box
		else if (.@i <= 4240) callsub L_Bag,6249,20; //Savage_Meat
		else if (.@i <= 4340) callsub L_Bag,6252,20; //Wolf_Blood
		else if (.@i <= 4440) callsub L_Bag,6254,20; //Beef_Head_Meat
		else if (.@i <= 4540) callsub L_Bag,6253,20; //Cold_Ice
		else if (.@i <= 4640) callsub L_Bag,6256,20; //Ice_Fragment
		else if (.@i <= 4740) callsub L_Bag,6257,20; //Ice_Crystal
		else if (.@i <= 4840) callsub L_Bag,6260,20; //Petti_Tail
		else if (.@i <= 4940) callsub L_Bag,7932,40; //Poison_Herb_Nerium
		else if (.@i <= 5040) callsub L_Bag,7933,40; //Poison_Herb_Rantana
		else if (.@i <= 5140) callsub L_Bag,7934,40; //Poison_Herb_Makulata
		else if (.@i <= 5240) callsub L_Bag,7935,40; //Poison_Herb_Seratum
		else if (.@i <= 5340) callsub L_Bag,7936,40; //Poison_Herb_Scopolia
		else if (.@i <= 5440) callsub L_Bag,7937,40; //Poison_Herb_Amoena
		else if (.@i <= 5540) callsub L_Bag,6217,40; //Mandragora_Flowerpot
		else if (.@i <= 6000) callsub L_Bag,12016,20; //Speed_Up_Potion
		else if (.@i <= 6500) callsub L_Bag,2483,1; //Siege_Greave
		else if (.@i <= 7000) callsub L_Bag,2484,1; //Siege_Boots
		else if (.@i <= 7500) callsub L_Bag,2485,1; //Siege_Shoes
		else if (.@i <= 8000) callsub L_Bag,2586,1; //Siege_Manteau
		else if (.@i <= 8500) callsub L_Bag,2587,1; //Siege_Muffler
		else if (.@i <= 9000) callsub L_Bag,15046,1; //Siege_Plate
		else if (.@i <= 9500) callsub L_Bag,15047,1; //Siege_Suits
		else callsub L_Bag,15048,1; //Siege_Robe
		end;
	}
	end;

//callsub L_Purchase,<ticket cost>,<reward ID>,<reward amount>{,<reward name>};
L_Purchase:
	if (countitem(6488) < getarg(0)) {
		mes "您沒有足夠的 ^FF0000投資感謝票^000000 用來兌換.";
		close;
	}
	delitem 6488,getarg(0); //Thanks_Invest_Ticket
	getitem getarg(1),getarg(2);
	mes "- 投資完成! -";
	next;
	mes "獲得獎勵物品 ^0000FF"+((getargcount() > 3)?getarg(3,""):getitemname(getarg(1)))+"^000000.";
	close;

//callsub L_Bag,<reward ID>,<reward amount>{,<reward name>};
L_Bag:
	getitem getarg(0),getarg(1);
	mes "- 兌換完成! -";
	next;
	mes "獲得獎勵物品 ^0000FF"+((getargcount() > 2)?getarg(2,""):getitemname(getarg(0)))+"^000000.";
	close;
}
