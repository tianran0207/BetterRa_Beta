//===== rAthena Script ======================================= 
//= Hazy Forest
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Cross through the Hazy Forest to reach the Bifrost.
//= Contains the "Wandering Guardian" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Instance system rewrite. [Euphy]
//============================================================ 

// Instance Creation :: md_mistwoods_enter
//============================================================
bif_fild01,158,340,5	script	精靈士兵#mist	447,{

	set .@party_id,getcharid(1);
	set .@md_name$,"迷霧森林迷宮";

	mes "[精靈士兵]";
	mes "你想進入森林?";
	next;
	if (!.@party_id) {
		mes "[精靈士兵]";
		mes "很好, 很好, 很好, 你是不是頭腦發熱了?";
		mes "你至少也要在一個組隊中吧?";
		close;
	}
	set .@playtime, checkquest(7211,PLAYTIME);
	if (is_party_leader() == true) {
		if (.@playtime == 0 || .@playtime == 1) {
			mes "[精靈士兵]";
			mes "...你确定你不需要再多准備一下?";
			mes "我想你需要多休息一下.";
			mes "只有你一個人的話, 什么都做不了";
			next;
			mes "[精靈士兵]";
			mes "我可不認為當局會讓你進入這里, 而且.";
			mes "...我認為他們會發出禁止通行的命令.";
			next;
			select("你能明白我說的吧, 是不是?");
			mes "[精靈士兵]";
			mes "...難道我?";
			mes "其實, 我說的不是你們的語言.";
			mes "我說的是精靈語.";
			next;
			mes "[精靈士兵]";
			mes "不過這對迷霧森林來說是個秘密.";
			mes "就是我們用不同的語言對話, 但是卻能夠听懂對方說什么.";
			next;
			mes "[精靈士兵]";
			mes "能夠和所有的种族對話...";
			mes "可能這是彩虹島的神奇力量.";
			close;
		} else if (.@playtime == 2) {
			mes "[精靈士兵]";
			mes "很好, 很好.";
			mes "這片森林看樣子沒有驅逐你.";
			mes "接下去你想怎么做?";
			erasequest 7211;
		} else {
			mes "[精靈士兵]";
			mes "你是個令人難以置信的人類冒險家.";
			mes "你竟然敢于進入大家都懼怕的森林深處.";
			next;
			mes "[精靈士兵]";
			mes "唷. 但是, 我必須向有關部門上報";
			mes "以防万一, 亂七八糟的人都進入森林冒險.";
			next;
			mes "[精靈士兵]";
			mes "你會得到許可";
			mes "在你進行注冊之后.";
			mes "你确定要進入迷霧森林嗎?";
		}
		next;
		switch(select("- 進入迷霧森林探險.:- 放棄.")) {
		case 1:
			mes "[精靈士兵]";
			mes "隊伍名稱 "+getpartyname(.@party_id)+"...";
			mes "隊長 "+strcharinfo(0)+"...";
			if (instance_create(.@md_name$) < 0) {
				mes "嗯...";
				next;
				mes "[精靈士兵]";
				if (.@playtime == -1)
					mes "森林中充滿危險.";
				else
					mes "現在森林里的气氛有些緊張.";
				mes "...今天你為什么不回去呢?";
				close;
			}
			mes "I've got it. I've written them down on the report here.";
			next;
				mes "[精靈士兵]";
				mes "你會很快得到許可.";
				mes "現在可以去傳送門了, 那是迷霧森林的唯一入口.";
				mes "明白了嗎?";
			next;
				mes "[精靈士兵]";
				mes "我們都不能違背森林的規則,";
				mes "不過可能你們人類會有机會.";
				mes "祝你們好運!";
			close;
		case 2:
			mes "[精靈士兵]";
			mes "很好, 你已經做出了正确的決定.";
			if (.@playtime == -1)
				mes "這對你有好處.";
			close;
		}
	} else {
		mes "[精靈士兵]";
		mes "你們的隊長呢?";
		mes "那里非常危險.";
		next;
		if (.@playtime == 2) {
			mes "[精靈士兵]";
			mes "我認為在你們已經進入森林前.";
			mes "好吧, 至少這片森林還沒有把你們驅逐出去.";
			mes "然而...";
			erasequest 7211;
			next;
		}
		mes "[精靈士兵]";
		mes "跟著我的腳步, 入口的大門, 還有讓我見一下你們隊伍的代表.";
		mes "我要与他/她討論一些事情.";
		close;
	}
}

bif_fild01,161,355,0	script	彩虹橋	844,{
	mes "透過彩虹橋我看到了整片森林.";
	mes "看上去我能穿過彩虹橋進入森林.";
	next;
	if(select("- 進入彩虹橋.:- 放棄.") == 2)
		close;
	switch(instance_enter("迷霧森林迷宮")) {
	case IE_OTHER:
		mes "[Laphine Soldier]";
		mes "Something doesn't feel right. Looks like something dangerous is going on, so you'd better turn back today.";
		close;
	case IE_NOINSTANCE:
		mes "You try to crawl into the log, but some mysterious power pushes you back with a gush of wind.";
		mes "It seems like you can't force your way into the forest.";
		close;
	case IE_NOMEMBER:
		mes "[Laphine Soldier]";
		mes "Hey, look!";
		mes "Are you going alone?";
		mes "That's impossible. Too rash.";
		mes "Team up with some friends and go together!";
		close;
	case IE_OK:
		if (checkquest(7211,PLAYTIME) == 2) erasequest 7211;
		if (checkquest(7211,PLAYTIME) == -1) setquest 7211;
		mapannounce "bif_fild01",getpartyname(getcharid(1))+" 率領隊伍 "+strcharinfo(0)+" 的成員們進入了副本.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
		//warp "1@mist",89,29;
		close;
	default:
		mes "The world beyond the log tunnel looks dark and suffocating.";
		mes "Your instinct is screaming that this forest is dangerous.";
		close;
	}
}

// Instance Scripts :: md_mistwoods_in
//============================================================
1@mist,89,29,0	script	#Whisper_mist	844,1,1,{
	end;
OnInstanceInit:
	enablenpc instance_npcname("#Whisper_mist");
	end;
OnTouch:
	disablenpc instance_npcname("#Whisper_mist");
	initnpctimer;
	end;
OnTimer10000:
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"呢喃聲: 這是誰? 這些是誰!? 森林開啟了! 湯姆, 湯姆巴, 疲倦的雷姆, 雷米! 快起來!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer60000:
	mapannounce instance_mapname("1@mist"),"大聲的呢喃: 森林的守護者想做什么? 那些園丁在干什么??",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer120000:
	mapannounce instance_mapname("1@mist"),"大聲的呢喃: 別出去, 永遠四處流浪, 和我們一起玩!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer180000:
	mapannounce instance_mapname("1@mist"),"大聲的呢喃: 沒用, 沒用. 四處晃悠, 我們最終會碰面的!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
}

// callfunc "F_Mora_Mist",<warp to enable>,<success message>,<fail message>{,<end flag>}
function	script	F_Mora_Mist	{
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname(strnpcinfo(0))+"::OnMyMobDead") == 0) {
		mes "顯而易見, 這里無人照料.";
		mes "看上去你可以砍掉這些花園里的樹木.";
		next;
		if(select("- 砍掉這些樹木.:- 放棄.") == 2)
			close;
		mes "你砍掉了樹, 那些阻擋這迷宮路線的, "+((getarg(3,0))?"掃清走出森林的障礙.":"你現在可以繼續了.");
		mapannounce .@map$,getarg(1),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
		enablenpc instance_npcname(getarg(0));
		disablenpc();
		close;
	} else
		mapannounce .@map$,((getarg(3,0))?getarg(2):getarg(2)+"的哭聲: 嗯? 是誰對我的樹木做了這些?!"),bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
}

1@mist,251,121,3	script	湯姆的花園	858,{ end; }
1@mist,247,123,0	script	湯姆花園的樹	527,{
	callfunc "F_Mora_Mist",
		"a25_a18-1",
		"咆哮吶喊: 湯姆的螺栓被打破了! 當心, 湯姆巴! 當心, 湯姆巴!",
		"Tom";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),249,120,"湯姆",2136,1,instance_npcname("湯姆花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,226,95,3	script	湯姆巴的花園	858,{ end; }
1@mist,225,98,0	script	湯姆巴花園的樹	527,{
	callfunc "F_Mora_Mist",
		"a27-2_af1-2",
		"咆哮哭泣: 湯姆巴花園的樹跟著湯姆巴走了. 雷米, 你現在准備怎么做?",
		"湯姆巴";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),200,64,"湯姆巴",2136,1,instance_npcname("湯姆巴花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,157,183,3	script	雷米的花園	858,{ end; }
1@mist,159,184,0	script	雷米花園的樹	527,{
	callfunc "F_Mora_Mist",
		"a15-2_a4-1",
		"咆哮哭泣: 雷米, 雷米花園的樹 被砍掉了 - 雷姆, 下一個會是你了. 愚蠢的雷姆!",
		"雷米";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),154,184,"疲倦的雷米",2137,1,instance_npcname("雷米花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,58,39,3	script	疲倦雷姆的花園	858,{ end; }
1@mist,61,39,0	script	疲倦雷姆花園的樹	527,{
	callfunc "F_Mora_Mist",
		"a4-2_a11",
		"雷姆絕望的哭泣: 啊... 雷姆要睡覺. 雷姆要睡覺了, 永遠不會醒來!",
		"雷姆";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),101,107,"雷姆的園丁",2136,1,instance_npcname("疲倦雷姆花園的樹")+"::OnMyMobDead";
	end;
OnTimer10000:
	mapannounce instance_mapname("1@mist"),"森林的呢喃聲: 湯姆的, 湯姆巴的, 雷米的, 和雷姆的花園里的樹都被砍掉了?",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@mist"),"森林的呢喃聲: 麻煩, 麻煩, 我們遇到大麻煩了.",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer20000:
	mapannounce instance_mapname("1@mist"),"巨大的呢喃聲: 他們已經來第二片森林. 森林深處的園丁, 警報!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,227,179,3	script	羅恩的花園	858,{ end; }
1@mist,230,179,0	script	羅恩花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b4-2_b2",
		"羅恩園丁的哭泣: 我的工作完了! 別找羅恩了, 第二個園丁, 更多!!",
		"羅恩";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),227,178,"羅恩的園丁",2134,1,instance_npcname("羅恩花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,288,226,3	script	羅孚的花園	858,{ end; }
1@mist,285,225,0	script	羅孚花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b7_bf1-2",
		"咆哮的聲音: 羅恩, 羅孚的樹木被砍下. 是誰逼著他們走進森林? 是誰?",
		"羅孚";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),304,237,"羅孚的Strutter",2134,1,instance_npcname("羅孚花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,159,318,3	script	莫納的花園	858,{ end; }
1@mist,161,316,0	script	莫納花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b31_b16",
		"吵鬧的呢喃聲: 蒙娜麗莎的种子探索者花園的樹已經沒了! 蒙娜麗莎也死了! 納蒙會沒事的, 他很勇敢, 對嗎? 納蒙, 你還好嗎?",
		"莫納";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),239,253,"莫納的种子探索者",2133,1,instance_npcname("莫納花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,205,300,3	script	納蒙的花園	858,{ end; }
1@mist,204,299,0	script	納蒙花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b27-3_bN-1",
		"吵雜的呢喃聲: 勇敢的納蒙在遭遇宿命之時卻沒有那么勇敢... *傻笑*",
		"納蒙";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),89,173,"勇敢的納蒙",2134,1,instance_npcname("納蒙花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,219,232,3	script	悲傷尼奧隆的花園	858,{ end; }
1@mist,221,236,0	script	尼奧隆花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b6-1_b33",
		"惊嘆的呢喃聲: 悲傷的尼奧隆變快樂了! 斯帕德的高級蜘蛛, 你准備怎么做? 你現在准備怎么辦?",
		"尼奧隆";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),143,265,"悲傷的尼奧隆",2137,1,instance_npcname("尼奧隆花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,206,202,3	script	斯帕德的花園	858,{ end; }
1@mist,206,200,0	script	斯帕德花園的樹	527,{
	callfunc "F_Mora_Mist",
		"b5_b14",
		"郁悶的呢喃聲: 現在第二片森林一切都結束了. 園丁正在消失-",
		"斯帕德";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),209,200,"斯帕德的八條腿",2132,1,instance_npcname("斯帕德花園的樹")+"::OnMyMobDead";
	end;
OnTimer5000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃聲: 本通路即將開啟. 緊繃的螺栓已經被移除!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer9000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃聲: 一片森林打開了, 另一片也是, 還有一片也快了. 蒂托和普姆巴, 他們正朝著你而來!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer13000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃聲: 四處游蕩了一圈又一圈, 在那里你你所遇見的的每個人都轉悠了一圈又一圈,",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	end;
OnTimer17000:
	mapannounce instance_mapname("1@mist"),"咆哮的呢喃聲: 所有的園丁, 都手持武器等待著你-!",bc_map,"0xccffcc"; //FW_NORMAL 12 0 0
	stopnpctimer;
	end;
OnMyMobDead:
	end;
}

1@mist,96,288,3	script	蒂托的花園	858,{ end; }
1@mist,95,287,0	script	蒂托花園的樹	527,{
	callfunc "F_Mora_Mist",
		"c1_c13-1",
		"蒂托的哭聲: *嗚咽* *嗚咽* 蒂托就要死了! 蒂托做了! 呸呸!!",
		"蒂托";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),264,291,"蒂托的腳蹼",2133,1,instance_npcname("蒂托花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,326,325,3	script	普姆巴的花園	858,{ end; }
1@mist,324,325,0	script	普姆巴花園的樹	527,{
	callfunc "F_Mora_Mist",
		"c13-3_c4",
		"咆哮的呢喃聲: 蒂托的努力沒能避免失敗的命運! 停止嘻嘻快去工作, 特塔! 開始做事了!",
		"普姆巴";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),309,165,"勤奮的普姆巴",2134,1,instance_npcname("普姆巴花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,278,345,3	script	特塔的花園	858,{ end; }
1@mist,280,344,0	script	特塔花園的樹	527,{
	callfunc "F_Mora_Mist",
		"c19_c4",
		"呢喃聲: 他們很強壯, 确實強大. 我們現在已經怎么辦? 是否有任何离開的園丁? 是否有任何?",
		"特塔";
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),277,343,"安閑的特塔",2136,1,instance_npcname("特塔花園的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,345,186,0	script	園丁的樹	527,{
	callfunc "F_Mora_Mist",
		"#to_bif02",
		"咆哮的呢喃聲: 森林會記得你! 它會記住你, 那個將所有園丁的電鋸全部擊碎的人!",
		"園丁: 我們還沒死, 還沒!!!!",1;
	end;
OnInstanceInit:
	set .@map$, instance_mapname("1@mist");
	monster .@map$,318,137,"湯姆",2136,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,321,137,"湯姆巴",2136,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,324,137,"雷米",2137,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,319,135,"雷蒙",2136,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,323,135,"羅恩",2134,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,320,133,"羅孚",2134,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,323,133,"莫納",2133,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,317,132,"納蒙",2134,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,326,132,"尼奧隆",2137,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,317,129,"斯帕德",2132,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,320,129,"蒂托",2133,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,324,129,"普姆巴",2134,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	monster .@map$,327,129,"特塔",2136,1,instance_npcname("園丁的樹")+"::OnMyMobDead";
	end;
OnMyMobDead:
	end;
}

1@mist,73,290,0	script	神秘的花#1	844,{
	specialeffect EF_LEVEL99_4;
	disablenpc();
	getitem 12561,1; //Mysterious_Seed
	end;
}
1@mist,69,295,0	duplicate(神秘的花#1)	神秘的花#2	844
1@mist,62,291,0	duplicate(神秘的花#1)	神秘的花#3	844
1@mist,54,293,0	duplicate(神秘的花#1)	神秘的花#4	844
1@mist,48,286,0	duplicate(神秘的花#1)	神秘的花#5	844
1@mist,54,277,0	duplicate(神秘的花#1)	神秘的花#6	844
1@mist,52,270,0	duplicate(神秘的花#1)	神秘的花#7	844
1@mist,62,268,0	duplicate(神秘的花#1)	神秘的花#8	844
1@mist,72,271,0	duplicate(神秘的花#1)	神秘的花#9	844
1@mist,72,276,0	duplicate(神秘的花#1)	神秘的花#10	844
1@mist,118,194,0	duplicate(神秘的花#1)	神秘的花#11	844
1@mist,122,204,0	duplicate(神秘的花#1)	神秘的花#12	844
1@mist,137,209,0	duplicate(神秘的花#1)	神秘的花#13	844
1@mist,144,203,0	duplicate(神秘的花#1)	神秘的花#14	844
1@mist,146,186,0	duplicate(神秘的花#1)	神秘的花#15	844
1@mist,142,182,0	duplicate(神秘的花#1)	神秘的花#16	844
1@mist,132,179,0	duplicate(神秘的花#1)	神秘的花#17	844
1@mist,123,184,0	duplicate(神秘的花#1)	神秘的花#18	844
1@mist,194,118,0	duplicate(神秘的花#1)	神秘的花#19	844
1@mist,188,109,0	duplicate(神秘的花#1)	神秘的花#20	844
1@mist,193,94,0	duplicate(神秘的花#1)	神秘的花#21	844
1@mist,205,92,0	duplicate(神秘的花#1)	神秘的花#22	844
1@mist,213,96,0	duplicate(神秘的花#1)	神秘的花#23	844
1@mist,216,103,0	duplicate(神秘的花#1)	神秘的花#24	844
1@mist,212,116,0	duplicate(神秘的花#1)	神秘的花#25	844
1@mist,207,120,0	duplicate(神秘的花#1)	神秘的花#26	844
1@mist,169,310,0	duplicate(神秘的花#1)	神秘的花#27	844
1@mist,172,296,0	duplicate(神秘的花#1)	神秘的花#28	844
1@mist,191,301,0	duplicate(神秘的花#1)	神秘的花#30	844
1@mist,195,301,0	duplicate(神秘的花#1)	神秘的花#31	844
1@mist,192,316,0	duplicate(神秘的花#1)	神秘的花#32	844
1@mist,187,320,0	duplicate(神秘的花#1)	神秘的花#33	844
1@mist,174,318,0	duplicate(神秘的花#1)	神秘的花#34	844
1@mist,308,136,0	duplicate(神秘的花#1)	神秘的花#35	844
1@mist,314,122,0	duplicate(神秘的花#1)	神秘的花#36	844
1@mist,321,118,0	duplicate(神秘的花#1)	神秘的花#37	844
1@mist,331,123,0	duplicate(神秘的花#1)	神秘的花#38	844
1@mist,335,126,0	duplicate(神秘的花#1)	神秘的花#39	844
1@mist,332,141,0	duplicate(神秘的花#1)	神秘的花#40	844
1@mist,328,144,0	duplicate(神秘的花#1)	神秘的花#41	844
1@mist,314,144,0	duplicate(神秘的花#1)	神秘的花#42	844
1@mist,238,224,0	duplicate(神秘的花#1)	神秘的花#43	844
1@mist,244,231,0	duplicate(神秘的花#1)	神秘的花#44	844
1@mist,257,232,0	duplicate(神秘的花#1)	神秘的花#45	844
1@mist,262,229,0	duplicate(神秘的花#1)	神秘的花#46	844
1@mist,265,216,0	duplicate(神秘的花#1)	神秘的花#47	844
1@mist,260,214,0	duplicate(神秘的花#1)	神秘的花#48	844
1@mist,252,206,0	duplicate(神秘的花#1)	神秘的花#49	844
1@mist,244,211,0	duplicate(神秘的花#1)	神秘的花#50	844

// Wandering Guardian Quest
//============================================================

// ----------------- Part of md_mistwoods_in -----------------
1@mist,2,2,0	script	#Dragon Herder	844,{
	end;
OnInstanceInit:
	switch(rand(1,6)) {
		case 1: setarray .@c[0],77,240; break;
		case 2: setarray .@c[0],147,261; break;
		case 3: setarray .@c[0],182,263; break;
		case 4: setarray .@c[0],194,350; break;
		case 5: setarray .@c[0],268,183; break;
		case 6: setarray .@c[0],65,278; break;
	}
	monster instance_mapname("1@mist"),.@c[0],.@c[1],"流浪紫金神龍",2131,1,instance_npcname("#Dragon Herder")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	if (mobcount(.@map$,instance_npcname("#Dragon Herder")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("地上的女子#mist")+"::OnEnable";
		donpcevent instance_npcname("神秘的年輕人#mis")+"::OnEnable";
		donpcevent instance_npcname("洛基#mist")+"::OnEnable";
		mapannounce .@map$,"龍的叫聲: 我被懲罰了... 放棄這方式... 守護者...?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
		disablenpc instance_npcname("#Dragon Herder");
		initnpctimer;
	}
	end;
OnTimer3000:
	stopnpctimer;
	mapannounce instance_mapname("1@mist"),"在你的腦袋里的聲音: ...你會... 救... 我... 請求你......?",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	end;
}

1@mist,97,30,3	script	神秘的年輕人#mis	512,{
	if (BaseLevel < 98) {
		mes "[神秘的年輕男子]";
		mes "嗯? 你打算從這里經過?";
		mes ".....恐怕它超出了你的能力, 但我不會干涉你的事.";
		mes "我們會再見面的, 如果你活著.";
		close;
	}
	if (ep14_1_mistwoods == 0) {
		if (ep13_3_secret > 23) {
			mes "[神秘的年輕男子]";
			mes "你... 我記得我們在營地見過.";
			mes "什么是有用的信息.";
			mes "謝謝!!";
			next;
			mes "[神秘的年輕男子]";
			mes "但是你打算通過這里?";
			mes "穿越彩虹橋對你有什么好處呢?";
			next;
		} else {
			mes "[神秘的年輕男子]";
			mes "你打算從這里經過?";
			mes "如果你只是好奇, 我會反對這樣做.";
			mes "這里只會帶給你死亡...";
			next;
		}
		mes "[神秘的年輕男子]";
		mes "如果你有決心通過這里,";
		mes "我會請求你們的幫我一些忙.";
		next;
		select("你是誰?");
		mes "[洛基]";
		mes "洛基... 來自刺客公會.";
		mes "這是我的名字.";
		mes "你的呢?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "....";
		mes "...."+strcharinfo(0)+" 先生.";
		next;
		mes "[洛基]";
		mes "我明白. "+strcharinfo(0)+".";
		mes "我再問你一次.";
		mes "你确定打算從穿越這片森林?";
		next;
		switch(select("是,我确定.:不,我沒有.")) {
		case 1:
			mes "[洛基]";
			mes "然后我問你一個問題.";
			mes "你是否見過一名警衛... 一個紫色頭發的女孩?";
			next;
			switch(select("- 沒,沒見過.:- 有,我可能有...")) {
			case 1:
				mes "[洛基]";
				mes "嗯, 你沒見過.";
				mes "你說的是真話嗎.";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等等.";
				mes "(洛基靠近了.)";
				mes "... 不.";
				mes "你說的是真的.";
				next;
				break;
			}
			mes "[洛基]";
			mes "我們現在要分別了.";
			mes "當然, 希望你能夠活著走出這片森林.";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		case 2:
			mes "[洛基]";
			mes "我明白了.";
			mes "如果你還沒准備好, 記得退回來.";
			mes "這片森林不喜歡陌生人.";
			set ep14_1_mistwoods,1;
			close;
		}
	} else if (ep14_1_mistwoods == 1) {
		mes "[洛基]";
		mes "我告訴過你 - 如果不想死就退回來.";
		mes "穿越森林不是一件簡單的事情.";
		mes "這可能要你的命.";
		next;
		switch(select("好的.:你想要什么?")) {
		case 1:
			mes "- 當你退一步,";
			mes "洛基輕輕地點點頭, 然后看著遠處. -";
			close;
		case 2:
			mes "[洛基]";
			mes "我在找人.";
			mes "你有沒有見過一名警衛... 一個紫色頭發的女孩?";
			next;
			switch(select("沒,沒見過.:有,我可能有...")) {
			case 1:
				mes "[洛基]";
				mes "你沒見過.";
				mes "你說的是事實.";
				next;
				break;
			case 2:
				mes "[洛基]";
				mes "等等.";
				mes "(洛基靠近了你.)";
				mes "... 不.";
				mes "你說的是真話.";
				next;
				break;
			}
			mes "[洛基]";
			mes "現在我們應該分別了.";
			mes "希望, 我們會在莫拉村再見.";
			set ep14_1_mistwoods,2;
			setquest 7212;
			close;
		}
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "我不會和你同行.";
		mes "希望, 我們會在莫拉村再見.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "我們又見面了.";
		mes "你經常來這地方?";
		mes "來莫拉的旅館.";
		mes "我有些東西要給你.";
		close;
	} else {
		mes "[洛基]";
		mes "這些天以來, 我們對對方都了解了很多.";
		mes "看上去你對這里的環境非常的适應.";
		mes "不過你可別太驕傲.";
		close;
	}
	end;
OnEnable:
	enablenpc instance_npcname("神秘的年輕人#mis");
	end;
OnDisable:
	disablenpc instance_npcname("神秘的年輕人#mis");
	end;
}

1@mist,183,304,3	script	洛基#mist	512,{
	if (ep14_1_mistwoods < 2) {
		mes "[洛基]";
		mes "在森林中的冒險家.";
		mes ".....";
		mes "通過這片森林, 你可以到達莫拉村.";
		close;
	} else if (ep14_1_mistwoods == 2) {
		mes "[洛基]";
		mes "你能不能照顧好這個刁蠻的丫頭?";
		mes "我想我要感謝你.";
		mes "你能讓她溫順下來.";
		next;
		select("發生什么了?:這女孩...?");
		mes "[洛基]";
		mes "你也許能夠改變你的外觀, 但你不能改變你的特點, 懼怕光線.";
		mes "強大到足以毀掉你的鼻子的气味.";
		next;
		mes "[洛基]";
		mes "這是尼德霍格,  世界之樹的守護者,";
		mes "我要找的人.";
		mes "現在她不會到處閑逛和發瘋了.";
		next;
		select("你現在想怎么做?");
		mes "[洛基]";
		mes "我等她醒來, 然后走出這片森林.";
		mes "我可以自己來管束她.";
		mes "希望在莫拉村再見到你.";
		set ep14_1_mistwoods,3;
		erasequest 7212;
		setquest 7213;
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "我不會和你同行.";
		mes "我可以自己管束住她.";
		mes "如果你還有什么要說的, 就在我們离開森林之前都說完把.";
		close;
	} else {
		mes "[洛基]";
		mes "我不會和你同行.";
		mes "如果你還有什么要說的, 就在我們离開森林之前都說完把.";
		close;
	}
	end;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("洛基#mist");
	end;
OnEnable:
	enablenpc instance_npcname("洛基#mist");
	end;
}

1@mist,181,303,3	script	地上的女子#mist	511,{
	mes "你看到一個長著神秘頭發的女子癱倒在地上.";
	mes "當你接近她, 你聞到一股花的香味, 這濃郁的香味使你頭暈.";
	close;
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("地上的女子#mist");
	end;
OnEnable:
	enablenpc instance_npcname("地上的女子#mist");
	end;
}

// --------------- Part of md_mistwoods_enter ----------------
mora,46,152,5	script	滿身花香的女子	510,{
	if (ep14_1_mistwoods < 3) {
		mes "[滿身花香的女子]";
		mes "我需要休息.";
		mes "和你說話讓我非常疲憊.";
		mes "我和抱歉.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[守護者尼德霍格]";
		mes "我在這里等你.";
		mes "我感到十分的困惑, 但是我知道我做錯了什么.";
		next;
		mes "[守護者尼德霍格]";
		mes "如果不是你, 我會仍然在黑暗的深淵中徘徊.";
		mes "感謝你, 發自我的肺腑.";
		next;
		mes "[守護者尼德霍格]";
		mes "我為自己感到羞愧, 在孤獨的時候陶醉在甜蜜卻空洞的話語中.";
		mes "....";
		next;
		mes "[守護者尼德霍格]";
		mes "我已經不配再成為守護者了.";
		mes "一條瘋了的龍不再是守護者了.";
		next;
		mes "[洛基]";
		mes "世界之樹并不這樣認為.";
		mes "尼蒂, 不要忘記你的使命.";
		next;
		mes "[守護者尼德霍格]";
		mes "你是...?";
		mes "我明白...";
		mes "我想我被拋棄了...";
		next;
		mes "守護者慢慢移開目光仿佛陷入沉思.";
		set ep14_1_mistwoods,4;
		close2;
	} else if (ep14_1_mistwoods == 4) {
		mes "- 他似乎是在思想深處. -";
		close;
	} else {
		mes "[守護者尼德霍格]";
		mes "我還沒有恢复我所有力量.";
		mes "對不起, 我保護不了你.";
		next;
		mes "[守護者尼德霍格]";
		mes "洛基說...";
		mes "我不會因為孤獨而再度哭泣,";
		mes "我將會一直有你這樣的新朋友...";
		next;
		mes "[守護者尼德霍格]";
		mes "...我失去了一切, 但是現在我很快樂.";
		close;
	}
}

mora,48,152,3	script	犀利的男人	512,{
	if (checkweight(1201,1) == 0 || MaxWeight - Weight < 1000) {
		mes "- 你的物品种類太多了. -";
		close;
	}
	if (ep14_1_mistwoods < 3) {
		mes "他沒有看你一眼. 感覺他對任何事物都不感興趣.";
		close;
	} else if (ep14_1_mistwoods == 3) {
		mes "[洛基]";
		mes "你已經穿越了森林.";
		mes "看上去尼蒂好像有話要對你說.";
		close;
	} else if (ep14_1_mistwoods == 4) {
		mes "[洛基]";
		mes "尼德霍格是世界之樹的守護者之一.";
		mes "但是在夢羅克誘惑他的時候, 他放棄了作為守護者的職責.";
		next;
		mes "[洛基]";
		mes "有群傻瓜在夢羅克舉行了一次破除封印的儀式.";
		mes "不過儀式沒有任何效果. 夢羅克的隨從們未能打破封印.";
		next;
		mes "[洛基]";
		mes "但封印已經非常脆弱, 它有可能隨時都會被破開.";
		mes "世界之樹用它的意志支撐著封印, 避免封印被毀.";
		next;
		mes "[守護者尼德霍格]";
		mes "這是正确的. 世界之樹貫穿著整個世界的力量.";
		mes "當守護者和世界之樹直接發生問題時, 世界之樹會變得不穩定.";
		next;
		mes "[守護者尼德霍格]";
		mes "這就是為什么所有的災難都針對世界之樹而發生.";
		mes "當世界之樹不穩定時, 它對封印的控制也會隨之減弱.";
		next;
		mes "[洛基]";
		mes "然后夢羅克自己突破了封印.";
		mes "雖然看上去好像他只是用了自己的力量打破封印...";
		next;
		mes "[守護者尼德霍格]";
		mes "這都是我的錯.";
		mes "我無法掙脫來自夢羅克的誘惑.";
		mes "太... 太久...";
		next;
		mes "[守護者尼德霍格]";
		mes "...我想緩解我成為數千守護者的孤獨感.";
		mes "并沒有在正确的在我心靈, 然后.";
		mes "我想沒有任何一件事能夠比我對世界之樹的傷害更大.";
		next;
		mes "[守護者尼德霍格]";
		mes "我失去了意識, 并做了我不應該做的事情.";
		mes "我的另一面, 成為一個影子, 一個魯莽的暴君.";
		next;
		mes "[守護者尼德霍格]";
		mes "這就是我如何獨自留下...";
		mes "我失落的感覺太大…這比我經歷了數千年的孤獨痛苦的多.";
		next;
		mes "[守護者尼德霍格]";
		mes "面對世界, 我意識到.";
		mes "我感到太大的失落感... 它比我忍受了几千年的寂寞痛苦得多.";
		next;
		mes "[守護者尼德霍格]";
		mes "因此我變成了罪人, 被世界所遺忘.";
		next;
		mes "[洛基]";
		mes "還為時不晚.";
		mes "你始終還是守護者.";
		mes "你始終還有事情需要去面對.";
		next;
		mes "[守護者尼德霍格]";
		mes "... 呃, 是的. 現在已經晚了, 但是我必須履行我作為守護者的職責.";
		mes "現在我知道. 我將要用自己的雙手來摧毀夢羅克.";
		mes "那是我的使命, 守護者的使命.";
		next;
		switch(select("你并不孤單.:這些都是非常重要的職責.")) {
		case 1:
			mes "[守護者尼德霍格]";
			mes "你說得對.";
			mes "這可能就是為什么我沒有被拋棄 - 要認識到我并不孤單.";
			mes "你就是一种心靈寄托.";
			next;
			break;
		case 2:
			mes "[守護者尼德霍格]";
			mes "不過這是我的贖罪.";
			mes "我很高興, 雖然.";
			mes "我不用再去對抗無盡的寂寞了.";
			next;
			break;
		}
		mes "[守護者尼德霍格]";
		mes strcharinfo(0)+"...";
		mes "謝謝你听我說完呢么長的故事.";
		next;
		mes "[守護者尼德霍格]";
		mes "這就是我永遠有我.";
		mes "我敢肯定, 總有一天它會保護你的.";
		next;
		mes "[洛基]";
		mes "(從他的脖子上摘下他的披肩, 并把它交給你.)";
		next;
		mes "[守護者尼德霍格]";
		mes "愿大地母親的祝福与你同在...";
		set ep14_1_mistwoods,10;
		completequest 7213;
		getitem 2568,1; //Muffler_Of_Roki
		getitem 2858,1; //Pendant_Of_Guardian
		getexp 400000,400000;
		close;
	} else {
		mes "[洛基]";
		mes "我會在這里住一段時間.";
		mes "我們會再見的.";
		next;
		mes "[洛基]";
		mes "當夢羅克揭示了他的卑劣本質的那天...";
		mes "我會等著他.";
		close;
	}
}

// Monster Spawns
//============================================================
1@mist,1,1,0	script	#Mist Mobs Respawn1	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"米米怪",2137,15,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"米米怪",2137,.@num_mobs,instance_npcname("#Mist Mobs Respawn1")+"::OnMyMobDead";
	end;
}

1@mist,1,2,0	script	#Mist Mobs Respawn2	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"胖姆蜘蛛",2132,35,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"胖姆蜘蛛",2132,.@num_mobs,instance_npcname("#Mist Mobs Respawn2")+"::OnMyMobDead";
	end;
}

1@mist,1,3,0	script	#Mist Mobs Respawn3	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"安格拉螳螂",2133,35,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"安格拉螳螂",2133,.@num_mobs,instance_npcname("#Mist Mobs Respawn3")+"::OnMyMobDead";
	end;
}

1@mist,1,4,0	script	#Mist Mobs Respawn4	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"煤山雀",2134,35,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 35 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"煤山雀",2134,.@num_mobs,instance_npcname("#Mist Mobs Respawn4")+"::OnMyMobDead";
	end;
}

1@mist,1,5,0	script	#Mist Mobs Respawn5	844,{
	end;
OnInstanceInit:
	monster instance_mapname("1@mist"),0,0,"小幻獸",2136,15,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@mist");
	set .@num_mobs, 15 - mobcount(.@map$,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead");
	if (.@num_mobs > 0)
		monster .@map$,0,0,"小幻獸",2136,.@num_mobs,instance_npcname("#Mist Mobs Respawn5")+"::OnMyMobDead";
	end;
}

1@mist,1,6,0	script	#Fragments of Memory	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Fragments of Memory");
	monster instance_mapname("1@mist"),0,0,"Guardian's Fragments of Memory",2138,15;
	end;
}

// Warp Portals
//============================================================
bif_fild01,38,374,0	script	神秘的花#ep14_1	844,{
	mes "這是一朵充滿神秘力量的巨大花朵.";
	if (countitem(12561)) {
		mes "它對你手中的神奇种子產生了巨大的反應.";
		next;
		switch(select("觀察反應.:終止反應.")) {
		case 1:
			mes "花朵開始劇烈搖晃";
			mes "并向前擺動, 并試圖盡可能的將种子送到遠處!!!";
			close2;
			delitem 12561,1; //Mysterious_Seed
			warp "bif_fild02",160,230;
			end;
		case 2:
			mes "正如你緊緊地抓住了神秘的花卉种子,";
			mes "花劇烈地顫抖起來, 而后又恢复了平靜.";
			close;
		}
	}
	mes "它看起來應該有一些特殊的用途, 但你還不清楚是什么.";
	close;
}

1@mist,104,23,0	warp	#to_bif01	1,1,bif_fild01,160,352

1@mist,109,70,0	script	a1_a2	45,1,1,{
	end;
OnTouch_:
	if (checkquest(7211,PLAYTIME) == -1)
		setquest 7211;
	warp instance_mapname("1@mist"),116,40;
	end;
}

1@mist,113,40,0	warp	a2_a7	1,1,1@mist,90,81
1@mist,93,81,0	warp	a7_a14	1,1,1@mist,108,152
1@mist,111,154,0	warp	a14_a24	1,1,1@mist,236,108
1@mist,236,110,0	warp	a24_a1	1,1,1@mist,106,69
1@mist,161,67,0	warp	a18-1_a22	1,1,1@mist,159,85
1@mist,180,116,0	warp	a23_a18-1	1,1,1@mist,164,66
1@mist,180,63,0	warp	a18-2_a9	1,1,1@mist,141,90
1@mist,116,84,0	warp	a8_a26	1,1,1@mist,222,133
1@mist,220,133,0	warp	a26_a27-1	1,1,1@mist,200,64
1@mist,197,64,0	warp	a27-1_a23	1,1,1@mist,177,116
1@mist,146,194,0	warp	af1-2_a27-2	1,1,1@mist,225,96
1@mist,133,195,0	warp	af1-1_af2-1	1,1,1@mist,202,110
1@mist,202,107,0	warp	af2-1_af1-1	1,1,1@mist,134,189
1@mist,217,110,0	warp	af2-2_a20-1	1,1,1@mist,240,56
1@mist,240,54,0	warp	a20-1_af2-2	1,1,1@mist,214,110
1@mist,234,67,0	warp	a20-2_a21	1,1,1@mist,239,80
1@mist,242,77,0	warp	a21_a15-1	1,1,1@mist,153,137
1@mist,156,139,0	warp	a15-1_a3	1,1,1@mist,123,55
1@mist,124,51,0	warp	a3_a20-1	1,1,1@mist,240,56
1@mist,48,41,0	warp	a4-1_a17	1,1,1@mist,174,165
1@mist,171,165,0	warp	a17_a16	1,1,1@mist,168,143
1@mist,169,141,0	warp	a16_a10	1,1,1@mist,101,107
1@mist,99,107,0	warp	a10_a4-2	1,1,1@mist,57,38
1@mist,65,113,0	warp	a11_a28	1,1,1@mist,218,151
1@mist,221,151,0	warp	a28_a13	1,1,1@mist,117,126
1@mist,114,126,0	warp	a13_a5	1,1,1@mist,36,62
1@mist,61,88,0	warp	a6_a19	1,1,1@mist,189,139
1@mist,186,140,0	warp	a19_a12	1,1,1@mist,98,126
1@mist,101,126,0	warp	a12_a29-1	1,1,1@mist,238,151
1@mist,235,152,0	warp	a29-1_af1-2	1,1,1@mist,144,194
1@mist,254,151,0	warp	a29-2E_b1	1,1,1@mist,271,164
1@mist,275,211,0	warp	b1_b13	1,1,1@mist,277,112
1@mist,301,141,0	warp	b14_b24	1,1,1@mist,99,232
1@mist,99,230,0	warp	b24_b25	1,1,1@mist,103,255
1@mist,102,253,0	warp	b25_b4-1	1,1,1@mist,215,177
1@mist,212,177,0	warp	b4-1_b1	1,1,1@mist,276,208
1@mist,289,167,0	warp	b2_b19	1,1,1@mist,152,219
1@mist,149,219,0	warp	b19_b20	1,1,1@mist,119,227
1@mist,115,227,0	warp	b20_b23	1,1,1@mist,230,353
1@mist,233,353,0	warp	b23_b7	1,1,1@mist,289,225
1@mist,76,273,0	warp	bf1-2_b7	1,1,1@mist,289,225
1@mist,61,282,0	warp	bf1-1_bf2-1	1,1,1@mist,256,220
1@mist,252,220,0	warp	bf2-1_bf1-1	1,1,1@mist,65,282
1@mist,267,213,0	warp	bf2-2_b28	1,1,1@mist,214,268
1@mist,212,265,0	warp	b28_b12	1,1,1@mist,288,266
1@mist,285,266,0	warp	b12_b21	1,1,1@mist,143,234
1@mist,143,231,0	warp	b21_b29	1,1,1@mist,222,281
1@mist,223,279,0	warp	b29_b31	1,1,1@mist,158,317
1@mist,134,287,0	warp	b30_b3	1,1,1@mist,293,195
1@mist,295,197,0	warp	b3_b10	1,1,1@mist,239,253
1@mist,236,253,0	warp	b10_b12	1,1,1@mist,288,266
1@mist,111,203,0	warp	b16_b34	1,1,1@mist,202,333
1@mist,199,333,0	warp	b34_b27-1	1,1,1@mist,203,251
1@mist,206,249,0	warp	b27-1_b22	1,1,1@mist,168,239
1@mist,165,239,0	warp	b22_b15	1,1,1@mist,89,173
1@mist,177,265,0	warp	b27-2_a14	1,1,1@mist,108,152
1@mist,166,314,0	warp	bN-1_b27-3	1,1,1@mist,206,297
1@mist,197,299,0	warp	bN-2_b17	1,1,1@mist,167,200
1@mist,165,200,0	warp	b17_bN-2	1,1,1@mist,193,299
1@mist,86,173,0	warp	b15_b32	1,1,1@mist,218,313
1@mist,215,313,0	warp	b32_b28	1,1,1@mist,214,268
1@mist,230,324,0	warp	b33_b6-1	1,1,1@mist,221,233
1@mist,230,230,0	warp	b6-2_b11	1,1,1@mist,263,261
1@mist,263,258,0	warp	b11_b9	1,1,1@mist,272,246
1@mist,269,246,0	warp	b9_b26	1,1,1@mist,143,265
1@mist,140,265,0	warp	b26_b5	1,1,1@mist,209,200
1@mist,195,206,0	warp	b18_b35	1,1,1@mist,169,341
1@mist,166,341,0	warp	b35_b36	1,1,1@mist,198,354
1@mist,201,354,0	warp	b36_b37-1	1,1,1@mist,83,224
1@mist,87,224,0	warp	b37-1_b35	1,1,1@mist,169,341
1@mist,84,273,0	warp	b37-2_c1	1,1,1@mist,98,287
1@mist,111,298,0	warp	c2_c3	1,1,1@mist,82,307
1@mist,79,307,0	warp	c3_c7	1,1,1@mist,284,89
1@mist,281,89,0	warp	c7_c12	1,1,1@mist,264,291
1@mist,261,291,0	warp	c12_c2	1,1,1@mist,115,298
1@mist,326,275,0	warp	c13-1_b13	1,1,1@mist,277,112
1@mist,294,293,0	warp	c13-2_c5	1,1,1@mist,78,330
1@mist,81,330,0	warp	c5_c17	1,1,1@mist,308,330
1@mist,308,327,0	warp	c17_c10	1,1,1@mist,309,165
1@mist,306,165,0	warp	c10_c13-2	1,1,1@mist,298,293
1@mist,46,316,0	warp	c4_c6	1,1,1@mist,113,328
1@mist,95,328,0	warp	c6_c9	1,1,1@mist,342,157
1@mist,339,157,0	warp	c9_c16	1,1,1@mist,343,328
1@mist,343,325,0	warp	c16_c8-2	1,1,1@mist,346,120
1@mist,344,123,0	warp	c8-2_c19	1,1,1@mist,277,343
1@mist,256,313,0	warp	c18_c15	1,1,1@mist,343,307
1@mist,342,305,0	warp	c15_cKEY	1,1,1@mist,332,121
1@mist,335,120,0	warp	cKEY_c18	1,1,1@mist,259,313
1@mist,317,89,0	warp	c8-1_c14	1,1,1@mist,339,284
1@mist,339,281,0	warp	c14_c11-1	1,1,1@mist,332,172
1@mist,329,171,0	warp	c11-1_c8-2	1,1,1@mist,346,120
1@mist,301,237,0	warp	b8_b2	1,1,1@mist,289,170

// Disable Garden Tree warps.
// Custom, but saves processing and lines.
1@mist,1,1,0	script	#mist_warp_init	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("a25_a18-1");
	disablenpc instance_npcname("a27-2_af1-2");
	disablenpc instance_npcname("a15-2_a4-1");
	disablenpc instance_npcname("a4-2_a11");
	disablenpc instance_npcname("b4-2_b2");
	disablenpc instance_npcname("b7_bf1-2");
	disablenpc instance_npcname("b31_b16");
	disablenpc instance_npcname("b27-3_bN-1");
	disablenpc instance_npcname("b6-1_b33");
	disablenpc instance_npcname("b5_b14");
	disablenpc instance_npcname("c1_c13-1");
	disablenpc instance_npcname("c13-3_c4");
	disablenpc instance_npcname("c19_c4");
	disablenpc instance_npcname("#to_bif02");
	disablenpc instance_npcname("#mist_warp_init");
	end;
}
1@mist,247,123,0	warp	a25_a18-1	1,1,1@mist,164,66	//Tom
1@mist,225,98,0	warp	a27-2_af1-2	1,1,1@mist,144,194	//Tomba
1@mist,159,184,0	warp	a15-2_a4-1	1,1,1@mist,49,37	//Remi
1@mist,61,39,0	warp	a4-2_a11	1,1,1@mist,68,113	//Rem
1@mist,230,179,0	warp	b4-2_b2	1,1,1@mist,289,170	//Ron
1@mist,285,225,0	warp	b7_bf1-2	1,1,1@mist,72,274	//Rover
1@mist,161,316,0	warp	b31_b16	1,1,1@mist,108,203	//Mona
1@mist,204,299,0	warp	b27-3_bN-1	1,1,1@mist,169,314	//Namon
1@mist,221,236,0	warp	b6-1_b33	1,1,1@mist,233,324	//Neoron
1@mist,206,200,0	warp	b5_b14	1,1,1@mist,298,142	//Spyder
1@mist,95,287,0	warp	c1_c13-1	1,1,1@mist,323,276	//Tito
1@mist,324,325,0	warp	c13-3_c4	1,1,1@mist,49,316	//Pumba
1@mist,280,344,0	warp	c19_c4	1,1,1@mist,49,316	//Tete
1@mist,345,186,0	warp	#to_bif02	1,1,bif_fild02,151,121	//The Gardeners' Tree
