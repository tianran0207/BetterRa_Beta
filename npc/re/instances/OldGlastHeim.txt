//===== rAthena Script =======================================
//= Old Glast Heim
//===== Description: =========================================
//= Discover the history of events that took place in the
//= Glast Heim castle and how it ended up in ruins.
//===== Changelogs: =================================
//= 1.0 First version. [Euphy, Ziu, Heris]
//=     This is a custom version, and may contain bugs.
//= 1.1 Bug fixes; removed redundant OnInstanceInit scripts.
//= 1.2 Add NPC Hugin's Follower [exneval]
//=     NPC that give access to Glast Heim Nightmare Mode.
//= 1.3 Add some NPCs placeholder. [exneval]
//= 1.4 Update to its official text. [exneval]
//=     Support merchant, enchant, socket features.
//= 1.5 idAthena merge. Removed Hugin's Follower NPCs. [Secretdataz]
//=     Changed `set` calls to direct assignments.
//=     Cleaned up the script.
//=     TODO: Confirm Hugin NPC's code flow.
//= 1.6 Cleaned up the dialogue. [Aleos]
//============================================================

glast_01,204,273,5	script	華金	4_M_SAGE_C,{
	if(BaseLevel < 130) {
		mes "[華金]";
		mes "等你再提升提升自己，我才可能對你感興趣，冒險者。";
		mes "先升到130級吧﹗";
		close;
	}
	if (isbegin_quest(12316) == 0) {
		mes "[華金]";
		mes "很久以前，這座城堡并不是這樣的。";
		next;
		mes "[華金]";
		mes "啊...不好意思，我在陌生人面前自顧自的說什么呢。";
		next;
		mes "[華金]";
		mes "我叫華金，我是專門研究時空的次元裂縫的旅行者。";
		next;
		select("這里有那樣的東西么﹖");
		mes "[華金]";
		mes "你對克雷斯特漢姆古城的過去不好奇么﹖";
		next;
		mes "[華金]";
		mes "曾經居住在這的國王叫做舒米徹，在盧恩米德加爾特王國的歷史中，這是一位暴君。";
		next;
		mes "[華金]";
		mes "但這是真相么﹖也許只有時間旅行能揭示。";
		next;
		if(select("我對歷史不感興趣。:哦，听起來很有意思，有什么發現么﹖") == 1) {
			mes "[華金]";
			mes "真的﹖那等你有興趣了再來吧。";
			close;
		}
		mes "[華金]";
		mes "實際上，我發現了時空裂縫，可以前往古代的克雷斯特漢姆，但我有點懼怕進去。";
		next;
		mes "[華金]";
		mes "對了﹗你是冒險者吧，也許你會有興趣去冒險。";
		next;
		mes "[華金]";
		mes "你想經歷時間旅行么﹖";
		next;
		if(select("算了:是的，當然﹗") == 1) {
			mes "[華金]";
			mes "真的﹖不過跳進時空裂縫中确實也挺危險的。";
			close;
		}	
		mes "[華金]";
		mes "我就知道你愿意，請回來之后告訴我你的見聞﹗";
		setquest 12316;
		completequest 12316;
	}
	else {
		mes "[華金]";
		mes "准備好進去了﹖";
		if (isbegin_quest(12322) == 1)
			erasequest 12322;
	}
	next;
	mes "[華金]";
	mes "你說我看起來面熟﹖這不可能，我們不是剛剛第一次見面么﹖";
	mes "接下來你想怎么做﹖";
	next;
	switch( checkquest(12317,PLAYTIME) ) {
	case -1:
		.@party_id = getcharid(1);
		.@p_name$ = getpartyname(.@party_id);
		.@md_name$ = "Old Glast Heim";

		if (!instance_check_party(.@party_id)) {
			mes "[華金]";
			mes "你的隊伍成員呢﹖";
		}
		if (is_party_leader() == true)
			.@menu$ = "Generate Time Gap";
		else {
			mes "[Hugin]";
			mes "Have we met before? No way. It's my first time seeing you. What do you want?";
		}
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		switch( select( .@menu$, "Enter Old Glast Heim", "Cancel" ) ) {
		case 1:
			switch( instance_create(.@md_name$) ) {
			case -3:
				dispbottom "記憶地牢, '古城的記憶' 已經被創建過了.",0xFFFFFF;
				close;
			case -4:
			case -2:
			case -1:
				mes "隊伍名稱: " + getpartyname( getcharid(1) );
				mes "隊    長: " + strcharinfo(0);
				mes "^0000ff" + .@md_name$ + "^000000 - 創建副本失敗.";
				close;
			}
			mes "[華金]";
			mes "時間裂縫已經打開了，准備好了再來吧。";
			close;
		case 2:
			switch( instance_enter(.@md_name$) ) {
			case IE_OTHER:
				mes "[華金]";
				mes "發生一個未知錯誤。";
				close;
			case IE_NOINSTANCE:
				mes "[華金]";
				mes "副本進度 古代的克雷斯特漢姆 不存在。";
				mes "隊長還沒有建立副本進度。";
				close;
			case IE_NOMEMBER:
				mes "[華金]";
				mes "只有注冊過的成員可以進入副本 古代的克雷斯特漢姆(困難)。";
				close;
			case IE_OK:
				mapannounce "glast_01", "" + getpartyname( getcharid(1) ) + " party member " + strcharinfo(0) + " enters the " + .@md_name$ + ".", bc_map,0x00FF99;
				setquest 12317;// Trace of Time Travel
				// warp "1@gl_k",150,20;
				end;
			}
		case 3:
			close;
		}
	case 0:
	case 1:
		mes "[華金]";
		mes "噢，天啊。";
		mes "你的身上還有副本的印記。這种情況下，你是不可以再次進入副本的。";
		next;
		mes "[華金]";
		mes "你應該多休息一下，晚些時候再來。";
		close;
	case 2:
		mes "^0000ff古代的克雷斯特漢姆的印記已被清除。現在可以再次与華金對話了。^000000";
		if (isbegin_quest(12318) == 0)
			setquest 12318;// Corrupted Soul Hunt
		erasequest 12317;
		close;
	}
}

// Warps
1@gl_k,96,80,0	warp	#2Control	2,2,1@gl_k,80,80
1@gl_k,90,80,0	warp	#2Control2	2,2,1@gl_k,105,80
1@gl_k,202,79,0	warp	#3Control	2,2,1@gl_k,215,79
1@gl_k,206,79,0	warp	#3Control2	2,2,1@gl_k,195,79
1@gl_k,227,216,0	warp	#4Control	2,2,1@gl_k,215,216
1@gl_k,222,216,0	warp	#4Control2	2,2,1@gl_k,233,216
1@gl_k,150,284,0	warp	#2F Entrance	2,2,2@gl_k,150,46
2@gl_k,149,32,0	warp	#1 Control	2,2,1@gl_k,150,270
2@gl_k,145,123,0	warp	#22 Control	2,2,2@gl_k,126,123
2@gl_k,136,122,0	warp	#22 Control2	2,2,2@gl_k,150,116
2@gl_k,154,101,0	warp	#23 Control	2,2,2@gl_k,174,101
2@gl_k,165,101,0	warp	#23 Control2	2,2,2@gl_k,150,111
2@gl_k,150,163,0	warp	#24 Control	2,2,2@gl_k,150,179
2@gl_k,150,167,0	warp	#24 Control2	2,2,2@gl_k,150,149
1@gl_k,69,168,0	warp	#Secret Room Exit	2,2,1@gl_k,48,168

// Floor 1
//============================================================
1@gl_k,149,41,5	script	巴爾蒙德#0	4_M_BARMUND,{
	if (is_party_leader() == false) {
		mes "[巴爾蒙德]";
		mes "你們的隊長呢? 我們需要幫助..";
		cutin "gl_barmund2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[巴爾蒙德]";
	mes "你來了﹖你是來幫助我們的吧﹖";
	npctalk "巴爾蒙德﹕你來了﹖你是來幫助我們的吧﹖";
	cutin "gl_barmund1",2;
	next;
	select("呃...那個...");
	mes "["+.@player_name$+"]";
	mes "嗯，有人告訴我們來找巴爾蒙德。";
	unittalk .@account_id,.@player_name$ + ": 嗯，有人告訴我們來找巴爾蒙德。";
	next;
	mes "[巴爾蒙德]";
	mes "已經沒有多少時間了，我們必須找到海因里希長官，告訴他希梅爾麥茲要來了。";
	npctalk "巴爾蒙德﹕那別浪費時間了。我們必須告訴海因里希司令官，希梅爾麥茲要來了。";
	cutin "gl_barmund2",2;
	next;
	select("誰是希梅爾麥茲...");
	mes "[" + .@player_name$ + "]";
	mes "希梅爾麥茲﹖她是誰﹖";
	unittalk .@account_id,.@player_name$ + ": 希梅爾麥茲﹖她是誰﹖";
	next;
	mes "[巴爾蒙德]";
	mes "難道沒有人告訴你這些最基本的信息么﹖";
	npctalk "巴爾蒙德﹕難道沒有人告訴你這些最基本的信息么﹖";
	cutin "gl_barmund3",2;
	next;
	mes "[巴爾蒙德]";
	mes "她是代表死亡的瓦爾基里。尤米爾的心臟藏在這里，希梅爾麥茲就是為此而來。";
	npctalk "巴爾蒙德﹕她是代表死亡的瓦爾基里。尤米爾的心臟藏在這里，希梅爾麥茲就是為此而來。";
	cutin "gl_barmund2",2;
	next;
	mes "[巴爾蒙德]";
	mes "為了這個目的，她甚至能摧毀這整座城堡。";
	npctalk "巴爾蒙德﹕為了這個目的，她甚至能摧毀這整座城堡。";
	next;
	mes "[巴爾蒙德]";
	mes "快點﹗去告訴海因里希長官這個消息。我要先檢查一下周邊，希梅爾麥茲很可能已經在這布下了結界﹗";
	npctalk "巴爾蒙德﹕快點﹗去告訴海因里希長官這個消息。我要先檢查一下周邊，希梅爾麥茲很可能已經在這布下了結界﹗";
	close2;
	cutin "",255;
	disablenpc instance_npcname("巴爾蒙德#0");
	enablenpc instance_npcname("海因里希#1");
	enablenpc instance_npcname("巴爾蒙德#1");
	end;
}

1@gl_k,145,104,6	script	卡利斯格十字軍#1	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "快要到換班的時間了, 下班要去哪里呢?.";
	close;
}

1@gl_k,154,104,3	script	卡利斯格十字軍#2	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "有什么事情想要告訴我嗎?";
	close;
}

1@gl_k,145,99,6	script	純白騎士#3	4_WHITEKNIGHT,{
	mes "[純白騎士]";
	mes "你是根巴爾蒙德一起來的? 長官在等你.";
	close;
}

1@gl_k,154,99,3	duplicate(卡利斯格十字軍#1)	純白騎士#4	4_WHITEKNIGHT

1@gl_k,145,94,6	script	卡利斯格十字軍#5	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "我昨天做了一個夢, 我夢到了我的媽媽, 不知道他還好嗎?...";
	close;
}

1@gl_k,154,94,3	duplicate(純白騎士#3)	卡利斯格十字軍#6	4_F_KHALITZBURG

1@gl_k,145,89,6	script	純白騎士#7	4_WHITEKNIGHT,{
	mes "[純白騎士]";
	mes "請不要在這里亂來.";
	close;
}
1@gl_k,154,89,3	duplicate(純白騎士#7)	純白騎士#8	4_WHITEKNIGHT

1@gl_k,145,84,6	script	卡利斯格十字軍#9	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "請遵守秩序.";
	close;
}
1@gl_k,154,84,3	duplicate(卡利斯格十字軍#9)	卡利斯格十字軍#10	4_F_KHALITZBURG

1@gl_k,145,79,6	script	純白騎士#11	4_WHITEKNIGHT,{
	mes "[純白騎士]";
	mes "How do you know 巴爾蒙德? I heard that he's not really a friendly man...";
	close;
}
1@gl_k,154,79,3	duplicate(純白騎士#11)	純白騎士#12	4_WHITEKNIGHT

1@gl_k,145,74,6	script	卡利斯格十字軍#13	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "Doesn't this castle seem weird all of a sudden? Something's not quite right.";
	close;
}
1@gl_k,154,74,3	duplicate(卡利斯格十字軍#13)	卡利斯格十字軍#14	4_F_KHALITZBURG

1@gl_k,145,69,6	script	純白騎士#15	4_WHITEKNIGHT,{
	mes "[純白騎士]";
	mes "I do not like to chat during work.";
	close;
}
1@gl_k,154,69,3	duplicate(純白騎士#15)	純白騎士#16	4_WHITEKNIGHT

1@gl_k,145,64,6	script	卡利斯格十字軍#17	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "...";
	close;
}
1@gl_k,154,64,3	duplicate(卡利斯格十字軍#17)	卡利斯格十字軍#18	4_F_KHALITZBURG

1@gl_k,145,59,6	script	純白騎士#19	4_WHITEKNIGHT,{
	mes "[純白騎士]";
	mes "What can I do for you?";
	close;
}
1@gl_k,154,59,3	duplicate(純白騎士#19)	純白騎士#20	4_WHITEKNIGHT

1@gl_k,145,54,6	script	卡利斯格十字軍#21	4_F_KHALITZBURG,{
	mes "[卡利斯格十字軍]";
	mes "Is there something you need?";
	close;
}
1@gl_k,154,54,3	duplicate(卡利斯格十字軍#21)	卡利斯格十字軍#22	4_F_KHALITZBURG

1@gl_k,149,100,6	script	海因里希#1	4_F_KHALITZBURG,{
	if (is_party_leader() == false) {
		mes "[海因里希]";
		mes "你們的隊長呢? 我有很重要的事情跟他說.";
		cutin "gl_heinrich2",2;
		close3;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	cutin "gl_heinrich2",2;
	select("海因里希，這座城堡...");
	mes "[" + .@player_name$ + "]";
	mes "你知道這城中發生了什么嗎，海因里希﹖";
	unittalk .@account_id, .@player_name$ + " : 你知道這城中發生了什么嗎，海因里希﹖";
	next;
	mes "[海因里希]";
	mes "你就是那個和巴爾蒙德一起來的冒險者﹖";
	npctalk "海因里希﹕你就是那個和巴爾蒙德一起來的冒險者﹖";
	next;
	mes "[海因里希]";
	mes "找我有什么事﹖有什么問題么﹖";
	npctalk "海因里希﹕找我有什么事﹖有什么問題么﹖";
	next;
	select("尤米爾的心臟，希梅爾麥茲她...");
	mes "["+.@player_name$+"]";
	mes "亡者的巴基力，希梅爾麥茲正在尋找尤米爾的心臟，据說就藏在這座城堡內﹗";
	unittalk .@account_id, .@player_name$ + " : 亡者的巴基力，希梅爾麥茲正在尋找尤米爾的心臟，据說就藏在這座城堡內﹗";
	next;
	mes "[海因里希]";
	mes "哈哈。不錯，挺好笑的。說實話，你來這有什么事么﹖";
	npctalk "海因里希﹕哈哈。不錯，挺好笑的。說實話，你來這有什么事么﹖";
	cutin "gl_heinrich1",2;
	next;
	mes "[巴爾蒙德]";
	mes "我們不是在開玩笑。如果我估計正确的話，希梅爾麥茲應該快來了。";
	npctalk "巴爾蒙德﹕我們不是在開玩笑。如果我估計正确的話，希梅爾麥茲應該快來了。", instance_npcname("巴爾蒙德#1");
	cutin "gl_barmund2",2;
	next;
	mes "[巴爾蒙德]";
	mes "在她出現之前，我們必須把尤米爾的心臟碎片藏在安全的地方﹗";
	npctalk "巴爾蒙德﹕在她出現之前，我們必須把尤米爾的心臟碎片藏在安全的地方﹗", instance_npcname("巴爾蒙德#1");
	next;
	select("就算你不相信...");
	mes "[" + .@player_name$ + "]";
	mes "就算你不相信這事，這樣做總沒有坏處。我們已經沒有太多時間了﹗";
	unittalk .@account_id, .@player_name$ + " : 就算你不相信這事，這樣做總沒有坏處。我們已經沒有太多時間了﹗";
	cutin "",255;
	next;
	mes "[海因里希]";
	mes "謝謝你們的好意，但國王現在不在這。";
	npctalk "海因里希﹕謝謝你們的好意，但國王現在不在這。";
	cutin "gl_heinrich1",2;
	next;
	mes "[海因里希]";
	mes "我不能隨便下令處置如此重要的物品。";
	npctalk "海因里希﹕我不能隨便下令處置如此重要的物品。";
	close2;
	disablenpc instance_npcname("海因里希#1");
	enablenpc instance_npcname("海因里希#2");
	enablenpc instance_npcname("希梅爾麥茲#1");
	donpcevent instance_npcname("#talkinstance1") + "::OnEnable";
	cutin "",255;
	end;
}

1@gl_k,152,97,3	script	巴爾蒙德#1	4_M_BARMUND,{ end; }
1@gl_k,149,97,5	duplicate(巴爾蒙德#1)	海因里希#2	4_F_KHALITZBURG
1@gl_k,149,100,5	duplicate(巴爾蒙德#1)	海因里希#3	4_F_KHALITZBURG
1@gl_k,149,89,1	duplicate(巴爾蒙德#1)	希梅爾麥茲#1	4_F_HIMEL

// Control Timer
//============================================================
1@gl_k,1,1,0	script	#talkinstance1	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	mapannounce 'map_name$[0], "????'s: 啊哈哈哈哈哈~!", bc_map,0xFFFF00,FW_NORMAL,18;
	initnpctimer;
	'npc_himelmez1$ = instance_npcname("希梅爾麥茲#1");
	'npc_varmundt1$ = instance_npcname("巴爾蒙德#1");
	'npc_heinrich2$ = instance_npcname("海因里希#2");
	'npc_heinrich3$ = instance_npcname("海因里希#3");
	end;
OnTimer1500:
	npctalk "海因里希﹕是誰﹖﹗", 'npc_heinrich2$;
	end;
OnTimer4500:
	npctalk "希梅爾麥茲﹕呵呵~我是不是妨礙你們說話了﹖客人來了這么久都沒發現，這可是很失禮的事情啊...", 'npc_himelmez1$;
	end;
OnTimer10000:
	npctalk "巴爾蒙德﹕希梅爾麥茲﹗﹗什么時候來的﹖", 'npc_varmundt1$;
	end;
OnTimer17500:
	npctalk "希梅爾麥茲﹕我是麗莎•卡恩•希梅爾麥茲，人們都叫我不死者的祭司，無頭騎士的統治者。", 'npc_himelmez1$;
	end;
OnTimer22000:
	npctalk "海因里希﹕你的意思是你能操控死者﹖你看起來是一位优雅的女性，但是我們可不懂得接待淑女...", 'npc_heinrich2$;
	end;
OnTimer28500:
	npctalk "希梅爾麥茲﹕不要故作鎮定了，如果你知道自己的國王現在的情況，估計就不會這么悠閑的在站在這里了。", 'npc_himelmez1$;
	end;
OnTimer36000:
	npctalk "海因里希﹕你剛剛說什么﹖﹗", 'npc_heinrich2$;
	end;
OnTimer41000:
	npctalk "希梅爾麥茲﹕呵呵呵，這就沉不住气了么﹖真是個有趣的男人。", 'npc_himelmez1$;
	end;
OnTimer47000:
	npctalk "希梅爾麥茲﹕雖說你是個沒有野心，一直對自己國王盡忠的愚蠢之人，但你的這份純真我很欣賞。", 'npc_himelmez1$;
	end;
OnTimer54000:
	npctalk "希梅爾麥茲﹕真希望我們不是在這种情況下相見，太可惜了~", 'npc_himelmez1$;
	end;
OnTimer59000:
	npctalk "海因里希﹕我們的國王只是受邀前往盧恩米德加茲王國，現在還沒有回來而已。", 'npc_heinrich2$;
	end;
OnTimer66500:
	npctalk "海因里希﹕不過你剛剛這番話是什么意思，國王發生了什么事﹖", 'npc_heinrich2$;
	end;
OnTimer71500:
	npctalk "希梅爾麥茲﹕這個嘛...真不湊巧，雖然我很想慢慢說給你听，但我已經沒有太多時間了。", 'npc_himelmez1$;
	end;
OnTimer78000:
	npctalk "希梅爾麥茲﹕我需要繼續做我應該做的事了，有什么問題和你自己的部下慢慢聊吧，呵呵呵~", 'npc_himelmez1$;
	end;
OnTimer84500:
	npctalk "海因里希﹕難道她已經知道尤米爾心臟的位置了﹖", 'npc_heinrich2$;
	disablenpc 'npc_himelmez1$;
	end;
OnTimer90500:
	npctalk "海因里希﹕卡利斯格十字軍們，純白騎士們，跟我走...", 'npc_heinrich2$;
	end;
OnTimer92000:
	for (.@i = 1; .@i <= 20; .@i += 4) {
		hideonnpc instance_npcname("卡利斯格十字軍#" + .@i);
		hideonnpc instance_npcname("卡利斯格十字軍#" + (.@i+1));
		hideonnpc instance_npcname("純白騎士#" + (.@i+2));
		hideonnpc instance_npcname("純白騎士#" + (.@i+3));
	}
	hideonnpc instance_npcname("卡利斯格十字軍#21");
	hideonnpc instance_npcname("卡利斯格十字軍#22");

	.@label$ = instance_npcname("#talkinstance1") + "::OnMyMobDead";
	monster 'map_name$[0],145,59,"墮落的深淵騎士",2470,1,.@label$;// MG_KNIGHT_OF_ABYSS
	monster 'map_name$[0],154,59,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],145,69,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],154,69,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],145,79,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],154,79,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],145,89,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],154,89,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],145,99,"墮落的深淵騎士",2470,1,.@label$;
	monster 'map_name$[0],154,99,"墮落的深淵騎士",2470,1,.@label$;

	monster 'map_name$[0],145,54,"痛苦的卡利斯格",2471,1,.@label$;// G_MG_KHALITZBURG
	unittalk $@mobid[0],"水... 誰能給我些水...";
	monster 'map_name$[0],154,54,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...呃...";
	monster 'map_name$[0],145,64,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"海因里希長官，救命﹗";
	monster 'map_name$[0],154,64,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我找不到...";
	monster 'map_name$[0],145,74,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"不...不...我不要死...";
	monster 'map_name$[0],154,74,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...呃。";
	monster 'map_name$[0],145,84,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我好渴啊.";
	monster 'map_name$[0],154,84,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我還不想死...";
	monster 'map_name$[0],145,94,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"身体好不舒服，呃﹗";
	monster 'map_name$[0],154,94,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我是誰...";
	monster 'map_name$[0],145,104,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"啊...我的身体...";
	monster 'map_name$[0],154,104,"痛苦的卡利斯格",2471,1,.@label$;
	unittalk $@mobid[0],"我餓了﹗";
	end;
OnTimer94000:
	npctalk "海因里希﹕怎么回事﹗我的部下們...這怎么可能﹗", 'npc_heinrich2$;
	end;
OnTimer96500:
	npctalk "巴爾蒙德﹕海因里希，長官﹗振作點﹗這不是該憂傷的時候。", 'npc_varmundt1$;
	end;
OnTimer100000:
	npctalk "海因里希﹕怎么會...", 'npc_heinrich2$;
	end;
OnTimer103000:
	npctalk "海因里希﹕對不起﹗請原諒我﹗", 'npc_heinrich2$;
	end;
OnTimer106000:
	mapannounce 'map_name$[0], "海因里希﹕怎么會...", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109000:
	mapannounce 'map_name$[0], "海因里希﹕對不起﹗請原諒我﹗", bc_map,0xFFFF33,FW_NORMAL,15;
	end;
OnTimer109500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#1");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#2");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#1");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#2");
	end;
OnTimer110000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#3");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#4");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#5");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#6");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#3");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#4");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#5");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#6");
	end;
OnTimer110500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#7");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#8");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#9");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#10");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#7");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#8");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#9");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#10");
	end;
OnTimer111000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#11");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#12");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#13");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#14");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#11");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#12");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#13");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#14");
	end;
OnTimer111500:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#15");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#16");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#17");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#18");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#15");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#16");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#17");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#18");
	end;
OnTimer112000:
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#19");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("純白騎士#20");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#21");
	specialeffect EF_GRANDCROSS,AREA, instance_npcname("卡利斯格十字軍#22");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#19");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("純白騎士#20");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#21");
	specialeffect EF_LEXAETERNA,AREA, instance_npcname("卡利斯格十字軍#22");
	disablenpc 'npc_heinrich2$;
	enablenpc 'npc_heinrich3$;
	killmonster 'map_name$[0], instance_npcname("#talkinstance1") + "::OnMyMobDead";
	end;
OnTimer112500:
	npctalk "海因里希﹕我親手殺了自己的部下...", 'npc_heinrich3$;
	end;
OnTimer115500:
	npctalk "巴爾蒙德﹕我們要快點阻止席梅爾梅斯﹗", 'npc_varmundt1$;
	end;
OnTimer118500:
	npctalk "巴爾蒙德﹕你還是站在我們這邊的吧﹗希望你能幫我們。", 'npc_varmundt1$;
	end;
OnTimer121500:
	npctalk "海因里希﹕巴爾蒙德是對的，現在不能沉浸在自己的懊悔之中。", 'npc_heinrich3$;
	end;
OnTimer124500:
	npctalk "海因里希﹕士兵們，現在集合，跟著我。", 'npc_heinrich3$;
	end;
OnTimer127500:
	npctalk "海因里希﹕希梅爾麥茲會讓人類陷入噩夢，然后轉化為不死怪物。", 'npc_heinrich3$;
	end;
OnTimer130500:
	npctalk "海因里希﹕說不定這附近還會有生還者。", 'npc_heinrich3$;
	end;
OnTimer134500:
	npctalk "海因里希﹕請你幫助我們搜救生還者，并將他們帶离這里。", 'npc_heinrich3$;
	end;
OnTimer138500:
	npctalk "海因里希﹕巴爾蒙德和我現在要去追擊希梅爾麥茲。", 'npc_heinrich3$;
	end;
OnTimer143500:
	npctalk "海因里希﹕走吧，巴爾蒙德，不能讓她造成更多傷亡。", 'npc_heinrich3$;
	end;
OnTimer147500:
	disablenpc 'npc_heinrich3$;
	end;
OnTimer148500:
	stopnpctimer;
	donpcevent instance_npcname("#ghmemorialmob01") + "::OnStart";

	disablenpc 'npc_varmundt1$;
	disablenpc instance_npcname("#talkinstance1");
	'npc_himelmez1$ = 'npc_varmundt1$ = 'npc_heinrich2$ = 'npc_heinrich3$ = "";
	end;
OnMyMobDead:
	end;
}

// Sector 1 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob01	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob01");
	enablenpc instance_npcname("#2Control");
	enablenpc instance_npcname("#2Control2");
	mapannounce 'map_name$[0], "9點鐘方向的大門已經開啟..", bc_map,0xFFFF00;

	// note: x17 MG_GHOUL / x17 MG_ZOMBIE / x17 MG_WRAITH
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	areamonster 'map_name$[0],76,99,87,10,"痛苦的侍從長",2466,14,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"墮落的修道士",2465,17,.@label$;
	areamonster 'map_name$[0],67,39,12, 6,"痛苦的侍從長",2466,3,.@label$;
	areamonster 'map_name$[0],32,75,51,58,"墮落的管家",2464,5,.@label$;
	areamonster 'map_name$[0],45,84,6,137,"墮落的管家",2464,12,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob01") + "::OnMyMobDead";
	.@mob_dead_num = 51 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 35) {
		mapannounce 'map_name$[0], "看起來希梅爾麥茲吸取了這些死者的靈魂，現在找找看有沒有生還者吧。", bc_map,0xFFFFFF;
		killmonster 'map_name$[0], .@label$;
		enablenpc instance_npcname("候補的服事少年#1");
		disablenpc instance_npcname("#ghmemorialmob01");
	}
	end;
}

1@gl_k,17,51,3	script	候補的服事少年#1	4_M_KID1,{
	if (is_party_leader() == false) {
		mes "[候補的服事少年]";
		mes "Save me, save me, please...";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[候補的服事少年]";
	mes "救命﹗救命﹗﹗﹗";
	next;
	select("嘿，冷靜點﹗這還有其他生還者么﹖");
	mes "["+.@player_name$+"]";
	mes "嘿，冷靜點﹗還有其他人么﹖";
	unittalk .@account_id, .@player_name$ + " : 嘿，冷靜點﹗還有其他人么﹖";
	next;
	mes "[候補的服事少年]";
	mes "侍從們...僧侶們...他們都變成了怪物，我只能躲起來。";
	npctalk "候補的服事少年﹕侍從們...僧侶們...他們都變成了怪物，我只能躲起來。";
	next;
	mes "[候補的服事少年]";
	mes "我只能在這躲著...一點都不敢動...";
	npctalk "候補的服事少年﹕我只能在這躲著...一點都不敢動...";
	next;
	select("冷靜﹗");
	mes "["+.@player_name$+"]";
	mes "冷靜﹗現在往東走，從中央大廳出去后就安全了﹗";
	unittalk .@account_id, .@player_name$ + " : 冷靜﹗現在往東走，從中央大廳出去后就安全了﹗";
	next;
	mes "[候補的服事少年]";
	mes "東邊﹖一個人么﹖現在就走﹖";
	npctalk "候補的服事少年﹕東邊﹖一個人么﹖現在就走﹖";
	next;
	select("我可以帶你過去。");
	mes "["+.@player_name$+"]";
	mes "我帶你過去吧。如果你害怕的話，閉上眼睛跟著走。";
	unittalk .@account_id, .@player_name$ + " : 我帶你過去吧。如果你害怕的話，閉上眼睛跟著走。";
	next;
	mes "[候補的服事少年]";
	mes "好的，我會努力。";
	npctalk "候補的服事少年﹕好的，我會努力。";
	disablenpc instance_npcname("候補的服事少年#1");
	donpcevent instance_npcname("#ghmemorialmob02") + "::OnStart";
	close;
}

// Sector 2 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob02	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob02");
	enablenpc instance_npcname("#3Control");
	enablenpc instance_npcname("#3Control2");
	for ( .@i = 1; .@i <= 26; .@i++ )
		enablenpc instance_npcname("已經死掉的人#" + .@i);
	mapannounce 'map_name$[0], "3點方向的傳送點已開放，現在可以前往區域3了", bc_map,0xFFFF00;

	// note: x34 MG_ZOMBIE / x35 MG_GHOUL
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	areamonster 'map_name$[0],236,12,288,52,"憤怒的敲擊者",2466,18,.@label$;// MG_GHOUL
	areamonster 'map_name$[0],236,12,288,52,"腐爛的鐵匠",2464,17,.@label$;// MG_ZOMBIE
	areamonster 'map_name$[0],242,71,286,145,"憤怒的敲擊者",2466,17,.@label$;
	areamonster 'map_name$[0],242,71,286,145,"腐爛的鐵匠",2464,17,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob02") + "::OnMyMobDead";
	.@mob_dead_num = 69 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 56) {
		mapannounce 'map_name$[0], "看起來希梅爾麥茲吸取了這些死者的靈魂，現在找找看有沒有生還者吧。", bc_map,0xFFFFFF;
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("精煉工匠的母親#1");
		disablenpc instance_npcname("#ghmemorialmob02");
	}
	end;
}

1@gl_k,291,145,3	script	精煉工匠的母親#1	4_F_JOB_BLACKSMITH,{
	if (is_party_leader() == false) {
		mes "[精煉工匠的母親]";
		mes "有人么﹖這里需要幫助...";
		close;
	}
	.@account_id = getcharid(3);
	.@player_name$ = strcharinfo(0);
	mes "[精煉工匠的母親]";
	mes "呀﹗﹗死吧﹗﹗﹗";
	npctalk "精煉工匠的母親﹕呀﹗﹗死吧﹗﹗﹗";
	specialeffect EF_CRASHEARTH;
	next;
	select("別擔心﹗");
	mes "["+.@player_name$+"]";
	mes "不要緊張﹗女士，你一個人么﹖還有其他生還者么﹖";
	unittalk .@account_id, .@player_name$ + " : 不要緊張﹗女士，你一個人么﹖還有其他生還者么﹖";
	next;
	mes "[精煉工匠的母親]";
	mes "這里只有我一個了。";
	npctalk "精煉工匠的母親﹕這里只有我一個了。";
	next;
	select("這里現在非常危險。");
	mes "["+.@player_name$+"]";
	mes "這個地方非常危險。你知道中央通道吧﹖我看你應該還能走過去...從那出去就安全了。";
	unittalk .@account_id, .@player_name$ + " : 這個地方非常危險。你知道中央通道吧﹖我看你應該還能走過去...從那出去就安全了。";
	next;
	mes "[精煉工匠的母親]";
	mes "好的，我還能動，為了我的孩子我一定要回去。";
	npctalk "精煉工匠的母親﹕好的，我還能動，為了我的孩子我一定要回去。";
	next;
	select("一定要活下去...");
	mes "["+.@player_name$+"]";
	mes "我相信你一定能堅持下去的。但我現在不能幫你了。";
	unittalk .@account_id, .@player_name$ + " : 我相信你一定能堅持下去的。但我現在不能幫你了。";
	next;
	mes "[精煉工匠的母親]";
	mes "沒關系，謝謝你來救我，我能一個人過去，祝你好運﹗";
	npctalk "精煉工匠的母親﹕沒關系，謝謝你來救我，我能一個人過去，祝你好運﹗";
	disablenpc instance_npcname("精煉工匠的母親#1");
	donpcevent instance_npcname("#ghmemorialmob03") + "::OnEnable";
	close;
}

// Tramp Mobs
//============================================================
1@gl_k,221,82,3	script	已經死掉的人#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"蛆虫",2467,.@mobs;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,233,123,4	duplicate(已經死掉的人#1)	已經死掉的人#2	4_M_DIEMAN,5,5
1@gl_k,258,150,2	duplicate(已經死掉的人#1)	已經死掉的人#3	4_M_DIEMAN,5,5
1@gl_k,255,157,5	duplicate(已經死掉的人#1)	已經死掉的人#4	4_M_DIEMAN,5,5
1@gl_k,280,167,4	duplicate(已經死掉的人#1)	已經死掉的人#5	4_M_DIEMAN,5,5
1@gl_k,293,161,2	duplicate(已經死掉的人#1)	已經死掉的人#6	4_M_DIEMAN,5,5
1@gl_k,249,101,3	duplicate(已經死掉的人#1)	已經死掉的人#7	4_M_DIEMAN,5,5
1@gl_k,241,86,6	duplicate(已經死掉的人#1)	已經死掉的人#8	4_M_DIEMAN,5,5
1@gl_k,246,62,0	duplicate(已經死掉的人#1)	已經死掉的人#9	4_M_DIEMAN,5,5
1@gl_k,240,43,7	duplicate(已經死掉的人#1)	已經死掉的人#10	4_M_DIEMAN,5,5
1@gl_k,271,19,1	duplicate(已經死掉的人#1)	已經死掉的人#11	4_M_DIEMAN,5,5
1@gl_k,282,48,5	duplicate(已經死掉的人#1)	已經死掉的人#12	4_M_DIEMAN,5,5
1@gl_k,285,81,7	duplicate(已經死掉的人#1)	已經死掉的人#13	4_M_DIEMAN,5,5
1@gl_k,276,106,0	duplicate(已經死掉的人#1)	已經死掉的人#14	4_M_DIEMAN,5,5
1@gl_k,261,164,0	duplicate(已經死掉的人#1)	已經死掉的人#15	4_M_DIEMAN,5,5
1@gl_k,269,173,0	duplicate(已經死掉的人#1)	已經死掉的人#16	4_M_DIEMAN,5,5
1@gl_k,252,120,0	duplicate(已經死掉的人#1)	已經死掉的人#17	4_M_DIEMAN,5,5
1@gl_k,213,63,0	duplicate(已經死掉的人#1)	已經死掉的人#18	4_M_DIEMAN,5,5
1@gl_k,222,39,0	duplicate(已經死掉的人#1)	已經死掉的人#19	4_M_DIEMAN,5,5
1@gl_k,214,27,0	duplicate(已經死掉的人#1)	已經死掉的人#20	4_M_DIEMAN,5,5
1@gl_k,223,17,1	duplicate(已經死掉的人#1)	已經死掉的人#21	4_M_DIEMAN,5,5
1@gl_k,230,50,2	duplicate(已經死掉的人#1)	已經死掉的人#22	4_M_DIEMAN,5,5
1@gl_k,235,16,3	duplicate(已經死掉的人#1)	已經死掉的人#23	4_M_DIEMAN,5,5
1@gl_k,226,96,4	duplicate(已經死掉的人#1)	已經死掉的人#24	4_M_DIEMAN,5,5
1@gl_k,222,119,5	duplicate(已經死掉的人#1)	已經死掉的人#25	4_M_DIEMAN,5,5
1@gl_k,251,20,6	duplicate(已經死掉的人#1)	已經死掉的人#26	4_M_DIEMAN,5,5

// Sector 3 Mobs
//============================================================
1@gl_k,1,1,0	script	#ghmemorialmob03	HIDDEN_WARP_NPC,{
	end;
OnEnable:
	enablenpc instance_npcname("#ghmemorialmob03");
	enablenpc instance_npcname("#4Control");
	enablenpc instance_npcname("#4Control2");
	mapannounce 'map_name$[0], "12點方向的傳送門(區域3內)已開放。", bc_map,0xFFFF00;

	// note: x50 MG_RAYDRIC / x50 MG_RAYDRIC_ARCHER
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	areamonster 'map_name$[0],16,186,43,280,"憤怒的弓箭手",2469,25,.@label$;
	areamonster 'map_name$[0],44,191,89,225,"飢餓的近衛兵",2468,25,.@label$;
	areamonster 'map_name$[0],115,215,188,273,"腐敗的近衛兵",2468,20,.@label$;
	areamonster 'map_name$[0],108,232,193,281,"腐敗的近衛兵",2468,5,.@label$;
	areamonster 'map_name$[0],158,236,230,250,"徘徊的弓箭手",2469,12,.@label$;
	areamonster 'map_name$[0],65,231,155,256,"徘徊的弓箭手",2469,13,.@label$;
	end;
OnMyMobDead:
	.@label$ = instance_npcname("#ghmemorialmob03") + "::OnMyMobDead";
	.@mob_dead_num = 100 - mobcount('map_name$[0],.@label$);
	if (.@mob_dead_num > 85) {
		killmonster 'map_name$[0],.@label$;
		enablenpc instance_npcname("#Mimelon");
		enablenpc instance_npcname("希梅爾麥茲#2");
		enablenpc instance_npcname("巴爾蒙德#2");
		enablenpc instance_npcname("海因里希#4");
		disablenpc instance_npcname("#ghmemorialmob03");
		mapannounce 'map_name$[0], "希梅爾麥茲﹕不錯嘛，我還以為你們會死在我的不死軍團手里~", bc_map,0xFFFFFF;
		initnpctimer;
	}
	end;
OnTimer5000:
	mapannounce 'map_name$[0], "希梅爾麥茲: 我在北面等你, 本來是想看看你有沒有運气走到這里.", bc_map,0xFFFFFF;
	stopnpctimer;
	end;
}

// 1st MVP
//============================================================
1@gl_k,144,258,6	script	海因里希#4	4_F_KHALITZBURG,{
	mes "[海因里希]";
	mes "希梅爾麥茲...你殺了我這么多部下，我永遠不會忘記這些。";
	cutin "gl_heinrich1",2;
	close3;
}

// note: never hidden
1@gl_k,156,258,3	script	巴爾蒙德#2	4_M_BARMUND,{
	cutin "gl_barmund1",2;
	mes "[巴爾蒙德]";
	if (checkquest(12318,HUNTING) != 2) {
		mes "我們一定要想辦法阻止這一切﹗";
		close3;
	}
	erasequest 12318;
	if (isbegin_quest(12319) == 0)
		setquest 12319;// Amdarais Hunt
	if (isbegin_quest(12320) == 0) {
		setquest 12320;// Time Traveler
		completequest 12320;
		getexp 250000,250000;
	}
	mes "這些東西帶有一种非比尋常的魔力，如果能夠利用的話，肯定會對你有很大的幫助。";
	getitem 6607,1;// Temporal_Crystal
	getitem 6608,1;// Coagulated_Spell
	close3;
}

1@gl_k,150,257,3	script	希梅爾麥茲#2	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[希梅爾麥茲]";
	mes "別這樣盯著我看，他們這不是得到了永久的安息么...";
	close3;
}

1@gl_k,150,257,0	script	#Mimelon	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Mimelon");
	initnpctimer;
	'npc_himelmez2$ = instance_npcname("希梅爾麥茲#2");
	'npc_heinrich4$ = instance_npcname("海因里希#4");
	end;
OnTimer3000:
	npctalk "希梅爾麥茲﹕能跟著我到這，運气挺不錯嘛。", 'npc_himelmez2$;
	end;
OnTimer6000:
	npctalk "希梅爾麥茲﹕不過也就到此為止了。", 'npc_himelmez2$;
	end;
OnTimer9000:
	npctalk "希梅爾麥茲﹕現在，你們全都要死。", 'npc_himelmez2$;
	end;
OnTimer12000:
	npctalk "海因里希﹕希梅爾麥茲﹗你別想再進一步。", 'npc_heinrich4$;
	end;
OnTimer15000:
	npctalk "希梅爾麥茲﹕哈哈哈，你們覺得我會一個人過來么﹖", 'npc_himelmez2$;
	end;
OnTimer18000:
	npctalk "巴爾蒙德﹕海因里希長官﹗看起來有未知的有強力生物在靠近﹗", instance_npcname("巴爾蒙德#2");
	end;
OnTimer21000:
	npctalk "海因里希﹕這...這是什么﹖﹗", 'npc_heinrich4$;
	end;
OnTimer24000:
	npctalk "希梅爾麥茲﹕給你們看看我的新玩具，你們就好好一起玩玩吧。", 'npc_himelmez2$;
	end;
OnTimer27000:
	npctalk "希梅爾麥茲﹕另外~，很期待再次見到你哦，海因里希。", 'npc_himelmez2$;
	end;
OnTimer28000:
	disablenpc 'npc_himelmez2$;
	end;
OnTimer31000:
	npctalk "海因里希﹕巴爾蒙德﹗幫一下冒險者們，我先去追希梅爾麥茲了﹗", 'npc_heinrich4$;
	end;
OnTimer32000:
	disablenpc 'npc_heinrich4$;
	end;
OnTimer35000:
	mapannounce 'map_name$[0], "回聲來自于騎士神殿的深處。", bc_map,0xFFFFFF;
	end;
OnTimer38000:
	donpcevent instance_npcname("#ghmemorialmob04") + "::OnStart";
	stopnpctimer;
	'npc_himelmez2$ = 'npc_heinrich4$ = "";
	end;
}

1@gl_k,1,1,0	script	#ghmemorialmob04	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob04");
	monster 'map_name$[0],150,259,"墮落之根",2475,1, instance_npcname("#ghmemorialmob04") + "::OnMyMobDead";// MG_CORRUPTION_ROOT
	unittalk $@mobid[0],"呃~~啊~~~";
	end;
OnMyMobDead:
	if (mobcount('map_name$[0], instance_npcname("#ghmemorialmob04") + "::OnMyMobDead") < 1) {
		mapannounce 'map_name$[0], "12點方向的傳送門已打開，現在可以從中央大廳到二層了", bc_map,0xFFFF00;
		npctalk "巴爾蒙德﹕我從怪物身上找到了一些奇怪的東西，有興趣的人可以來找我來拿。", instance_npcname("巴爾蒙德#2");

		enablenpc instance_npcname("#2F Entrance");
		enablenpc instance_npcname("#1 Control");
		enablenpc instance_npcname("#22 Control");
		enablenpc instance_npcname("#22 Control2");
		enablenpc instance_npcname("#Servanton");
		enablenpc instance_npcname("海因里希#21");
		enablenpc instance_npcname("巴爾蒙德#21");
		donpcevent instance_npcname("#ghmemorialmob05") + "::OnStart";
		disablenpc instance_npcname("#ghmemorialmob04");
	}
	end;
}

// Floor 2
//============================================================
2@gl_k,148,67,1	script	海因里希#21	4_F_KHALITZBURG,{
	mes "[海因里希]";
	mes "希梅爾麥茲的結界，";
	mes "這里到處都是...";
	cutin "gl_heinrich1",2;
	close3;
}

2@gl_k,151,71,7	script	巴爾蒙德#21	4_M_BARMUND,{
	mes "[巴爾蒙德]";
	mes "我怎么又在做這事﹖這好像并不是第一次，我在夢中重复見過這個場景。這個感覺...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,150,66,0	script	#Servanton_effect	HIDDEN_WARP_NPC,{
	end;
OnEffect:
	specialeffect EF_LORD;
	end;
}

// note: this part can be skipped
2@gl_k,150,66,0	script	#Servanton	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Servanton");
	initnpctimer;
	'npc_heinrich$ = instance_npcname("海因里希#21");
	'npc_varmundt$ = instance_npcname("巴爾蒙德#21");
	end;
OnTimer3000:
	npctalk "海因里希﹕這座城堡里還從未有過這些東西﹗", 'npc_heinrich$;
	end;
OnTimer6000:
	npctalk "巴爾蒙德﹕希梅爾麥茲的結界完全阻絕了我們的去路。", 'npc_varmundt$;
	end;
OnTimer9000:
	npctalk "海因里希﹕普通的武器根本無法破坏它們，我試過了。", 'npc_heinrich$;
	end;
OnTimer12000:
	npctalk "巴爾蒙德﹕請讓開一下，我來嘗試破坏它們。", 'npc_varmundt$;
	end;
OnTimer15000:
	specialeffect EF_LORD,AREA, instance_npcname("#Servanton_effect");
	disablenpc instance_npcname("#Servanton_effect");
	end;
OnTimer18000:
	npctalk "巴爾蒙德﹕似乎有一邊的結界被破坏了。", 'npc_varmundt$;
	end;
OnTimer21000:
	npctalk "巴爾蒙德﹕我怎么對這個結界有些印象", 'npc_varmundt$;
	end;
OnTimer24000:
	npctalk "巴爾蒙德﹕這些結界并不是依靠魔法刻印存在的。", 'npc_varmundt$;
	end;
OnTimer27000:
	npctalk "巴爾蒙德﹕希梅爾麥茲使用的是魔石，這些魔石提供了維持結界所需的能量。", 'npc_varmundt$;
	end;
OnTimer30000:
	npctalk "巴爾蒙德﹕我們需要找到持有魔石的不死怪物，消滅它們并摧毀魔石。", 'npc_varmundt$;
	end;
OnTimer33000:
	npctalk "巴爾蒙德﹕但我們無法感受到魔石的确切位置，所以需要盡量多的消滅不死怪物。", 'npc_varmundt$;
	end;
OnTimer36000:
	npctalk "海因里希﹕又要親手...真是無法原諒﹗", 'npc_heinrich$;
	end;
OnTimer39000:
	npctalk "海因里希﹕犧牲的人實在太多了，還不知道接下來會有多少...", 'npc_heinrich$;
	end;
OnTimer42000:
	npctalk "巴爾蒙德﹕長官...", 'npc_varmundt$;
	end;
OnTimer45000:
	npctalk "巴爾蒙德﹕長官，你還不能在這個時候憂傷。", 'npc_varmundt$;
	end;
OnTimer48000:
	npctalk "巴爾蒙德﹕而且他們已經成為怪物了，不是我們以前的同伴了。", 'npc_varmundt$;
	end;
OnTimer51000:
	npctalk "巴爾蒙德﹕他們不會再變回來了。", 'npc_varmundt$;
	end;
OnTimer54000:
	npctalk "海因里希: ...", 'npc_heinrich$;
	end;
OnTimer57000:
	npctalk "巴爾蒙德﹕走吧，繼續前進。", 'npc_varmundt$;
	end;
OnTimer60000:
	npctalk "巴爾蒙德﹕冒險者們，謝謝你們一路幫到這，接下來也希望得到你們的幫助。", 'npc_varmundt$;
	end;
OnTimer63000:
	npctalk "巴爾蒙德﹕前面將是更加艱難的戰斗，最好先准備准備。", 'npc_varmundt$;
	end;
OnTimer66000:
	npctalk "巴爾蒙德﹕好了，我們出發吧，海因里希長官。", 'npc_varmundt$;
	end;
OnTimer69000:
	mapannounce 'map_name$[1], "通往西邊區域的大門已在中央走廊打開.", bc_map,0xFFFF00;
	disablenpc 'npc_varmundt$;
	disablenpc 'npc_heinrich$;
	stopnpctimer;
	'npc_varmundt$ = 'npc_heinrich$ = "";
	end;
}

// Spots
// Note: timer is the only condition for them to respawn
2@gl_k,118,141,0	script	#ogh_2-1	HIDDEN_WARP_NPC,20,20,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@hidden_name$ = strnpcinfo(2);
	.@event_type = atoi( charat(.@hidden_name$,4) );
	if (.@event_type == 2)
		.@label$ = instance_npcname("#ghmemorialmob05") + "::OnMyMobDead";
	else
		.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	getmapxy .@map$,.@x,.@y, BL_NPC;
	monster .@map$,.@x,.@y, "腐敗的近衛兵", 2468,1, .@label$;
	monster .@map$,.@x,.@y, "死靈弓箭手", 2469,1, .@label$;
	monster .@map$,.@x,.@y, "墮落的深淵騎士", 2470,1, .@label$;
	monster .@map$,.@x,.@y, "痛苦的卡利斯格", 2471,1, .@label$;
	monster .@map$,.@x,.@y, "血腥騎士", 2472,1, .@label$;
	initnpctimer;
	end;
OnTimer30000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
OnStop:
	disablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,128,81,0	duplicate(#ogh_2-1)	#ogh_2-2	HIDDEN_WARP_NPC,20,20
2@gl_k,131,54,0	duplicate(#ogh_2-1)	#ogh_2-3	HIDDEN_WARP_NPC,20,20
2@gl_k,89,48,0	duplicate(#ogh_2-1)	#ogh_2-4	HIDDEN_WARP_NPC,20,20
2@gl_k,64,117,0	duplicate(#ogh_2-1)	#ogh_2-5	HIDDEN_WARP_NPC,20,20
2@gl_k,62,82,0	duplicate(#ogh_2-1)	#ogh_2-6	HIDDEN_WARP_NPC,20,20
2@gl_k,38,138,0	duplicate(#ogh_2-1)	#ogh_2-7	HIDDEN_WARP_NPC,20,20

2@gl_k,171,120,0	duplicate(#ogh_2-1)	#ogh_3-1	HIDDEN_WARP_NPC,20,20
2@gl_k,232,133,0	duplicate(#ogh_2-1)	#ogh_3-2	HIDDEN_WARP_NPC,20,20
2@gl_k,256,149,0	duplicate(#ogh_2-1)	#ogh_3-3	HIDDEN_WARP_NPC,20,20
2@gl_k,212,106,0	duplicate(#ogh_2-1)	#ogh_3-4	HIDDEN_WARP_NPC,20,20
2@gl_k,243,73,0	duplicate(#ogh_2-1)	#ogh_3-5	HIDDEN_WARP_NPC,20,20
2@gl_k,229,26,0	duplicate(#ogh_2-1)	#ogh_3-6	HIDDEN_WARP_NPC,20,20
2@gl_k,181,34,0	duplicate(#ogh_2-1)	#ogh_3-7	HIDDEN_WARP_NPC,20,20

// Commander 1
2@gl_k,1,1,0	script	#ghmemorialmob05	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_2-" + .@i);
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "邪惡的气息在此地愈發凝重。", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob05") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 41,146, 58,44, 122,148, 131,64;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "毀滅的第1騎士團長",2473,1, instance_npcname("#ghmemorialmob05") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "通往東邊區域的大門已在中央走廊打開.", bc_map,0xFFFF00;
	enablenpc instance_npcname("#23 Control");
	enablenpc instance_npcname("#23 Control2");
	donpcevent instance_npcname("#ghmemorialmob06") + "::OnStart";

	disablenpc instance_npcname("#ghmemorialmob05");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_2-" + .@i) + "::OnStop";
	end;
}

// Commander 2
2@gl_k,1,1,0	script	#ghmemorialmob06	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		enablenpc instance_npcname("#ogh_3-" + .@i);

	// Fix spawn
	.@label$ = instance_npcname("#ghmemorialmob06") + "::OnMyMobDead";
	monster 'map_name$[1],252, 75, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],253, 76, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],247, 77, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],248, 80, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],236,100, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],240,100, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],242,100, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],244,100, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],199,111, "腐敗的近衛兵",2468,1, .@label$;
	monster 'map_name$[1],181,107, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],177,110, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],236, 27, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],242, 27, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],252, 26, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],172,130, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],171,127, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],173,127, "徘徊的弓箭手",2469,1, .@label$;
	monster 'map_name$[1],177, 69, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],186, 66, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],189, 67, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],190, 68, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],257,157, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],246,159, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],237,158, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],206,159, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],172,120, "墮落的深淵騎士",2470,1, .@label$;
	monster 'map_name$[1],226, 30, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],226, 38, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],228, 29, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],226, 63, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],268,137, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],263,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],259,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],259,138, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],187,121, "痛苦的卡利斯格",2471,1, .@label$;
	monster 'map_name$[1],265, 52, "血腥騎士",2472,1, .@label$;
	monster 'map_name$[1],265, 55, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],264, 79, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],225, 92, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],237, 92, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],249, 92, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],256, 93, "血腥騎士",2472,1, .@label$; 
	monster 'map_name$[1],264,100, "血腥騎士",2472,1, .@label$;
	monster 'map_name$[1],181,120, "血腥騎士",2472,1, .@label$;
	end;

OnMyMobDead:
	if (rand(50) == 0) {// can re-spawn
		mapannounce 'map_name$[1], "邪惡的气息開始在此處蔓延。", bc_map,0xFFFF44,FW_NORMAL,15;
		killmonster 'map_name$[1], instance_npcname("#ghmemorialmob06") + "::OnBossDead";

		.@r = rand(4) * 2;
		setarray .@coord[0], 166,119, 211,45, 227,139, 245,74;
		monster 'map_name$[1],.@coord[.@r],.@coord[.@r+1], "毀滅的第2騎士團長",2474,1, instance_npcname("#ghmemorialmob06") + "::OnBossDead";
	}
	end;

OnBossDead:
	mapannounce 'map_name$[1], "中央走廊盡頭的大門已經打開。", bc_map,0xFFFF00;
	enablenpc instance_npcname("#24 Control");
	enablenpc instance_npcname("#24 Control2");
	enablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		enablenpc instance_npcname("腐爛的尸体#" + .@i);

	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ )
		donpcevent instance_npcname("#ogh_3-" + .@i) + "::OnStop";

	// Hidden mobs
	setarray .@coord[0],
		 37,265, 57,265, 77,265, 97,265, 117,265, 188,264, 208,264,
		 50,172, 70,172, 90,172, 110,172, 170,172, 210,172, 230,172,
		 88,214, 108,214, 128,214, 180,219, 200,219, 220,219, 240,219;
	.@size = getarraysize(.@coord);
	for ( .@i = 0; .@i < .@size; .@i += 2 )
		monster 'map_name$[1], .@coord[.@i], .@coord[.@i+1], "Flame of destruction",2337,1;// HIDDEN_MOB
	end;
}

// Amdarais Room Entrance
2@gl_k,150,180,0	script	#Geron	HIDDEN_WARP_NPC,7,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("#Geron");
	mapannounce 'map_name$[1], "???﹕不要過來﹗到處都是陷阱...啊﹗﹗﹗﹗", bc_map,0xFF7777,FW_NORMAL,15;
	enablenpc instance_npcname("希梅爾麥茲#22");
	enablenpc instance_npcname("巴爾蒙德#23");
	enablenpc instance_npcname("海因里希#23");
	enablenpc instance_npcname("格哈德#23");
	end;
}

2@gl_k,143,260,4	script	腐爛的尸体#1	4_M_DIEMAN,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname( strnpcinfo(0) );
	.@i = rand(1,10);
	if (.@i == 1) .@mobs = 3;
	else if (.@i == 2) .@mobs = 4;
	else if (.@i == 3) .@mobs = 5;
	else if (.@i < 7) .@mobs = 6;
	else .@mobs = 7;
	getmapxy .@map$,.@x,.@y, BL_NPC;
	specialeffect EF_VENOMDUST;
	monster .@map$,.@x,.@y,"Maggot",2467,.@mobs;
	initnpctimer;
	end;
OnTimer45000:
	enablenpc instance_npcname( strnpcinfo(0) );
	stopnpctimer;
	end;
}
2@gl_k,145,236,4	duplicate(腐爛的尸体#1)	腐爛的尸体#2	4_M_DIEMAN,5,5
2@gl_k,141,222,2	duplicate(腐爛的尸体#1)	腐爛的尸体#3	4_M_DIEMAN,5,5
2@gl_k,147,203,5	duplicate(腐爛的尸体#1)	腐爛的尸体#4	4_M_DIEMAN,5,5
2@gl_k,167,225,4	duplicate(腐爛的尸体#1)	腐爛的尸体#5	4_M_DIEMAN,5,5
2@gl_k,172,233,2	duplicate(腐爛的尸体#1)	腐爛的尸体#6	4_M_DIEMAN,5,5
2@gl_k,176,244,3	duplicate(腐爛的尸体#1)	腐爛的尸体#7	4_M_DIEMAN,5,5
2@gl_k,184,248,6	duplicate(腐爛的尸体#1)	腐爛的尸体#8	4_M_DIEMAN,5,5
2@gl_k,193,228,0	duplicate(腐爛的尸体#1)	腐爛的尸体#9	4_M_DIEMAN,5,5
2@gl_k,206,250,7	duplicate(腐爛的尸体#1)	腐爛的尸体#10	4_M_DIEMAN,5,5
2@gl_k,130,249,1	duplicate(腐爛的尸体#1)	腐爛的尸体#11	4_M_DIEMAN,5,5
2@gl_k,122,236,5	duplicate(腐爛的尸体#1)	腐爛的尸体#12	4_M_DIEMAN,5,5
2@gl_k,130,228,7	duplicate(腐爛的尸体#1)	腐爛的尸体#13	4_M_DIEMAN,5,5
2@gl_k,106,226,0	duplicate(腐爛的尸体#1)	腐爛的尸体#14	4_M_DIEMAN,5,5
2@gl_k,104,245,0	duplicate(腐爛的尸体#1)	腐爛的尸体#15	4_M_DIEMAN,5,5
2@gl_k,131,187,0	duplicate(腐爛的尸体#1)	腐爛的尸体#16	4_M_DIEMAN,5,5
2@gl_k,121,197,0	duplicate(腐爛的尸体#1)	腐爛的尸体#17	4_M_DIEMAN,5,5
2@gl_k,107,194,0	duplicate(腐爛的尸体#1)	腐爛的尸体#18	4_M_DIEMAN,5,5
2@gl_k,92,187,0	duplicate(腐爛的尸体#1)	腐爛的尸体#19	4_M_DIEMAN,5,5
2@gl_k,153,214,3	duplicate(腐爛的尸体#1)	腐爛的尸体#20	4_M_DIEMAN,5,5
2@gl_k,155,195,4	duplicate(腐爛的尸体#1)	腐爛的尸体#21	4_M_DIEMAN,5,5
2@gl_k,154,188,2	duplicate(腐爛的尸体#1)	腐爛的尸体#22	4_M_DIEMAN,5,5
2@gl_k,143,195,5	duplicate(腐爛的尸体#1)	腐爛的尸体#23	4_M_DIEMAN,5,5
2@gl_k,132,214,4	duplicate(腐爛的尸体#1)	腐爛的尸体#24	4_M_DIEMAN,5,5
2@gl_k,125,208,2	duplicate(腐爛的尸体#1)	腐爛的尸体#25	4_M_DIEMAN,5,5
2@gl_k,114,210,3	duplicate(腐爛的尸体#1)	腐爛的尸体#26	4_M_DIEMAN,5,5
2@gl_k,137,182,6	duplicate(腐爛的尸体#1)	腐爛的尸体#27	4_M_DIEMAN,5,5
2@gl_k,138,246,0	duplicate(腐爛的尸体#1)	腐爛的尸体#28	4_M_DIEMAN,5,5
2@gl_k,132,260,7	duplicate(腐爛的尸体#1)	腐爛的尸体#29	4_M_DIEMAN,5,5
2@gl_k,128,251,1	duplicate(腐爛的尸体#1)	腐爛的尸体#30	4_M_DIEMAN,5,5
2@gl_k,179,260,3	duplicate(腐爛的尸体#1)	腐爛的尸体#31	4_M_DIEMAN,5,5
2@gl_k,170,261,4	duplicate(腐爛的尸体#1)	腐爛的尸体#32	4_M_DIEMAN,5,5
2@gl_k,177,219,2	duplicate(腐爛的尸体#1)	腐爛的尸体#33	4_M_DIEMAN,5,5
2@gl_k,190,214,5	duplicate(腐爛的尸体#1)	腐爛的尸体#34	4_M_DIEMAN,5,5
2@gl_k,201,214,4	duplicate(腐爛的尸体#1)	腐爛的尸体#35	4_M_DIEMAN,5,5

// Amdarais Spawn
2@gl_k,153,250,8	script	海因里希#23	4_F_KHALITZBURG,{
	cutin "gl_heinrich1",2;
	mes "[海因里希]";
	mes "你在這里做什么? 希梅爾麥茲!";
	close3;
}

2@gl_k,162,250,1	script	巴爾蒙德#23	4_M_BARMUND,{
	mes "[巴爾蒙德]";
	mes "不能讓他們跑掉...";
	cutin "gl_barmund2",2;
	close3;
}

2@gl_k,158,255,1	script	格哈德#23	4_LEVITATEMAN,{
	mes "[格哈德]";
	mes "呃... 啊! 快帶著長官离開這里.";
	close;
}

2@gl_k,158,252,1	script	希梅爾麥茲#22	4_F_HIMEL,3,3,{
	cutin "gl_himel2",2;
	mes "[希梅爾麥茲]";
	mes "這太令人惊奇了，你一直都讓它留在這里。我可以說聲謝謝嗎﹖";
	close3;

OnTouch:
	end;
OnTouch_:
	disablenpc instance_npcname("希梅爾麥茲#22");
	enablenpc instance_npcname("希梅爾麥茲#23");
	initnpctimer;
	'npc_himelmez$ = instance_npcname("希梅爾麥茲#23");
	'npc_gerhalt$ = instance_npcname("格哈德#23");
	'npc_heinrich$ = instance_npcname("海因里希#23");
	'npc_varmundt$ = instance_npcname("巴爾蒙德#23");
	end;
OnTimer3000:
	npctalk "希梅爾麥茲﹕很好~我還以為你們沒辦法來到這里...", 'npc_himelmez$;
	end;
OnTimer6000:
	npctalk "格哈德﹕混帳﹗快跑﹗我快要堅持不住了﹗", 'npc_gerhalt$;
	end;
OnTimer9000:
	npctalk "海因里希: 格哈德!", 'npc_heinrich$;
	end;
OnTimer15000:
	npctalk "海因里希﹕你想對我的部下做什么，希梅爾麥茲﹖﹗", 'npc_heinrich$;
	end;
OnTimer18000:
	npctalk "希梅爾麥茲﹕呵呵，我已經找到尤米爾心臟碎片了，海因里希。", 'npc_himelmez$;
	end;
OnTimer21000:
	npctalk "希梅爾麥茲﹕沒有你們干扰的話，本來不需要花這么久時間。", 'npc_himelmez$;
	end;
OnTimer24000:
	npctalk "格哈德﹕長官...快走，你必須活下來...呃。.", 'npc_gerhalt$;
	end;
OnTimer27000:
	npctalk "希梅爾麥茲﹕你說怎么樣﹖就假裝這是一場瘟疫，席卷了國王和他的人民", 'npc_himelmez$;
	end;
OnTimer30000:
	npctalk "海因里希﹕希梅爾麥茲﹗有必要做到這一步么﹖﹗", 'npc_heinrich$;
	end;
OnTimer33000:
	npctalk "海因里希﹕我不會讓你再傷害我的部下﹗", 'npc_heinrich$;
	end;
OnTimer36000:
	npctalk "希梅爾麥茲﹕你真的這么天真﹖", 'npc_himelmez$;
	end;
OnTimer39000:
	npctalk "希梅爾麥茲﹕這一次就和你的部下在地獄相見吧，海因里希。", 'npc_himelmez$;
	end;
OnTimer42000:
	npctalk "希梅爾麥茲﹕看來你有點認不清形勢，這里沒人能夠阻止我。", 'npc_himelmez$;
	end;
OnTimer45000:
	npctalk "希梅爾麥茲﹕心情不錯，這次就讓你們見識我的新寵物吧，暗達萊斯。", 'npc_himelmez$;
	end;
OnTimer48000:
	npctalk "海因里希﹕我一定要讓你付出代价﹗", 'npc_heinrich$;
	end;
OnTimer51000:
	npctalk "希梅爾麥茲﹕哦~好可怕。", 'npc_himelmez$;
	end;
OnTimer54000:
	npctalk "希梅爾麥茲﹕哈哈，這次我玩得很開心，下次再見咯。", 'npc_himelmez$;
	end;
OnTimer57000:
	npctalk "希梅爾麥茲﹕還會再見的，不是么﹖", 'npc_himelmez$;
	end;
OnTimer60000:
	specialeffect EF_BARRIER, AREA, 'npc_gerhalt$;
	end;
OnTimer63000:
	disablenpc 'npc_himelmez$;
	end;
OnTimer65000:
	npctalk "格哈德﹕你可以控制我的身体，但你無法奪走我的靈魂，希梅爾麥茲﹗", 'npc_gerhalt$;
	end;
OnTimer66000:
	specialeffect EF_CHAINCOMBO, AREA, 'npc_gerhalt$;
	end;
OnTimer67000:
	npctalk "海因里希﹕希梅爾麥茲﹗﹗", 'npc_heinrich$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	end;
OnTimer70000:
	specialeffect EF_MAPPILLAR2, AREA, 'npc_gerhalt$;
	specialeffect EF_MAPPILLAR, AREA, 'npc_gerhalt$;
	disablenpc 'npc_heinrich$;
	npctalk "巴爾蒙德﹕沒辦法了，我們必須防止暗達萊斯造成更大的破坏﹗", 'npc_varmundt$;
	end;
OnTimer73000:
	mapannounce 'map_name$[1], "格哈德的身体發生了變化.", bc_map,0xFFFFFF;
	end;
OnTimer76000:
	specialeffect EF_LORD, AREA, 'npc_gerhalt$;
	disablenpc 'npc_varmundt$;
	mapannounce 'map_name$[1], "巴爾蒙德﹕沒辦法了，我們必須防止暗達萊斯造成更大的破坏﹗", bc_map,0xFFFF00;
	end;
OnTimer80000:
	disablenpc 'npc_gerhalt$;
	donpcevent instance_npcname("#ghmemorialmob07") + "::OnStart";
	stopnpctimer;
	'npc_himelmez$ = 'npc_gerhalt$ = 'npc_heinrich$ = 'npc_varmundt$ = "";
	end;
}

2@gl_k,158,252,4	script	希梅爾麥茲#23	4_F_HIMEL,{
	cutin "gl_himel2",2;
	mes "[希梅爾麥茲]";
	mes "太令人惊訝了, 你們竟然一直讓他待在里, 我能對你們說句謝謝嘛?";
	close3;
}

2@gl_k,1,1,0	script	#ghmemorialmob07	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob07");
	monster 'map_name$[1],158,255,"暗達萊斯",2476,1,instance_npcname("#ghmemorialmob07") + "::OnMyMobDead";// MG_AMDARAIS
	'boss_id = $@mobid[0];
	unittalk 'boss_id, "R走開... 不要靠近我...";
	initnpctimer;
	end;
OnTimer5000:
	unittalk 'boss_id, "我不想... 我不想傷害任何人...呃啊...";
	end;
OnTimer10000:
	unittalk 'boss_id, "殺了我! 求求你殺了我﹗";
	end;
OnTimer16000:
	unittalk 'boss_id, "呃...死吧...去死吧...";
	end;
OnTimer22000:
	unittalk 'boss_id, "毀滅... 死亡﹗...";
	end;
OnTimer55000:
	donpcevent instance_npcname("#ghmemorialmob08") + "::OnStart";// 巴爾蒙德 buffs and additionnal monsters
	stopnpctimer;
	end;

OnMyMobDead:
	if (mobcount('map_name$[1], instance_npcname("#ghmemorialmob07") + "::OnMyMobDead") < 1) {
		stopnpctimer;
		enablenpc instance_npcname("華金#21");
		enablenpc instance_npcname("#Secret Room Exit");

		stopnpctimer;
		disablenpc instance_npcname("#ghmemorialmob07");
		// note: monsters from #ghmemorialmob08 still alive on Amdarais's dead
	}
	end;
}

// 巴爾蒙德 Buffs
2@gl_k,1,1,0	script	#ghmemorialmob08	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("#ghmemorialmob08");
	initnpctimer;
	end;
OnTimer5000:
	if (unitexists('boss_id) == 0) {
		disablenpc instance_npcname("#ghmemorialmob08");
		stopnpctimer;
		end;
	}
	getunitdata 'boss_id, .@data;
	.@percent_hp = (.@data[UMOB_HP] * 100) / .@data[UMOB_MAXHP];
	mapannounce 'map_name$[1], "暗達萊斯的 HP 達到了 " + .@percent_hp + "% !", bc_map,0x70DBDB;

	// event type every 10%
	switch( .@percent_hp / 10 ) {
	case 10:
		break;
	case 9:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff2") + "::OnEvent";
		break;
	case 8:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"痛苦的卡利斯格",2471,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 7:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"墮落的深淵騎士",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 6:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"血腥騎士",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 5:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff2") + "::OnEvent";
		break;
	case 4:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"徘徊的弓箭手",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 3:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"血腥騎士",2472,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 2:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff3") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"墮落的深淵騎士",2470,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	case 1:
	case 0:
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff1") + "::OnEvent";
		donpcevent instance_npcname("巴爾蒙德的靈魂#Buff4") + "::OnEvent";
		areamonster 'map_name$[1],140,220,175,255,"徘徊的弓箭手",2469,6, instance_npcname("#ghmemorialmob08") + "::OnMobDead";
		break;
	}
	end;
OnTimer30000:
	killmonster 'map_name$[1], instance_npcname("#ghmemorialmob08") + "::OnMobDead";
	initnpctimer;
	end;
OnMobDead:
	end;
}

2@gl_k,150,247,5	script	巴爾蒙德的靈魂#Buff1	4_M_BARMUND,2,2,{
	end;
OnTouch:
	specialeffect2 EF_ENHANCE;
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		sc_start SC_GLASTHEIM_HIDDEN,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 2)
		sc_start SC_GLASTHEIM_STATE,30000,1,10000,SCSTART_NOTICKDEF;
	else if (.@num == 3) {
		sc_start SC_GLASTHEIM_HEAL,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_HPSP,30000,1,10000,SCSTART_NOTICKDEF;
	}
	else {
		sc_start SC_GLASTHEIM_DEF,30000,1,10000,SCSTART_NOTICKDEF;
		sc_start SC_GLASTHEIM_ITEMDEF,30000,1,10000,SCSTART_NOTICKDEF;
	}
	end;
OnEvent:
	initnpctimer;
	enablenpc instance_npcname( strnpcinfo(0) );
	.@num = atoi( replacestr(strnpcinfo(2), "Buff", "") );
	if (.@num == 1)
		npctalk "巴爾蒙德的靈魂: 暗達萊斯可能使用強大的魔法護盾﹗靠近我，我會保護你﹗";
	else if (.@num == 2)
		npctalk "巴爾蒙德的靈魂: 現在是攻擊的好机會, 一起進攻!";
	else if (.@num == 3)
		npctalk "巴爾蒙德的靈魂: 如果你不想被安達姆斯的僵尸攻擊就靠近我, 我來給你們足夠強大的力量!";
	else
		npctalk "巴爾蒙德的靈魂: 如果你不想被安達姆斯的僵尸攻擊就靠近我, 我來給你們足夠強大的力量!";
	end;
OnTimer10000:
	stopnpctimer;
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
2@gl_k,165,247,3	duplicate(巴爾蒙德的靈魂#Buff1)	巴爾蒙德的靈魂#Buff2	4_M_BARMUND,2,2
2@gl_k,150,232,8	duplicate(巴爾蒙德的靈魂#Buff1)	巴爾蒙德的靈魂#Buff3	4_M_BARMUND,2,2
2@gl_k,165,232,2	duplicate(巴爾蒙德的靈魂#Buff1)	巴爾蒙德的靈魂#Buff4	4_M_BARMUND,2,2


2@gl_k,158,241,1	script	華金#21	4_M_SAGE_C,{
	if (checkquest(12319,HUNTING) == 2) {// note: complete hunting 'Corrupted Soul Hunt' prerequis
		mes "[華金]";
		mes "哈哈，暗達萊斯身上找到了這些東西，拿去吧。";
		erasequest 12319;
		setquest 12322;// Space Distortion
		if (isbegin_quest(12321) == 0) {
			setquest 12321;// Time Conqueror
			completequest 12321;
			getitem 6607,5;// Temporal_Crystal
			getitem 6608,5;// Coagulated_Spell
			getexp 350000,350000;
		}
		else {
			getitem 6607,1;// Temporal_Crystal
			getitem 6608,1;// Coagulated_Spell
		}
		next;
		mes "[華金]";
		mes "我已經暫停了時間。但很快，時間裂縫就會消失。";
		specialeffect2 EF_BLIND;
		soundeffect "_blind.wav",0;
		next;
		mes "[華金]";
		mes "可怜的時間困者。";
		mes "他們會無止盡地嘗試阻止這一悲劇的發生。";
		next;
		mes "[華金]";
		mes "但是他們每一次都意識不到自己之前的所作所為。";
		mes "悲劇還是會照常發生...";
		next;
		mes "[華金]";
		mes "現在，我會消除你的記憶，也許下一次你會遇見不同的命運。";
		specialeffect2 EF_FREEZE;
		close2;
		warp 'map_name$[1],158,244;
		end;
	}
	mes "[華金]";
	mes "時間裂縫已經開始扭曲了。";
	mes "現在是時候走了﹗";
	mes "我會把你送出去的﹗";
	next;
	select("哈...再稍等一會儿...");
	mes "[華金]";
	mes "嗯，還有什么事么...";
	mes "時間裂隙即將關閉，在不出去就來不及了。";
	next;
	if (select("我還想稍微探索一下:請送我出去") == 1) {
		mes "[華金]";
		mes "你确定﹖這個地方很快就要崩塌了。請盡快回來..";
		close;
	}
	close2;
	warp "glast_01",204,270;
	end;
}

// Treasure Room
//============================================================
1@gl_k,165,136,3	script	奇怪的裂縫#2	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "裂紋看起來可疑，但沒有特別的。";
		close;
	}
	specialeffect EF_SPELLBREAKER;

	.@random = rand(1,4);
	switch( atoi(strnpcinfo(2)) ) {
	case 2:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 719,1,"this",165,138;
		makeitem 6608,1,"this",165,138;
		break;
	case 3:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 720,1,"this",159,138;
		if (rand(1,4) == 4)
			makeitem2 15066,1,"this",159,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",159,138;
		makeitem 7229,1,"this",159,138;
		break;
	case 4:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 721,1,"this",153,138;
		if (rand(1,4) == 4)
			makeitem2 13086,1,"this",153,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",153,138;
		makeitem 7230,1,"this",153,138;
		break;
	case 5:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 722,1,"this",147,138;
		if (rand(1,4) == 4)
			makeitem2 2949,1,"this",147,138,0,0,0,0,0,0,0;
		makeitem 6612,1,"this",147,138;
		makeitem 6613,1,"this",147,138;
		makeitem 6608,1,"this",147,138;
		break;
	case 6:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 725,1,"this",141,138;
		makeitem 7228,1,"this",141,138;
		if (rand(1,4) == 4)
			makeitem2 13440,1,"this",141,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",141,138;
		break;
	case 7:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 726,1,"this",135,138;
		if (rand(1,4) == 4)
			makeitem2 2022,1,"this",135,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",135,138;
		break;
	case 8:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 727,1,"this",129,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",129,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",129,138;
		break;
	case 9:
		for ( .@i = 1; .@i <= .@random; ++.@i )
			makeitem 727,1,"this",171,138;
		if (rand(1,4) == 4)
			makeitem2 21007,1,"this",171,138,0,0,0,0,0,0,0;
		makeitem 6608,1,"this",171,138;
		break;
	}
	disablenpc instance_npcname( strnpcinfo(0) );
	end;
}
1@gl_k,159,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#3	CLEAR_NPC
1@gl_k,153,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#4	CLEAR_NPC
1@gl_k,147,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#5	CLEAR_NPC
1@gl_k,141,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#6	CLEAR_NPC
1@gl_k,135,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#7	CLEAR_NPC
1@gl_k,129,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#8	CLEAR_NPC
1@gl_k,171,136,3	duplicate(奇怪的裂縫#2)	奇怪的裂縫#9	CLEAR_NPC

1@gl_k,269,267,3	script	奇怪的裂縫#1	CLEAR_NPC,{
	if (isbegin_quest(12322) == 0) {// Space Distortion
		mes "裂紋看起來可疑，但沒有特別的。";
		close;
	}
	warp 'map_name$[0],149,198;
	end;

OnInstanceInit:
	'map_name$[0] = instance_mapname("1@gl_k");
	'map_name$[1] = instance_mapname("2@gl_k");

	// Entrance
	disablenpc instance_npcname("希梅爾麥茲#1");
	disablenpc instance_npcname("巴爾蒙德#1");
	disablenpc instance_npcname("海因里希#1");
	disablenpc instance_npcname("海因里希#2");
	disablenpc instance_npcname("海因里希#3");

	// Rescue 1
	disablenpc instance_npcname("#ghmemorialmob01");
	disablenpc instance_npcname("候補的服事少年#1");

	// Rescue 2
	disablenpc instance_npcname("#ghmemorialmob02");
	disablenpc instance_npcname("精煉工匠的母親#1");
	for ( .@i = 1; .@i <= 26; .@i++ )
		disablenpc instance_npcname("已經死掉的人#" + .@i);

	// Sector 3
	disablenpc instance_npcname("#ghmemorialmob03");

	// Root of Corruption
	disablenpc instance_npcname("希梅爾麥茲#2");
	disablenpc instance_npcname("巴爾蒙德#2");
	disablenpc instance_npcname("海因里希#4");
	disablenpc instance_npcname("#Mimelon");
	disablenpc instance_npcname("#ghmemorialmob04");

	// Entrance 2nd map
	disablenpc instance_npcname("巴爾蒙德#21");
	disablenpc instance_npcname("海因里希#21");
	disablenpc instance_npcname("#Servanton");
	hideonnpc instance_npcname("#Servanton_effect");

	// Commanders
	disablenpc instance_npcname("#ghmemorialmob05");
	disablenpc instance_npcname("#ghmemorialmob06");
	for ( .@i = 1; .@i <= 7; .@i++ ) {
		disablenpc instance_npcname("#ogh_2-" + .@i);
		disablenpc instance_npcname("#ogh_3-" + .@i);
	}

	// Amdarais Room Entrance
	disablenpc instance_npcname("#Geron");
	for ( .@i = 1; .@i <= 35; .@i++ )
		disablenpc instance_npcname("腐爛的尸体#" + .@i);

	// Amdarais Spawn
	disablenpc instance_npcname("格哈德#23");
	disablenpc instance_npcname("希梅爾麥茲#22");
	disablenpc instance_npcname("希梅爾麥茲#23");
	disablenpc instance_npcname("巴爾蒙德#23");
	disablenpc instance_npcname("海因里希#23");
	disablenpc instance_npcname("#ghmemorialmob07");

	// 巴爾蒙德 Buffs
	disablenpc instance_npcname("#ghmemorialmob08");
	disablenpc instance_npcname("巴爾蒙德的靈魂#Buff1");
	disablenpc instance_npcname("巴爾蒙德的靈魂#Buff2");
	disablenpc instance_npcname("巴爾蒙德的靈魂#Buff3");
	disablenpc instance_npcname("巴爾蒙德的靈魂#Buff4");
	disablenpc instance_npcname("華金#21");

	// Warps
	disablenpc instance_npcname("#2Control");
	disablenpc instance_npcname("#2Control2");
	disablenpc instance_npcname("#3Control");
	disablenpc instance_npcname("#3Control2");
	disablenpc instance_npcname("#4Control");
	disablenpc instance_npcname("#4Control2");
	disablenpc instance_npcname("#2F Entrance");
	disablenpc instance_npcname("#1 Control");
	disablenpc instance_npcname("#22 Control");
	disablenpc instance_npcname("#22 Control2");
	disablenpc instance_npcname("#23 Control");
	disablenpc instance_npcname("#23 Control2");
	disablenpc instance_npcname("#24 Control");
	disablenpc instance_npcname("#24 Control2");
	disablenpc instance_npcname("#Secret Room Exit");
	end;
}
