//===== rAthena Script ======================================= 
//= Eclage Instance Script
//===== By: ================================================== 
//= Dastgir
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Eclage Interior Instance
//===== Additional Comments: ================================= 
//= 1.0 Converted from the official Aegis script. [Dastgir]
//= 1.1 Converted from Hercules script to rAthena script & Small bug fix. [Skorm]
//============================================================ 

ecl_hub01,132,12,3	script	參謀長#tl02	435,{
	.@party_id = getcharid(1);
	.@md_name$ = "艾卡拉奇 內部";

	if (.@party_id == 0 || is_party_leader() == false){
		mes "-! 警告 !-";
		mes "當前任務將在紀念館的地牢里進行.";
		mes "只有隊長能進入，請先組建你的隊伍.";
		close;
	}
	if (ep14_2_mylord == 29) {
		mes "[舍尼密]";
		mes "這個地方設置了照明燈照明.";
		mes "如果你喜歡這里，你就要謹慎些.";
		next;
		switch (select("我要進去.:還是算了.")) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[舍尼密]";
				mes "不久前，這個地方就派了很多士兵駐守..";
				mes "當然，我不希望你是來打扰他們的，對不﹖";
				close;
			}
			ecl_interior_time = gettimetick(2) + 20 * 60;
			mes "[舍尼密]";
			mes "由于這里面把關非常的嚴密，所以你應該有所准備。";
			mes "你先在這里等等吧...";
			close;
		case 2:
			mes "[Shenime]";
			mes "這次的任務并不是那么簡單.";
			close;
		}
	}
	mes "[高級的拉菲思]";
	mes "嗯? 這間房?";
	mes "沒事的，這些你不需要擔心.";
	close;
}

ecl_hub01,130,15,0	script	關閉的大門	844,{
	if (ep14_2_mylord == 29) {
		mes "這扇門通往廣場.";
		mes "這里面照明非常充足，因此里頭看起來會很漂亮.";
		next;
		if (select("我要進去.:還是算了.") == 1) {
			.@party_id = getcharid(1);
			.@md_name$ = "艾卡拉奇 內部";
			if (.@party_id == 0 || is_party_leader() == false) {
				mes "-! 警告 !-";
				mes "當前任務將在紀念館的地牢里進行.";
				mes "只有隊長能進入，請先組建你的隊伍.";
				close;
			}
			if (gettimetick(2) >= ecl_interior_time){
				mes "[舍尼密]";
				mes "里面的士兵看起來很緊張.";
				mes "你進去了之后就很難找到出來的路了.";
				close;
			}
			switch (instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "出現了未知的錯誤.";
				close;
			case IE_NOINSTANCE:
				mes "它是關閉著的.";
				close;
			case IE_OK:
				mapannounce "ecl_hub01",getpartyname(.@party_id) + " 的隊長 " + strcharinfo(0) + " 正在進入 " + .@md_name$,bc_map,"0x00ff99";
				end;
			default:
			}
		} else
			close;
	}
	mes "這扇門通往廣場.";
	mes "這里面照明非常充足，因此里頭看起來會很漂亮.";
	mes "它是關閉著的.";
	close;
}

1@ecl,58,69,3	script	舍尼密#ecl01	435,{
	mes "[舍尼密]";
	mes "啊，你是怎么來到這里的?";
	mes "他們摧毀了我們派過來的士兵部隊..";
	next;
	select("什么...?:讓我來擊退怪物!");
	mes "[舍尼密]";
	mes "沒關系.";
	mes "你的任務到此為止.";
	mes "你現在成了叛徒.";
	mes "咱們永別了!";
	specialeffect EF_BEGINSPELL3;
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "不......!!!";
	specialeffect2 EF_LORD;
	percentheal -99,0;
	soundeffect "wander_man_move.wav",1;
	sc_start SC_BLIND,60000,0;
	close2;
	warp instance_mapname("1@ecl"),146,95;
	end;

OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,51,64,1	script	可疑生物#1	2375,{
	end;
}
1@ecl,43,67,3	duplicate(可疑生物#1)	可疑生物#2	2375,0,0
1@ecl,55,80,3	duplicate(可疑生物#1)	可疑生物#3	2375,0,0
1@ecl,60,75,3	duplicate(可疑生物#1)	可疑生物#4	2375,0,0

1@ecl,42,80,5	script	可疑生物#5	2376,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
}
1@ecl,48,86,3	duplicate(可疑生物#5)	可疑生物#6	2376,0,0
1@ecl,58,82,3	duplicate(可疑生物#5)	可疑生物#7	2376,0,0
1@ecl,56,88,3	duplicate(可疑生物#5)	可疑生物#8	2376,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
	end;

OnInstanceInit:
	'mob_summoned = 0;
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	sleep 1000;
    	mapannounce instance_mapname("1@ecl"),"警告: 室內出現了异常狀況﹗﹗﹗室內出現了异常狀況﹗﹗﹗",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	sleep 2000;
    	mapannounce instance_mapname("1@ecl"),"可疑生物: 咳咳咳咳....嗯啊哈哈哈哈哈!!",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	hideoffnpc instance_npcname("可疑生物#5");
	hideoffnpc instance_npcname("可疑生物#6");
	hideoffnpc instance_npcname("可疑生物#7");
	hideoffnpc instance_npcname("可疑生物#8");
	sleep 3000;
	hideonnpc instance_npcname("#sndmaster");
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("Shenime#ecl01")+"::OnEnable";
		mapannounce instance_mapname("1@ecl"),"舍尼密: 很好, 我來的正是時候.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	}
	end;

OnTouch:
	if ('mob_summoned > 1) end;
    mes "一旦消除了這里的入侵者，就會出現其他可疑的生物.";
    next;
    mes "咱們無法与它們進行溝通，其中一個怪物看上去貌似很憤怒，還一直在尋找他被打敗了的伙伴.";
    next;
    mes "[" + strcharinfo(0) + "]";
    mes "他們看起來就像是夢羅克的化身.";
    mes "難道這就是惡魔之王???";
    mes "或者說這只是巧合?";
    next;
    mes "[惡魔之王的下屬]";
    mes "　啊啊啊....啊哈哈哈哈哈!!!";
    mes "　哇哇哇哇哇哇哇!";
    next;
    mes "看來戰爭是無法避免了!";
    mes "他們會殺了我的!";
	close2;
	hideonnpc instance_npcname("可疑生物#5");
	hideonnpc instance_npcname("可疑生物#6");
	hideonnpc instance_npcname("可疑生物#7");
	hideonnpc instance_npcname("可疑生物#8");
	disablenpc instance_npcname("可疑生物#5");
	disablenpc instance_npcname("可疑生物#6");
	disablenpc instance_npcname("可疑生物#7");
	disablenpc instance_npcname("可疑生物#8");
	.@eclmap$ = instance_mapname("1@ecl");
	.@sndmaster$ = instance_npcname("#sndmaster");
	monster .@eclmap$,42,80,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,48,86,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,59,82,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,56,88,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,39,72,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,42,69,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,47,64,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,50,60,"惡魔之王的下屬",2376,1,.@sndmaster$+"::OnMyMobDead";
	'mob_summoned = 2;
    end;
}

1@ecl,57,65,0	script	#event	139,5,5,{
	end;
OnTouch:
	if ('mob_summoned > 0) end;
	.@eclmap$ = instance_mapname("1@ecl");
	.@eventname$ = instance_npcname("#event");
	mes "房間里頭空空的，什么都媚陽.";
	mes "有一束光透過房間中央的圓形設備照射了進來.";
	next;
	mes "[可疑生物]";
	mes "咳咳咳咳咳咳咳咳......";
	next;
	mes "一群來歷不明的生物正在恐怖地接近當中.";
	mes "它們看起來很惊悚，也很難与其溝通.";
	next;
	mes "[可疑生物]";
	mes "　呃呃呃...　...喀喀喀喀喀喀喀!!";
	next;
	mes "它們貌似要發起攻擊了.";
	mes "我們不能參与到戰斗當中!";
	close2;
	hideonnpc instance_npcname("可疑生物#1");
	hideonnpc instance_npcname("可疑生物#2");
	hideonnpc instance_npcname("可疑生物#3");
	hideonnpc instance_npcname("可疑生物#4");
	disablenpc instance_npcname("可疑生物#1");
	disablenpc instance_npcname("可疑生物#2");
	disablenpc instance_npcname("可疑生物#3");
	disablenpc instance_npcname("可疑生物#4");
	monster .@eclmap$,51,64,"可疑生物",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,43,67,"可疑生物",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,55,80,"可疑生物",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,60,75,"可疑生物",2375,1,.@eventname$+"::OnMyMobDead";
	'mob_summoned = 1;
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
		enablenpc instance_npcname("#sndmaster");
		donpcevent instance_npcname("#sndmaster")+"::OnEnable";
	}
	end;
}

1@ecl,148,97,3	script	阿凡#	618,{
	mes "[阿凡]";
	mes "Hi, 你看這是誰!";
	mes "你就是我儿子的朋友?";
	mes "我們能在這里相遇這真是奇遇啊!";
	cutin "avant01",1;
	next;
	mes "[阿凡]";
	mes "你怎么了嗎﹖";
	mes "傳來了一陣微妙而神奇的回音...";
	mes "接著，你就在這里被遺棄了...";
	next;
	if (select("這都是因為你!:舍尼密說...") == 1) {
		mes "[阿凡]";
		mes "我根本就不懂你在說什么.";
		mes "你就不能把你的故事告訴我嗎﹖難道你不知道我們這里有'5個W和1個H'的規則﹖";
		next;
	}
	mes "[阿凡]";
	mes "Oh...這到底是發生了什么.";
	mes "你說舍尼密是......?";
	next;
	mes "[阿凡]";
	mes "呵呵呵...哈哈哈哈，啊哈哈哈哈哈";
	mes "很好, 非常好...did he get me, too?";
	mes "Me, Avant, of all the people?! 哈哈哈!";
	cutin "avant02",1;
	next;
	select("你說的是啥意思?");
	mes "[阿凡]";
	mes "我正在從事對Orb力量的研究.";
	mes "舍尼密只是讓我繼續堅持罷了.";
	cutin "avant01",1;
	next;
	mes "[阿凡]";
	mes "我最開始研究它是因為它很美，久而久之我就很想知道它到底蘊含了怎樣的力量...";
	mes "當我們都從中獲取利益的時候，我就開始怀疑Orb的能力了.";
	next;
	mes "[阿凡]";
	mes "這里所有人都認為Orb是很正常的東西，就如同空气和水一般，但是正因為這個讓我從不同的角度去看待它，并開始調查他的來歷.";
	mes "沒錯...舍尼密. 他故意引起我的好奇心.";
	next;
	mes "[阿凡]";
	mes "沒人不知道空气和水的存在，這使得他清楚地認清了事實并展開調查!";
	mes "嗯...這不是很有趣嗎!";
	cutin "avant02",1;
	next;
	mes "[阿凡]";
	mes "哈...哈哈哈...";
	mes "...他利用我這件事讓我覺得很生气, 但更糟糕的是，我已經對我的子民做了一些很可怕的事情.";
	mes "我現在很惱火.";
	next;
	select("你還有良知?");
	mes "[阿凡]";
	mes "我很認真的問你一句，你認識我的儿子，那你從我儿子身上得知...";
	mes "我是個怎樣的人﹖﹗";
	mes "你說吧，我不會責怪你的.";
	cutin "avant01",1;
	next;
	mes "[阿凡]";
	mes "...呸.";
	mes "這不好笑.";
	mes "這一點都不好笑. 我對你完全失去了興趣了....";
	next;
	mes "[阿凡]";
	mes "......";
	mes "好像有人來了....";
	next;
	cutin "minuel01",4;
	mes "[馬吉爾]";
	mes "" + strcharinfo(0) + "Sir,";
	mes "雖然你因涉嫌在照明室最近發生的事件而被拘留，";
	mes "有目擊者証明你是無辜的.";
	next;
	mes "[馬吉爾]";
	mes "我并不了解細節的部分,";
	mes "但我依然堅信你是無辜的.";
	mes "你受傷過重了，我會把你送去治療室進行治療.";
	next;
	mes "[馬吉爾]";
	mes "我不認為這事有什么大不了的.";
	mes "...祈禱.";
	mes "我帶你走吧...";
	close2;
	cutin "",255;
	warp "1@ecl",146,29;
	end;
}
	
1@ecl,148,29,3	script	洛基#ecl01	512,{
	mes "[洛基]";
	mes "我旁邊這家伙說只會說不會做.";
	mes "你為什么不先向他解釋?";
	close;
}	
	
1@ecl,147,30,3	script	尼德豪克#ecl01	510,{
	mes "[森林守護者]";
	mes "我認為你應該先跟海歇爾說說.";
	close;
}
	
1@ecl,144,27,5	script	海歇爾#ecl01	623,{
	cutin "hisie01",0;
	mes "[海歇爾]";
	mes "你能告訴我到底發生了什么事嗎?";
	next;
	select("我曾經与怪物戰斗過.");
	cutin "hisie03",0;
	mes "[海歇爾]";
	mes "怪物﹖夢洛克的可疑生物﹖";
	mes "跟你戰斗的其實是我們的士兵--Laphines!!";
	mes "地球上到底發生了什么?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "正因如此.";
	mes "" + strcharinfo(0) + "他的身体內 -";
	mes "充滿了舍尼密的力量.";
	next;
	mes "啊 洛基的手, 變小了、又變瘦了、還長滿了刺.";
	next;
	cutin "hisie03",0;
	mes "[海歇爾]";
	mes "舍尼密, 參謀長?";
	mes "這跟他有什么關系?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[Loki]";
	mes "舍尼密是夢羅克的其中一個下屬.";
	mes "他的這种力量能夠讓他通過他身体的一部分直接操控別人的精神.";
	next;
	select("現在讓我想一想...");
	mes "[" + strcharinfo(0) + "]";
	mes "來這之前，舍尼密讓我跟他握手，那時候我只感覺到了一陣火花.";
	mes "我當時還以為這只是我的幻想而沒有管那么多，或許那個時候他正在.......";
	next;
	mes "[尼德豪克]";
	mes "你想的沒錯...";
	mes "他們用了邪惡的招數.";
	mes "蒙蔽了你的雙眼...";
	next;
	cutin "hisie03",0;
	mes "[海歇爾]";
	mes "所以你說他欺騙了我們?";
	mes "不會是參謀長的，他一直都跟我們待在一起...";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "他應該是被這影響了好一段時間了.";
	mes "不管怎樣，這家伙就是不認罪.";
	mes "我們要拿出証据.";
	next;
	mes "[尼德豪克]";
	mes "我也要向依格德拉修的監護人發誓.";
	mes "" + strcharinfo(0) + "- 絕不會做出這樣的事情.";
	next;
	cutin "hisie02",0;
	mes "[海歇爾]";
	mes "啊...";
	mes "但貌似事情失去控制了.";
	mes "我不希望是這樣.";
	next;
	select("Orb發生了什么事?");
	mes "[海歇爾]";
	mes "核心被偷走了.";
	mes "沒有人知道Orb的核心去了哪里,";
	mes "所以這就是為什么卡爾沒能在這儿.";
	npcskill "AL_HEAL",9,99,80;
	next;
	mes "[海歇爾]";
	mes "另外，因為你，有几名士兵都受傷了.";
	mes "幸運的是，他們沒有死，只是受了很重的傷.";
	mes "現在的情形變得不是那么理想了.";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "必須要有人來承擔這個責任.";
	mes "想必," + strcharinfo(0) + "就是你了.";
	next;
	cutin "hisie02",0;
	mes "[海歇爾]";
	mes "......這兩位先生會處理在Orb的所有相關收費,";
	mes "但你和卡爾的責任就在所難免了.";
	mes "你必須去抓住舍尼密.";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "hisie01",0;
	mes "[海歇爾]";
	mes "休息得差不多了，咱們走吧.";
	mes "卡爾正在等著你呢.";
	mes "那你們倆呢?";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "不用擔心我們.";
	mes "我們的目標是惡魔之王夢洛克.";
	mes "值得欣慰的是，我們在這里找到了証据.";
	next;
	cutin "hisie01",0;
	mes "[海歇爾]";
	mes "謝謝你.";
	mes "我們現在就触發.";
	mes "出發吧." + strcharinfo(0) + ".";
	ep14_2_mylord = 30;
	changequest 7450,7451;
	close2;
	cutin "",255;
	warp "ecl_in03",57,76;
	instance_destroy;
	end;
}
