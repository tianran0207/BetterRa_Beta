//===== rAthena Script =======================================
//= War of Emperium (Payon) - Sacred Altar
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= NPCs that relate to Payon Guild Castle 4
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Removed dialog from interior flags. [L0ne_W0lf]
//=     Fixed position of Guild dungeon switch.
//= 1.3 Partially rewrote Script. [Masao]
//= 1.4 Corrected guild name. [Euphy]
//============================================================

// Guild Flags (Outside Castle)
//============================================================
-	script	紅樓#1::OutsideFlagsPA4	722,{

	set .@GID, GetCastleData("payg_cas04",1);

	if (.@GID == 0) {
		mes "[ 神圣盧恩米德加茲王國條例 ]";
		mes " ";
		mes "1. 按照神圣盧恩米德加茲王國的條例,";
		mes "我們正式聲明此城堡沒有正式擁有者.";
		mes " ";
		mes "2. 經過所有試煉,";
		mes "并且破坏了華麗金屬的勇士.";
		mes "將會得到國王授予的城堡擁有權";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[ 回聲 ]";
		mes "勇士們...";
		mes "想要回到你們的勇气所在之地嗎?";
		next;
		if(select("返回公會城堡.:取消.") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("payg_cas04",1)) {
					warp "payg_cas04",40,235;
				end;
			}
			end;
		}
		close;
	}
	mes "[ 神圣盧恩米德加茲王國條例 ]";
	mes " ";
	mes "1. 按照神圣盧恩米德加茲王國的條例,";
	mes "我們批准該地區作為公會 ^ff0000"+GetGuildName(.@GID)+"^000000 的私有領地.";
	mes " ";
	mes "2. ^ff0000"+GetGuildName(.@GID)+"^000000 公會的領袖為:";
	mes "^ff0000"+GetGuildMaster(.@GID)+"^000000";
	mes "對此結果有意見的人，請用你手中的武器說話，証明你的勇气，捍衛你的光榮﹗";
	close;

OnRecvCastlePy04:
	FlagEmblem GetCastleData("payg_cas04",1);
	end;
}

pay_gld,137,160,0	duplicate(OutsideFlagsPA4)	紅樓#1-2	722
pay_gld,143,160,0	duplicate(OutsideFlagsPA4)	紅樓#2	722
pay_gld,133,151,2	duplicate(OutsideFlagsPA4)	紅樓#3	722
pay_gld,153,166,1	duplicate(OutsideFlagsPA4)	紅樓#4	722

// Guild Flags (Inside Castle)
//============================================================
-	script	紅樓#5::InsideFlagsPA4	722,{
	end;

OnRecvCastlePy04:
	FlagEmblem GetCastleData("payg_cas04",1);
	end;
}

payg_cas04,255,259,0	duplicate(InsideFlagsPA4)	紅樓#5-2	722
payg_cas04,248,259,0	duplicate(InsideFlagsPA4)	紅樓#6	722
payg_cas04,248,168,6	duplicate(InsideFlagsPA4)	紅樓#7	722
payg_cas04,248,160,6	duplicate(InsideFlagsPA4)	紅樓#8	722
payg_cas04,232,181,4	duplicate(InsideFlagsPA4)	紅樓#9	722
payg_cas04,239,181,4	duplicate(InsideFlagsPA4)	紅樓#10	722

// Guild Flag (Town)
//============================================================
payon,118,322,4	duplicate(InsideFlagsPA4)	紅樓#11	722
payon,166,165,3	duplicate(InsideFlagsPA4)	紅樓#12	722

// Kafra Employee
//============================================================
payg_cas04,40,235,1	duplicate(guildkafra)	卡普拉 職員#payg_cas04	117

// Steward
//============================================================
payg_cas04,38,284,3	duplicate(cm)	帝杰#payg_cas04	55

// Treasure Room Exit
//============================================================
payg_cas04,151,47,0	script	#lever_PA04	111,{
	mes " ";
	mes "這里有一個小小的控制杆, 去拉一下嗎?";
	next;
	if(select("上前拉杆.:算了.") == 1) {
		close2;
		warp "payg_cas04",38,285;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
payg_cas04,52,48,0	duplicate(gdlever)	#payg_cas04	111

// Guild Treasure Spawns
//============================================================
payg_cas04,147,48,0	duplicate(Gld_Trea_Spawn)	公會寶箱#payg_cas04	-1

// AGIT Manager
//============================================================
payg_cas04,271,29,0	duplicate(Gld_Agit_Manager)	Agit#payg_cas04	-1

// Guardians
//============================================================
payg_cas04,38,284,3	duplicate(Gld_Guard_Template)	城堡監護人#payg_cas04	-1
