//===== rAthena Script =======================================
//= War of Emperium (Prontera) - Fadhgridh
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= NPCs that relate to Prontera Guild Castle 3
//===== Additional Comments: =================================
//= 1.0 First Version. No longer uses functions. [L0ne_W0lf]
//= 1.1 Corrected flag-in spawn point. [L0ne_W0lf]
//= 1.2 Corrected treasure room switch coordinates. [L0ne_W0lf]
//= 1.3 Removed dialog from interior flags. [L0ne_W0lf]
//= 1.4 Partially rewrote Script. [Masao]
//============================================================

// Guild Flags (Outside Castle)
//============================================================
-	script	拉茲立克斯#1::OutsideFlagsPR3	722,{

	set .@GID, GetCastleData("prtg_cas03",1);

	if (.@GID == 0) {
		mes "[ 神圣盧恩米德加茲王國條例 ]";
		mes " ";
		mes "1. 按照神圣盧恩米德加茲王國的條例,";
		mes "我們正式聲明此城堡沒有正式擁有者.";
		mes " ";
		mes "2. 經過所有試煉,";
		mes "并且破坏了華麗金屬的勇士.";
		mes "將會得到國王授予的城堡擁有權";
		close;
	}
	if (getcharid(2) == .@GID) {
		mes "[ 回聲 ]";
		mes "勇士們...";
		mes "想要回到你們的勇气所在之地嗎?";
		next;
		if(select("返回公會城堡.:取消.") == 1) {
			close2;
			if (getcharid(2) == GetCastleData("prtg_cas03",1)) {
				warp "prtg_cas03",181,215;
				end;
			}
			end;
		}
		close;
	}
	mes "[ 神圣盧恩米德加茲王國條例 ]";
	mes " ";
	mes "1. 按照神圣盧恩米德加茲王國的條例,";
	mes "我們批准該地區作為公會 ^ff0000"+GetGuildName(.@GID)+"^000000 的私有領地.";
	mes " ";
	mes "2. ^ff0000"+GetGuildName(.@GID)+"^000000 公會的領袖為:";
	mes "^ff0000"+GetGuildMaster(.@GID)+"^000000";
	mes "對此結果有意見的人，請用你手中的武器說話，証明你的勇气，捍衛你的光榮﹗";
	close;

OnRecvCastlePt03:
	FlagEmblem GetCastleData("prtg_cas03",1);
	end;
}

prt_gld,147,140,4	duplicate(OutsideFlagsPR3)	拉茲立克斯#1-2	722
prt_gld,147,136,4	duplicate(OutsideFlagsPR3)	拉茲立克斯#2	722
prt_gld,158,140,4	duplicate(OutsideFlagsPR3)	拉茲立克斯#3	722
prt_gld,158,136,4	duplicate(OutsideFlagsPR3)	拉茲立克斯#4	722

// Guild Flags (Inside Castle)
//============================================================
-	script	拉茲立克斯#5::InsideFlagsPR3	722,{
	end;

OnRecvCastlePt03:
	FlagEmblem GetCastleData("prtg_cas03",1);
	end;
}

prtg_cas03,168,28,4	duplicate(InsideFlagsPR3)	拉茲立克斯#5-2	722
prtg_cas03,182,28,4	duplicate(InsideFlagsPR3)	拉茲立克斯#6	722
prtg_cas03,43,50,4	duplicate(InsideFlagsPR3)	拉茲立克斯#7	722
prtg_cas03,48,50,4	duplicate(InsideFlagsPR3)	拉茲立克斯#8	722
prtg_cas03,43,58,4	duplicate(InsideFlagsPR3)	拉茲立克斯#9	722
prtg_cas03,48,58,4	duplicate(InsideFlagsPR3)	拉茲立克斯#10	722
prtg_cas03,158,210,4	duplicate(InsideFlagsPR3)	拉茲立克斯#11	722
prtg_cas03,169,210,4	duplicate(InsideFlagsPR3)	拉茲立克斯#12	722
prtg_cas03,162,201,4	duplicate(InsideFlagsPR3)	拉茲立克斯#13	722
prtg_cas03,165,201,4	duplicate(InsideFlagsPR3)	拉茲立克斯#14	722

// Guild Flag (Town)
//============================================================
prontera,143,203,2	duplicate(InsideFlagsPR3)	拉茲立克斯#15	722

// Kafra Employee
//============================================================
prtg_cas03,181,215,4	duplicate(guildkafra)	卡普拉 職員#prtg_cas03	117

// Steward
//============================================================
prtg_cas03,51,100,4	duplicate(cm)	斯提克#prtg_cas03	55

// Treasure Room Exit
//============================================================
prtg_cas03,193,130,0	script	#Lever_PR03	111,{
	mes " ";
	mes "這里有一個小小的控制杆, 去拉一下嗎?";
	next;
	if(select("上前拉杆.:算了.") == 1) {
		close2;
		warp "prtg_cas03",51,101;
		end;
	}
	close;
}

// Guild Dungeon Switch
//============================================================
prtg_cas03,5,70,0	duplicate(gdlever)	#prtg_cas03	111

// Guild Treasure Spawns
//============================================================
prtg_cas03,189,132,0	duplicate(Gld_Trea_Spawn)	公會寶箱#prtg_cas03	-1

// AGIT Manager
//============================================================
prtg_cas03,17,221,0	duplicate(Gld_Agit_Manager)	Agit#prtg_cas03	-1

// Guardians
//============================================================
prtg_cas03,51,100,4	duplicate(Gld_Guard_Template)	城堡監護人#prtg_cas03	-1
