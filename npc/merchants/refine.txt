//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//= 3.5 Added Refine UI [Atemo, Lemongrass]
//============================================================

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	克理斯多福#1	63,{
	mes "[克理斯多福]";
	mes "你好, 冒險家!";
	mes "歡迎來到克里斯多福鐵匠鋪.";
	next;
	mes "[克理斯多福]";
	mes "這里販賣所有關于精煉用的物品.";
	mes "您需要什么呢?";
	next;
	switch(select("我想要買鐵砧:我想要買精煉的物品:我想要買金屬:我想要買制作原石:取消")) {
	case 1:
		mes "[克理斯多福]";
		mes "用來制作武器的鐵砧,";
		mes "當然是越堅硬的金屬越好.";
		mes "不過您應該也知道,";
		mes "愈堅硬的金屬价格上就愈貴.";
		mes "您可以根据自己的經濟情況選擇合适的.";
		next;
		switch(select("鐵砧 - 30,000z:神之金屬鐵砧 - 120,000z:黃金鐵砧 - 300,000z:更好的鐵砧:取消")) {
		case 1:
			if (Zeny < 30000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳";
				close;
			}
			getitem 986,1; // Anvil
			Zeny = Zeny-30000;
			mes "[克理斯多福]";
			mes "你選擇的這一种是比較便宜的一种.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[克理斯多福]";
			mes "噢!看來你是個行家。這一定是一個适合鐵匠的鐵砧，你說是嗎?";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳";
				close;
			}
			getitem 988,1; // Golden_Anvil
			Zeny = Zeny-300000;
			mes "[克理斯多福]";
			mes "這一個是工作室之中的最好的一种，用這种鐵砧，你絕對可以稱霸鐵匠界";
			close;
		case 4:
			mes "[克理斯多福]";
			mes "很抱歉!";
			mes "我這里沒有比黃金鐵砧更好的鐵砧了.";
			next;
			mes "[克理斯多福]";
			mes "或許傳說中的'Ringgel'有比這更好的鐵砧, 但是它只是一個傳說而已.";
			close;
		case 5:
			mes "[克理斯多福]";
			mes "如果有任何需要的話, ";
			mes "歡迎您再度光臨.";
			mes "請慢走~";
			close;
		}
	case 2:
		mes "[克理斯多福]";
		mes "偉大的鐵匠當然得要用偉大的道具。";
		mes "如果在克里斯多福鐵匠鋪購買道具的話，";
		mes "您一定可以成為一名偉大的鐵匠的。";
		mes "選選看那一种道具最何您的心意吧!";
		next;
		switch(select("攜帶用熔礦爐 - 150z:鋼鐵錘子 - 1000z:黃金錘子 - 3000z:神之金屬錘子 - 5000z:結束交易")) {
		case 1:
			mes "[克理斯多福]";
			mes "好的，請問您要購買多少數量?";
			mes "欲結束交易的話請按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易已經結束。請慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "請將購買數量減到500個以下。";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 150;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你沒辦法攜帶這么多東西唷!";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			getitem 613,1; // Iron_Hammer
			Zeny = Zeny-1000;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			getitem 614,1; // Golden_Hammer
			Zeny = Zeny-3000;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			getitem 615,1; // Oridecon_Hammer
			Zeny = Zeny-5000;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 5:
			mes "[克理斯多福]";
			mes "歡迎來到'克理斯多福'的工作室";
			close;
		}
	case 3:
		mes "[克理斯多福]";
		mes "我准備了很多高質量的金屬。現在你需要哪一种呢?";
		next;
		switch(select("強化武器金屬-級數一 - 200z:強化武器金屬-級數二 - 1000z:取消")) {
		case 1:
			mes "[克理斯多福]";
			mes "現在你需要買多少呢?如果你不想買任何東西，請輸入數字'0'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易取消";
					mes "再見!";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "只能購買500個或者更少";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 200;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你沒辦法攜帶這么多東西唷!";
				close;
			}
			getitem 1010,.@input; // Phracon
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 2:
			mes "[克理斯多福]";
			mes "現在你需要買多少呢?如果你不想買任何東西，請輸入數字'0'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易取消";
					mes "再見!";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "只能購買500個或者更少";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 1000;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我覺得這個价格已經是最低的了，因為這么低的价錢，所以也不允許賒帳……";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你沒辦法攜帶這么多東西唷!";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感謝您的光臨，歡迎下次光臨!";
			close;
		case 3:
			mes "[克理斯多福]";
			mes "歡迎來到'克理斯多福'的工作室";
			close;
		}
	case 4:
		mes "[克理斯多福]";
		mes "我這里可以制作优質的金屬礦石，5個神之金屬原石可以制作成一個神之金屬，5個鋁原石可以制作成一個鋁，你打算做哪一种呢?";
		next;
		switch(select("制作神之金屬:制作鋁:取消")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[克理斯多福]";
				mes "我告訴過你5個神之金屬原石才能做一個神之金屬";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[克理斯多福]";
				mes "給你一個神之金屬，歡迎你下次再來!";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[克理斯多福]";
				mes "我告訴過你5個鋁原石才能換一個鋁";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[克理斯多福]";
				mes "給你一個鋁，歡迎你下次再來!";
				close;
			}
		case 3:
			mes "[克理斯多福]";
			mes "你需要的時候，歡迎隨時再來，再見!";
			close;
		}
	case 5:
		mes "[克理斯多福]";
		mes "歡迎隨時來'克理斯多福'的工作室.";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	Paul Spanner	63,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Welcome, my friend.";
	mes "In my shop, you will find everything that you need in forging.";
	mes "Tell me what you need.";
	next;
	switch(select("Purchase Anvil.:Purchase Forging Items.:Purchase Metal.:Process Ores.:Quit.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "Anvil is the most necessary item for Blacksmiths.";
		mes "Since you will use an Anvil more than once, you'd better buy a nice one.";
		next;
		switch(select("Anvil - 30,000z.:Oridecon Anvil - 120,000z.:Golden Anvil - 300,000z.:I need a better anvil.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 986,1; //Anvil
			Zeny = Zeny-30000;
			mes "[Paul Spanner]";
			mes "It is the cheapest anvil which has the most basic ability.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[Paul Spanner]";
			mes "Ah, you have an eye for anvil. A Blacksmith needs an anvil at least as good as this.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			Zeny = Zeny-300000;
			mes "[Paul Spanner]";
			mes "I can tell your ambition to become a good Blacksmith just by looking at you to choose this Golden Anvil!";
			mes "This anvil will surely aid you in creating the best weapons.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "I am sorry, but I do not sell better anvils than Golden Anvil.";
			mes "Unless you find the legendary anvil of 'Linggell', I don't think that you could find better one than Golden Anvil in any other places.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "You need various materials to process ores and to forge weapons.";
		mes "I have everything that you need. Take a look.";
		next;
		switch(select("Mini Furnace - 150z.:Iron Hammer - 1,000z.:Golden Hammer - 3,000z.:Oridecon Hammer - 5,000z.:Cancel.")) {
		case 1:
			.@item = 612;
			.@item_cost = 150;
			.@item_weight = 200;
			mes "[Paul Spanner]";
			mes "You definately need this furnce to process ores!";
			next;
			break;
		case 2:
			.@item = 613;
			.@item_cost = 1000;
			.@item_weight = 200;
			break;
		case 3:
			.@item = 614;
			.@item_cost = 3000;
			.@item_weight = 300;
			break;
		case 4:
			.@item = 615;
			.@item_cost = 5000;
			.@item_weight = 400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "You have canceled the trade. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can only buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first.";
			close;
		}
		Zeny = Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "I have high quality metal.";
		mes "So, which metal would you like to buy?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1,000z.:Quit.")) {
		case 1:
			.@item = 1010;
			.@item_price = 200;
			break;
		case 2:
			.@item = 1011;
			.@item_price = 1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many of them do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "The trade has been canceled. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first?";
			close;
		}
		getitem .@item,.@input;
		Zeny = Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "I can process Oridecon and Elunium for you.";
		mes "You need 5 ores to process them into one Oridecon or Elunium.";
		mes "So, which one do you want to process?";
		switch(select("Oridecon:Elunium:Quit.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Elunium.";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "If you need anything, just let me know.";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	Hollgrehenn	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[Hollgrehenn]";
		mes "I'm a blacksmith who can refine weapons and equipment.";
		mes "Do you have any items that you'd like to refine?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Hollgrehenn",0;
		end;
	}
}

morocc_in,73,38,6	script	亞拉甘	4W_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[亞拉甘]";
		mes "De~ sert~ Blacksmith~";
		mes "Aragham Ssaleh~ who can refine anything Ssaleh~";
		mes "Let me see what you'd like to refine~";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Aragham",0;
		end;
	}
}

payon,144,173,5	script	安東尼奧	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[安東尼奧]";
		mes "Stop nagging and get me the equipment you want to refine.";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Antonio",0;
		end;
	}
}

alberta_in,28,58,0	script	弗雷德利	4_M_03,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[弗雷德利]";
		mes "I am a blacksmith who can refine your weapon or equipment.";
		mes "Do you have anything that you'd like to refine?";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Fredrik",0;
		end;
	}
}

yuno_in01,171,21,4	script	尤思特	4_M_ORIENT01,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[尤思特]";
		mes "Wel...come... This is... the best... smithy... in Juno.";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","Lambert",0;
		end;
	}
}

ein_in01,24,87,5	script	曼塔斯曼	4_M_DWARF,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[曼塔斯曼]";
		mes "Hahaha, you already knew it. So you'd like to refine something?";
		mes "Let's try something risky today!";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","曼塔斯曼",0;
		end;
	}
}

lhz_in02,282,20,7	script	福樂	4_M_LGTMAN,{
	if( getbattleflag( "feature.refineui" ) ){
		mes "[福樂]";
		mes "Hehe... You wanna refine?";
		mes "Hehe.. Consider it done..";
		mes "Hehe....";
		close2;
		refineui();
		end;
	}else{
		callfunc "refinemain","福樂",0;
		end;
	}
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine.yml as well.
//============================================================
function	script	refinemain	{
	.@npc_name$ = getarg(0);
	.@features = getarg(1);

	if( getbattleflag( "feature.refineui" ) == 3 ){
		mes "["+ .@npc_name$ +"]";
		mes "I've spent a long time refining powerful weapons.";
		mes "You are in the presence of ???? [Zhang Zhuangtan]"; // TODO: find a valid translation
		mes "Do you want to strengthen your equipment?";
		close2;
		refineui();
		end;
	}

	disable_items;
	mes "["+ .@npc_name$ +"]";
	mes "I'm the Armsmith.";
	mes "I can refine all kinds of weapons, armor and equipment, so let me";
	mes "know what you want me to refine.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		else
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[沒有裝備]";
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[" + .@npc_name$ + "]";
		mes "你好歹倒是把裝備穿上先...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "[" + .@npc_name$ + "]";
		mes "要不要我把你的頭腦精煉得";
		mes "聰明一點啊?";
		emotion ET_FRET;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "[" + .@npc_name$ + "]";
		mes "你選中的這個裝備是無法精煉的道具喔!";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "[" + .@npc_name$ + "]";
		mes "這件裝備已經鍛治到10級了.";
		mes "以我的能力, 無法再鍛造了..";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);
	.@itemtype = getiteminfo( .@refineitemid, ITEMINFO_TYPE );

	if( .@itemtype == IT_ARMOR ){
		.@equip_lv = getequiparmorlv( .@part );

		switch( .@equip_lv ){
			case 1:
				.@safe = 4;
				break;
			default:
				// TODO:
				close;
		}

		// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
		if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
			switch( .@equip_lv ){
				case 1:
					.@price = .@price * 10;
					break;
				default:
					// TODO:
					close;
			}
		}
	}else if( .@itemtype == IT_WEAPON ){
		.@equip_lv = getequipweaponlv( .@part );

		switch( .@equip_lv ){
			case 1:
				.@safe = 7;
				break;
			case 2:
				.@safe = 6;
				break;
			case 3:
				.@safe = 5;
				break;
			case 4:
				.@safe = 4;
				break;
			default:
				// TODO:
				close;
		}

		// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
		if( VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE) ){
			switch( .@equip_lv ){
				case 1:
					.@price = .@price * 40;
					break;
				case 2:
					.@price = .@price * 50;
					break;
				case 3:
					.@price = .@price * 2;
					break;
				case 4:
					.@price = .@price * 2;
					break;
				default:
					// TODO:
					close;
			}
		}
	}else{
		.@safe = 4;
	}

	// TODO: What was this? Maybe shadow?
	// case 5: .@price = .@price * 10; break;

	if(.@features != 1) {
		mes "[" + .@npc_name$ + "]";
		mes "精練這個物品我需要:";
		mes "- ^0000ff"+getitemname(.@material)+"^000000 x 1";
		mes "- 精煉服務費 ^ff0000"+ .@price + "^000000 Zeny";
		mes "你真的希望我繼續嗎?";
		next;
		if(select("是的, 繼續吧:還是算了吧...") == 2){
			mes "[" + .@npc_name$ + "]";
			mes "耶...";
			mes "那好吧, 歡迎下次再來!";
			close;
		}
		if(getequippercentrefinery(.@part, REFINE_COST_NORMAL) < 100) {
			mes "[" + .@npc_name$ + "]";
			mes "那我要繼續精煉它了哦?";
			mes "這真是一個有難度的挑戰啊啊啊!";
			switch(.@material) {
			case 985:
				mes "^ff0000看來這裝備已經精煉很多次了的樣子...";
				mes "要是再繼續精煉的話,";
				mes "我怕搞不好可能會造成裝備損坏喔!!!^000000";
				break;
			default:
				mes "^ff0000要是損坏的話可就不能繼續使用了!!";
				mes "插在里面的卡片和附魔也^0000ff全部消失^ff0000!!";
				mes "裝備也會消失掉哦, 你确定要繼續嗎?";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "我要再說一次, 一旦裝備損坏了,";	
			mes "那么它就永遠消失, 不能再使用了.";
			mes "也就是說, 你面臨著一個選擇";
			mes "^FF0000繼續的話, 可能會永遠失去這個裝備^000000 .";
			mes "你真的确定要繼續嗎?";
			next;
			if(select("繼續吧, 想變厲害總要承擔風險的!:我傷不起, 算了...") == 2){
				mes "[" + .@npc_name$ + "]";
				mes "我完全理解你放棄的原因...";
				mes "哪怕我就是一個好的精煉師,";
				mes "但也會有偶爾出錯的時候...";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[" + .@npc_name$ + "]";
			mes "你沒有足夠的金錢或者 ^0000ff"+getitemname(.@material)+"^000000...";
			mes "回去拿一些來, 我會在這里等你的.";
			close;
		}
		Zeny = Zeny-.@price;
		delitem .@material,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
			mes "["+ .@npc_name$ +"]";
			emotion ET_FRET;
			mes "等一會儿..";
			mes "你以為我是笨蛋嗎?!";
			mes "快出去, 不然我會用我的鐵錘打你..";
			close;
		}

		if(getequippercentrefinery(.@part, REFINE_COST_NORMAL) <= rand(100)) {
			failedrefitem .@part;
			mes "["+ .@npc_name$ +"]";
			emotion (!rand(5))?ET_MONEY:ET_HUK;
			.@lose = rand(1,3);
			if (.@lose == 1) {
				mes "哦! 我的天!! 它真的損坏了...";
				mes "對此我真的十分非常抱歉,";
				mes "不過多多練習對我有好處不是么?";
			} else if(.@lose == 2) {
				mes "咳咳咳咳咳咳咳!!!";
				mes "裝備在精煉的過程中損坏了.";
				mes "對...對不起...";
			} else {
				mes "砰! 啊啊啊啊~~ ";
				mes "裝備在精煉的過程中損坏了.";
				mes "很抱歉, 真的十分抱歉...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion ET_SMILE;
		.@win = rand(1,3);
		if (.@win == 1) {
			mes "太棒了! 哈哈!";
			mes "要不要再來一次呀~~";
			mes "好久沒精煉出這么优質的裝備了,";
			mes "裝備變強了你一定也很高興吧?";
		} else if(.@win == 2) {
			mes "非常好, 成功了..!";
			mes "這是你的裝備, 拿去吧!";
			mes "好久沒精煉出這么优質的裝備了.";
		} else {
			mes "哈哈! 我完成了.";
			mes "不要怀疑我的工作能力.";
			mes "肯定可以保証你滿意.";
			mes "裝備變強了你一定也很高興吧?";
		}
		close;
	}

// New Refining Functions ========================
	if (.@refinerycnt < .@safe) {
		mes "[" + .@npc_name$ + "]";
		mes "你可以選擇直接鍛冶到安全值，或是要鍛冶的次數... 我都沒意見......";
		next;
		.@menu2 = select("鍛冶到安全值.","讓我來指定鍛冶的次數.","我改變主意了...");
	} else
		.@menu2 = 2;
	switch(.@menu2){
	case 1: 
		.@refinecnt = .@safe - .@refinerycnt;
		break;
	case 2:
		next;
		mes "[" + .@npc_name$ + "]";
		mes "那么，你要我幫你鍛冶多少次呢?";
		next;
		input .@refinecnt;
		.@refinecheck = .@refinecnt + .@refinerycnt;
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + .@npc_name$ + "]";
			mes "我不能再精練這個物品啦.";
			close;
		}
		if(.@refinecheck > .@safe) {
			.@refinecheck = .@refinecheck - .@safe;
			mes "[" + .@npc_name$ + "]";
			mes "那么，我要開始鍛冶  + .@refinecheck +  次，這將超過安定值. 你的裝備有可能會坏掉... 要鍛冶嗎?";
			next;
			if(select("好的...","算了...") == 2){
				mes "[" + .@npc_name$ + "]";
				mes "這可是你說的哦...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[" + .@npc_name$ + "]";
		mes "這可是你說的哦...";
		close;
	}
	.@fullprice = .@price * .@refinecnt;
	mes "[" + .@npc_name$ + "]";
	mes "總共需要" + .@refinecnt + " " + getitemname(.@material) + " 以及費用 " + .@fullprice + " Zeny... 要鍛冶嗎?";
	next;
	if(select("好的","算了...") == 2){
		mes "[" + .@npc_name$ + "]";
		mes "這可是你說的哦...";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + .@npc_name$ + "]";
		mes "這些就是你所擁有的所有物品嗎?雖然很可惜，但是你的材料不足，我也實在愛莫能助，再說，我付出也應該得要有所收獲嘛!";
		close;
	}
	Zeny = Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + .@npc_name$ + "]";
			mes "對不起,材料不夠我也不能幫你.....";
			close;
		}
		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
				callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || (.@menu2 == 1 && getequippercentrefinery(.@part, REFINE_COST_NORMAL) < 100)) {
			mes "[" + .@npc_name$ + "]";
			mes "不要開玩笑了,你以為我是笨蛋嗎 !! !?!";
			mes "快出去...";
			mes "不然我會用我的鐵錘打你..!!!";
			close;
		} 
		mes "　　, 　　!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part, REFINE_COST_NORMAL) <= rand(100)) {
			failedrefitem .@part;
			emotion ET_HUK;
			mes "[" + .@npc_name$ + "]";
			mes "咳咳咳咳咳咳咳!!! 武器在精煉的過程中損坏了...";
			.@refinecnt = .@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "這是剩下的錢和材料...";
			getitem .@material,.@refinecnt;
			.@fullprice = .@refinecnt * .@price;
			Zeny = Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion ET_BEST;
		.@refinecnt = .@refinecnt - 1;
		.@refinerycnt = getequiprefinerycnt(.@part);
		next;
	}
	mes "["+ .@npc_name$ +"]";
	mes "所有的都完成了，歡迎下次再來.";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	普勒越	86,{
	callfunc "phramain","普勒越";
	end;
}
payon,145,178,3	script	伯克納德	88,{
	callfunc "phramain","伯克納德";
	end;
}
morocc_in,63,32,6	script	沙德	99,{
	callfunc "phramain","沙德";
	end;
}
alberta_in,13,71,3	script	卡拉曼莉斯	86,{
	callfunc "phramain","卡拉曼莉斯";
	end;
}
yuno_in01,171,27,4	script	德雷曼	88,{
	callfunc "phramain","德雷曼";
	end;
}
ein_in01,15,87,3	script	迪樂意哈斯	86,{
	callfunc "phramain","迪樂意哈斯";
	end;
}
lhz_in02,278,24,3	script	客勒柯	86,{
	callfunc "phramain","客勒柯";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "你身上帶著太多种東西了,";
		mes "去整理一下后再來找我吧!";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "[" + .@npc_name$ + "]";
	mes "你好, 冒險家!";
	mes "我這里有賣一些鐵或是鋼鐵制物品在精煉時所需要的金屬.";
	next;
	mes "[" + .@npc_name$ + "]";
	mes "目前販賣的東西有:";
	mes "精煉等級1武器的^d78400強化武器金屬-級數一^000000.";
	mes "鍛冶等級2武器的^007777強化武器金屬-級數二^000000.";
	mes "請問你想要購買哪种?";
	next;
	switch(select("強化武器金屬-級數一 - ^f56925200 金幣^000000:強化武器金屬-級數二 - ^f569251000 金幣^000000:詢問其他材料")) {
	case 1:
		.@material = 1010;
		.@price = 200;
		break;
	case 2:
		.@material = 1011;
		.@price = 1000;
		break;
	case 3:
		mes "[" + .@npc_name$ + "]";
		mes "其他材料?";
		mes "你需要精煉高等級的武器?";
		mes "我想鋁和神之金屬是很難找到的啦...";
		close;
	}
	mes "[" + .@npc_name$ + "]";
	mes "你想要購買多少個?";
	mes "如果你不想購買, 請輸入數字 0 即可.";
	next;
	while(1) {
		input .@input;
		if (.@input <= 0) {
			mes "[" + .@npc_name$ + "]";
			mes "好的, 本次交易, 已經結束.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "[" + .@npc_name$ + "]";
			mes "好的, 咦....?";
			mes "你不可以一次購買超過 500 個, OK?";
			next;
		}
		else {
			break;
		}
	}
	.@sell = .@input * .@price;
	if (Zeny < .@sell) {
		mes "[" + .@npc_name$ + "]";
		mes "實在對不起哦.";
		mes "你現在的金幣不足以購買這些東西啊.";
		mes "這里可不許賒帳的.";
		mes ""+ .@input +" of them.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "[" + .@npc_name$ + "]";
		mes "很抱歉!";
		mes "您的負重無法購買那么多東西.";
		mes "請整理一下背包后再來吧.";
		close;
	}
	getitem .@material,.@input;
	Zeny = Zeny-.@sell;
	mes "[" + .@npc_name$ + "]";
	mes "好　, 這是你要的東西, 給你!";
	mes "非常感謝你的光顧.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	帝特立西	84,{
	callfunc "orimain","帝特立西";
	end;
}
payon,137,178,5	script	夏金	88,{
	callfunc "orimain","夏金";
	end;
}
morocc_in,72,32,6	script	亞度拉	99,{
	callfunc "orimain","亞度拉";
	end;
}
alberta_in,21,63,5	script	智諾普	84,{
	callfunc "orimain","智諾普";
	end;
}
yuno_in01,164,27,4	script	德雷伊	88,{
	callfunc "orimain","德雷伊";
	end;
}
ein_in01,18,82,6	script	馬帝修塔音	84,{
	callfunc "orimain","馬帝修塔音";
	end;
}
lhz_in02,281,24,5	script	葡魯依	84,{
	callfunc "orimain","葡魯依";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "你身上帶著太多种東西了,";
		mes "去整理一下后再來找我吧!";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "[" + .@npc_name$ + "]";
	mes "你好, 冒險家!";
	mes "只要你將^FF0000神之金屬原石^000000或^FF0000鋁原石^000000拿來.";
	mes "我馬上就可以為你提煉^0000ff神之金屬^000000和^0000ff鋁^000000.";
	mes "但是記住, 必須要有 5 個原石，";
	mes "我才有辦法幫你提煉.";
	next;
	switch(select("我想要制作^0000ff 神之金屬 ^000000!:請幫我提煉出一塊^ff0000 鋁 ^000000!:詢問關于屬性礦石...")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "[" + .@npc_name$ + "]";
			mes "這是我歷盡千辛提煉出來的神之金屬.";
			mes "歡迎你下次再來.";
			close;
		}
		else {
			mes "[" + .@npc_name$ + "]";
			mes "哈哈哈...你真愛開玩笑.";
			mes "我不是跟你說過, 要是沒有5個神之金屬原石的話, 是無法制作神之金屬的嗎?";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "[" + .@npc_name$ + "]";
			mes "這是我歷盡千辛提煉出來的鋁.";
			mes "歡迎你下次再來。";
			close;
		}
		else {
			mes "[" + .@npc_name$ + "]";
			mes "哈哈哈...你真愛開玩笑.";
			mes "我不是跟你說過, 要是沒有5個鋁原石的話, 是無法制作鋁的嗎?";
			close;
		}
	case 3:
		mes "[" + .@npc_name$ + "]";
		mes "關于屬性礦石...";
		mes "我做鍛冶金屬這行已經做了二十年, 听過很多這類別的傳說...";
		mes "但還沒實際見過呢!";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "我听說金屬都各自擁有一些類似";
		mes "水、地、火、風等屬性.";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "在鍛冶武器時加入屬性時的話...";
		mes "所制作出來的武器, 將擁有那屬性礦石的超強力量.";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "哈哈..不過話說回來, ";
		mes "能做出這种武器的人想必一定擁有极高的鍛冶技術.";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	修理工#alb	86,{
	callfunc "repairmain","修理工";
	end;
}

moc_ruins,107,94,4	script	修理工#moc	99,{
	callfunc "repairmain","修理工";
	end;
}

payon,143,165,4	script	修理工#pay	88,{
	callfunc "repairmain","修理工";
	end;
}

prt_in,63,54,2	script	修理工#prt	86,{
	callfunc "repairmain","修理工";
	end;
}

yuno_in01,175,28,3	script	修理工#juno	86,{
	callfunc "repairmain","修理工";
	end;
}

geffen_in,34,166,3	script	修理工#gef	99,{
	callfunc "repairmain","修理工";
	end;
}

aldeba_in,38,60,3	script	修理工#alde	86,{
	callfunc "repairmain","修理工";
	end;
}

lhz_in02,284,14,3	script	修理工#lhz	86,{
	callfunc "repairmain","修理工";
	end;
}

prt_gld,139,117,4	script	修理工#prt_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

gef_fild13,263,117,4	script	修理工#gef_fild	86,{
	callfunc "repairmain","修理工";
	end;
}

pay_gld,295,183,4	script	修理工#pay_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

alde_gld,220,152,4	script	修理工#alde_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

aru_gld,189,336,4	script	修理工#aru_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

sch_gld,340,80,7	script	修理工#sch_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	.@repairprice = 5000;
	.@npc_name$ = getarg(0);
	mes "["+.@npc_name$+"]";
	mes "你好, 冒險家!";
	mes "你想讓我修理東西嗎?";
	next;
	mes "["+.@npc_name$+"]";
	mes "我可以幫你修理坏掉的裝備及武器!";
	next;
	switch(select("恩...有東西修...:目前沒有要修理的東西...")) {
	case 1:
		.@checkitem = 1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			.@checkitem = .@checkitem+1;
		}
		.@checkitem = .@checkitem-1;
		if (!.@checkitem) {
			mes "["+.@npc_name$+"]";
			mes "噢! 這真是難以置信.";
			mes "你肯定非常關心你的物品! 沒有任何損坏哦~";
			next;
			mes "["+.@npc_name$+"]";
			mes "如果每個人都像你這樣愛護物品的話, 我就會要失業了!";
			close;
		}
		mes "["+.@npc_name$+"]";
		mes "呃... 讓我看看...";
		mes "你一共有 ^ff0000" + .@checkitem + "^000000 件物品損坏了.";
		mes "想修理他們嗎?";
		next;
		.@totalcost = .@repairprice*.@checkitem;
		mes "["+.@npc_name$+"]";
		mes "修理一件需要 " + .@repairprice + " 金幣.";
		mes "想修好全部物品需要 " + .@totalcost + " 金幣!";
		mes "你想修理他們嗎?";
		next;
		switch(select("是的, 幫我把它們都修理了吧.:算了, 暫時不修理了...")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+.@npc_name$+"]";
				mes "呃呃..";
				mes "你的金錢不足以支付維修費用啊.";
				mes "湊夠錢再來找我吧.";
				close;
			}
			.@checkitem2 = 1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				.@checkitem2 = .@checkitem2+1;
			}
			.@checkitem2 = .@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				Zeny = Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					.@checkitem = .@checkitem-1;
				}
				mes "["+.@npc_name$+"]";
				mes "Ok! 全都幫你修好了.";
				close;
			}
			else {
				mes "["+.@npc_name$+"]";
				mes "看來你并沒有任何坏掉的東西需要修理吶...";
				close;
			}
		case 2:
			mes "["+.@npc_name$+"]";
			mes "收費真的不算貴,";
			mes "客官你別走啊...";
			mes "我能幫你修理坏掉的裝備及武器!";
			close;
		}
	case 2:
		mes "["+.@npc_name$+"]";
		mes "哦, 好的.";
		mes "等你需要修理物品的話,";
		mes "可以來找我.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in Morocc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morocc repairman to Morocc Ruins. [L0ne_W0lf]
//============================================================
