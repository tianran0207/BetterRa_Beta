//===== rAthena Script ======================================= 
//= Renters
//===== By: ================================================== 
//= kobra_k88, mod by Lupus
//===== Current Version: ===================================== 
//= 2.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Knight and Crusader Peco Peco Breeders, Falcon Breeder scripts
//===== Additional Comments: ================================= 
//= Fully working
//= Added another Falcon Master into the Hunters Guild [Lupus]
//= 1.2: replaced checkoption(x) with checkriding,checkfalcon [Lupus]
//= 1.3: Added support Adv Classes + Baby Class [Lupus]
//= 1.4: Added different prices for normal, advanced and baby classes
//= but you could set them to the same [Lupus]
//= 1.5: Fixed spelling mistakes [Nexon]
//= 1.5a: Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//= 1.6: Moved the Falcon Master to Hugel [Poki#3]
//= 1.7 Updated to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.8 Added 3rd Job creature NPCs (Dragon/Gryphon) [L0ne_W0lf]
//= 1.9 Enabled Gryphon Renter NPC and added Mado Gear NPC. [Masao]
//= 2.0 Fixed problems with third classes and new mounts. [Euphy]
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Added warg checks. [Euphy]
//============================================================ 

// PecoPeco Breeder (for Knights) :: breeder
//============================================================
prontera,55,350,5	script	大嘴鳥飼養員(騎士)#knt	105,{
	if(Upper==0) set .@price,2500; //Normal Peco - default price
	if(Upper==1) set .@price,2500; //Armored Peco
	if(Upper==2) set .@price,2500; //Baby Peco

	mes "[大嘴鳥飼養員]";
	if (BaseJob == Job_Knight && Class < Job_Rune_Knight) {
		mes "你好, 榮耀的騎士呀, 歡迎光臨.";
		mes "你想要租用一只可愛的大嘴鳥嗎?";
		mes "租賃費用是 ^FF0000"+.@price+"^000000 金幣.";
		next;
		switch(select("是的, 我想租一只大嘴鳥:取消")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鳥飼養員]";
				mes "可是你沒有足夠的金錢喲.";
				mes "難道...你破產了?";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鳥飼養員]";
				mes "很抱歉, 你無法使用本服務, 因為你根本控制不了大嘴鳥.";
				mes "請你先學習技能列表中的^0000FF騎乘術^000000, 然后再來找我吧.";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鳥飼養員]";
				mes "你已經租用了一只可愛的大嘴鳥了喲.";
				mes "暫時不能租第二只啦 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鳥飼養員]";
				mes "喲, 你還乘坐著商城坐騎呢! 請先從商城坐騎身上下來吧, 不然你租了大嘴鳥也沒用的呀.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			mes "[大嘴鳥飼養員]";
			mes "好滴, 這是你的大嘴鳥, 請善待它喲.";
			close;
		case 2:
			mes "[大嘴鳥飼養員]";
			mes "噢..我明白了.";
			mes "祝你有個好心情, 再見啦.";
			close;
		}
	} else {
		mes "你好, 噢...? 很抱歉哈.";
		mes "大嘴鳥只能租給^0000FF騎士^000000或^0000FF騎士領主^000000.";
		close;
	}
}

// Grand PecoPeco Breeder (for Crusaders)
//============================================================
prontera,232,318,3	script	大嘴鳥飼養員(十字軍)#cru	105,{
	if(Upper==0) set .@price,3500; //Normal Peco - default price
	if(Upper==1) set .@price,3500; //Armored Peco
	if(Upper==2) set .@price,3500; //Baby Peco

	mes "[大嘴鳥飼養員]";
	if (BaseJob == Job_Crusader && Class < Job_Rune_Knight) {
		if(Upper != 1 ) mes "歡迎光臨, 十字軍.";
		else mes "歡迎光臨, 圣殿十字軍.";
		mes "我們為你准備了特殊的大嘴鳥.";
		mes "租用一只需要 ^FF0000"+.@price+"^000000 金幣.";
		next;
		switch(select("太好了, 租給我一只大嘴鳥吧:取消")) {
		case 1:
			if (Zeny < .@price) {
				mes "[大嘴鳥飼養員]";
				mes "你沒有足夠的金錢來支付租金呢.";
				mes "如果你想要租一只可愛的大嘴鳥的話, 請帶來 ^FF0000"+.@price+"^000000 金幣...";
				close;
			}
			else if(getskilllv("KN_RIDING") == 0) {
				mes "[大嘴鳥飼養員]";
				mes "你必須先學會^0000FF騎乘術^000000, 然后我才能租給你大嘴鳥.";
				close;
			}
			else if(checkriding()) {
				mes "[大嘴鳥飼養員]";
				mes "你已經租用了一只可愛的大嘴鳥了喲.";
				mes "暫時不能租第二只啦 ^-^";
				close;
			}
			else if(ismounting()) {
				mes "[大嘴鳥飼養員]";
				mes "喲, 你還乘坐著商城坐騎呢! 請先從商城坐騎身上下來吧, 不然你租了大嘴鳥也沒用的呀.";
				close;
			}
			set Zeny, Zeny-.@price;
			setriding;
			mes "[大嘴鳥飼養員]";
			mes "好滴, 這是你的大嘴鳥, 請善待它喲.";
			close;
		case 2:
			mes "[大嘴鳥飼養員]";
			mes "那么...回見 ^-^";
			close;
		}
	} else {
		mes "我能為您做些什么呢?";
		mes "大嘴鳥的租賃要求是十分嚴格的, 必須是^0000FF十字軍^000000或者^0000FF圣殿十字軍^000000才能租賃.";
		close;
	}
}

// Falcon Master :: breeder
//============================================================
hu_in01,381,304,5	script	獵鷹飼養員#hnt	105,{
	if(Upper==0) set .@price,2500; //Normal Falcon - default price
	if(Upper==1) set .@price,2500; //Scarf Falcon
	if(Upper==2) set .@price,2500; //Baby Falcon

	mes "[獵鷹飼養員]";
	if (BaseJob == Job_Hunter) {
		if (checkwug()) {
				mes "呃.. (☉o☉)...";
				mes "在解散你召喚的野狼之前,";
				mes "你不能租賃獵鷹!";
				mes "因為她們說不定會打起來.";
			close;
		}
		mes "你好, 冒險家, 想租賃獵鷹嗎?";
		mes "只需 "+.@price+" 金幣,";
		mes "就可以租到值得信賴的猛禽喲~";
		next;
		switch(select("太棒了, 我剛好想租一只:取消")) {
		case 1:
			if (checkwug()) {
				mes "[獵鷹飼養員]";
				mes "呃.. (☉o☉)...";
				mes "在解散你召喚的野狼之前,";
				mes "你不能租賃獵鷹!";
				mes "因為她們說不定會打起來.";
				close;
			}
			if (Zeny < .@price) {
				mes "[獵鷹飼養員]";
				mes "這是什么情況?";
				mes "你沒有足夠的錢哇?!";
				mes "赶快開始狩獵魔物賺錢吧~";
				close;
			}
			else if(getskilllv("HT_FALCON") == 0) {
				mes "[獵鷹飼養員]";
				mes "我的天哪~";
				mes "你必須先學會^0000ff馴鷹術^000000才行!";
				mes "如果你控制不了獵鷹,";
				mes "我是不放心把她們租給你的,";
				mes "明白我的意思了吧?";
				close;
			}
			else if(checkfalcon()) {
				mes "[獵鷹飼養員]";
				mes "呃.. (☉o☉)...";
				mes "你已經有一只獵鷹了.";
				mes "往你的頭上看,";
				mes "她在你的頭上盤旋飛翔喲...";
				close;
			}
			set Zeny, Zeny-.@price;
			setfalcon;
			mes "[獵鷹飼養員]";
			mes "好的, 這是你的獵鷹, 請善待它喲.";
			close;
		case 2:
			mes "[獵鷹飼養員]";
			mes "等、等一下, 你想去哪?";
			mes "我用人格擔保, 這些都是一流的獵鷹!";
			mes "來吧, 喲~";
			close;
		}
	} else {
		mes "年輕的傻瓜!";
		mes "獵鷹只有獵人、游俠系列職業能使用,";
		mes "明白了嗎? ...嘿嘿, 很羡慕吧?";
		close;
	}
}
