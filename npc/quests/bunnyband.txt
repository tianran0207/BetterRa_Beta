//===== rAthena Script ======================================= 
//= Bunny Band Quest
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: ===================================== 
//= 1.6
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Quest to obtain a Bunny Band headgear.
//===== Additional Comments: ================================= 
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//	Changed @BUNYBAN back to BUNYBND, it should be this way.
//	Removed duplicates. NPC is ALberta-only.
//= 1.6 Cleaning. [Euphy]
//= 2016.03.03 英文直譯漢化文字部分 [楊梅]
//============================================================ 

alberta,26,229,0	script	卡普拉服務人員#bunny	83,{
	if (BUNYBND == 1) {
		mes "[卡普拉服務人員]";
		mes "Hello 你好啊~!";
		mes "看來你已經參加了";
		mes "^529DFF幫助顧客將拿來的物品加工制作^FF0000'兔耳發圈'^529DFF的特別活動^000000 了呢";
		next;
		switch(select("沒錯，我已經拿來了需要的材料:活動信息:取消")) {
		case 1:
			mes "[卡普拉服務人員]";
			mes "現在開始檢查客人拿來的物品";
			next;
			setarray .@Items[0],949,100,706,1,722,1,2213,1;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
					mes "[卡普拉服務人員]";
		            mes "至少也要從:";
					mes "^FF0000"+callfunc("F_InsertPlural2",.@Items[.@i+1],getitemname(.@Items[.@i]),0 ,"%s x %d")+"^000000";
		            mes "開始收集吧";
					close;
				}
			mes "[卡普拉服務人員]";
			mes "哇!!太好了，太好了你把需要的材料都帶來了!!";
			mes "卡普拉服務人員將為你制作兔耳發圈";
			mes "請稍等一下";
			next;
			for(set .@i,0; .@i<8; set .@i,.@i+2)
				if (countitem(.@Items[.@i]) < .@Items[.@i+1]) {
					mes "[卡普拉服務人員]";
					mes "很抱歉,";
		            		mes "至少也要從:";
					mes "^FF0000"+callfunc("F_InsertPlural2",.@Items[.@i+1],getitemname(.@Items[.@i]), 0, "%s x %d")+"^000000";
		            		mes "開始收集，";
					mes "這樣我才能幫你制作...";
					close;
				}
			delitem 949,100; //Feather柔毛
			delitem 706,1; //Four_Leaf_Clover幸運四葉草
			delitem 722,1; //Scarlet_Jewel珍珠
			delitem 2213,1; //Cat_Hairband貓耳發圈
			mes "[卡普拉服務人員]";
			mes "^FF0000兔耳發圈^000000制作完成...!";
			getitem 2214,1; //Bunny_Band
			set BUNYBND,0;
			next;
			mes "[卡普拉服務人員]";
			mes "我們非常感謝您參加本活動，";
			mes "卡普拉將永遠為您服務";
			close;
		case 2:
			goto L_Info;
		case 3:
			close;
		}
	}
	mes "[卡普拉服務人員]";
	mes "你希望參加卡普拉与商人公會一同舉辦的";
	mes "^529DFF幫助顧客將拿來的物品加工制作^FF0000'兔耳發圈'^529DFF的特別活動^000000 嗎﹖";
	next;
	switch(select("參加活動:活動信息:沒興趣")) {
	case 1:
		set BUNYBND,1;
		mes "[卡普拉服務人員]";
		mes "感謝您們參加這次的特別活動!";
		mes "請將所需要的物品搜集齊全后在過來這里就可以了";
		next;
		mes "[卡普拉服務人員]";
		mes "制作兔耳發圈所需要的物品如下:";
		mes "柔毛 100 根";
		mes "四葉幸運草 1 個";
		mes "珍珠 1 顆";
		mes "貓耳發圈 1 個";
		close;
	case 2:
		break;
	case 3:
		close;
	}
L_Info:
	mes "[卡普拉服務人員]";
	mes "感謝您們參加這次的特別活動!";
	mes "請將所需要的物品搜集齊全后在過來這里就可以了";
	next;
	mes "[卡普拉服務人員]";
	mes "制作兔耳發圈所需要的物品如下:";
	mes "柔毛 100 根";
	mes "四葉幸運草 1 個";
	mes "珍珠 1 顆";
	mes "貓耳發圈 1 個";
	next;
	mes "[卡普拉服務人員]";
	mes "以上這些物品全部收集好了在過來這里就可以了";
	mes "我會幫你制作兔耳發圈.";
	mes "特別感謝本次活動的贊助方:";
	mes "(艾爾貝塔商人協會)";
	close;
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.1 using duplicate command
//= 1.2 Fixed NPC location, removed NPC dupes [Lupus]
//= 1.3 RE-Fixed exploit V_V, also cleared used VAR [Lupus]
//= 1.4 replaced BUNYBND with @BUNYBND [Lupus]
//============================================================ 
